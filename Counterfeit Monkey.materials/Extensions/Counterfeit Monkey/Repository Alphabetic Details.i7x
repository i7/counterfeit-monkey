Repository Alphabetic Details by Counterfeit Monkey begins here.

Use authorial modesty.


Chapter 2 - Content Details By Alphabet

[Items in the repository have already been defined, above, but they lack definitions and specialized behavior. This chapter provides those things, in alphabetical order for lack of other organizational principle.]

Section 1 - A

The heft of an aa is 3. The indefinite article of an aa is "some". The description of the aa is "A chunk of rough black lava, scratchy and porous: the kind of rock made when a volcano fountains into the air, not like the ropy smoother sort formed when lava flows in rivers."

Some aas-object are a plural-named thing. The heft of the aas-object is 5. The description of the aas-object is "A large heap of black lava, mounded up as though by a recent eruption." The printed name of the aas-object is "aas". Understand "aas" as the aas-object.

Instead of searching the aas-object:
	say "There are a surprising number of sharp bits: this is friable stone, but it hasn't yet been eroded into smoothness."


The description of the ad is "A folded, rumpled newspaper ad for [ad subject]." The ad has some text called the ad subject. The heft of the ad is 1.

Before examining the ad for the first time:
	if the restoration gel is not seen:
		now the ad subject of the ad is "Restoration Gel: a unique patented formula to restore lost letters and undo the effects of a letter-remover";
	otherwise:
		now the ad subject is "[one of]fixed-price dinners at Literal[or]an exhibit on the pre-history of linguistic concepts at the local museum, especially highlighting their possession of an amazing classic codex[or]seaweed wrap treatments at the Fleur d'Or spa[sticky random]";

The adage is an r-abstract thing. The heft of the adage is 0. The description of the adage is "It reads: [one of]Don't make a god of gold[or]The value of a thing is what it may become[or]From little acorns, much corn may be harvested[or]Every stick has three ends[or]Good fences make good fertil[ize]r[or]Even players have layers[or]If hoses were horses, gardeners would rule the racetrack[or]The fanciest word is the least productive[or]Wordplay is stronger than swordplay[or]Judge a man by his verbs alone[at random]."


The heft of the Aga is 6. The description of the Aga is "It is a massive and somewhat frightening stove, made of cast iron and with a constant fire inside." Instead of touching the Aga: say "It is very hot."


An age is r-abstract. The heft of an age is 0. The description is "I was expecting something about the elderly and mortality: perhaps a grey beard or a cane or something like that.

But no, it has come out as the representation of an era, the golden age of Atlantis. Across the flickering surface [--] as though you had a documentary on fast-forward [--] play the sepia features of Jon Rosehip and Clarence Arbot, the discovery of linguistic efficacy, the foundation of the University. A deplural[ization] cannon fires, the effects discreetly off-screen. A tiny parade goes by. Phyllida Shaply gives a speech to a clamorous crowd. Confetti rains on Deep Street. And then the loop repeats."

The ahi is an edible thing. The indefinite article of an ahi is "some". The description of the ahi is "Sashimi-grade tuna, deep translucent pink except around the edges, where it has been seared and coated with blackened sesame seeds.". Understand "tuna" or "fish" as the ahi.


The heft of an air is 0. The indefinite article of an air is "some". The description of an air  is "It's hard to see." The scent-description of an air is "nothing".

Sanity-check doing something other than smelling with an air when the noun must be touched:
	say "[The noun] is not susceptible to [action name part of the current action]." instead.

Sanity-check doing something when the second noun is an air and the second noun must be touched:
	if the noun is the restoration gel:
		make no decision;
	say "[The second noun] can't be used that way." instead.


Test air with "purloin air / x air / wear monocle / x air / touch air / smell air / drop air / listen to air / taste air / tie sky to air" holding the monocle.

Every turn when an air (called target air) is enclosed by location:
	move the target air to the repository;
	say "The air becomes indistinguishable from all the other air around us."

An ale is usually edible, contained, and fluid. The description is "A delicious nutbrown ale in a cool glass." The indefinite article is "some". Understand "pool" or "puddle" as the ale. The heft of the ale is usually 3.
	The flavor-description is "I find it pleasant; you apparently prefer harder spirits or wine."

The description of the all is "It's everything. That simple." The all is r-abstract.

[The original Amy was a light Amy Winehouse reference, chosen by the expedient of typing 'Amy' into Google and seeing what the first match was. (Surely the most popular figure would be the one to be reified by linguistic tools.) When her namesake died suddenly, however, it felt like that was in bad taste, so I revised. This Amy is based on the actress Amy Acker, in particular her role as Winifred on Angel.

I also considered Amy Pond, but as I find her kind of annoying, I couldn't bring myself to do it.]

The description of an Amy is usually "She smiles at [us] when we look at her. She has an air of confused good will, as though she means well but isn't quite paying attention to this plane of reality."
	The greeting of an Amy is usually "'Oh, hello,' says Amy. She blinks at [us] a few times in surprise and then seems to forget again that we're here."

The description of the anagram bullet is "Just one by itself. It can't be loaded like this, but it still manages to look vaguely sinister. It's heavy for its size and has a pointed end."

The description of the anagram bullets is "Heavy, high-impact stuff. Anagramming requires a great deal of force to break the linguistic cohesion of the word or phrase being modified. On the other hand, it's the one process that is routinely effective even on adjective-adhering nouns."

The description of the angle bolt is "A bit of hardware with a 90-degree bend in the middle, and threading at one end to attach a hex nut."

The ant is an insect. The heft of the ant is 0. The description is "This particular ant is large, and fire-red, which makes it menacing, but fortunately harder to lose."

An applet is r-abstract. The heft is 0. The description is "An abstract representation. An anonymous browser window appears to hover in the air, [one of][if protest-scenario has happened and protest-scenario ended in DP-tanking]currently replaying a satellite footage of the deplural[ization] a few hours ago[otherwise]currently showing satellite footage of the big demonstration a few hours ago[end if][or]streaming a television show from one of Atlantis['] top chefs, demonstrating how to depluralize fruit for best effect. [one of]'Single varietal fruits produce the most intense flav[our],' she's explaining to the screen, a bushel of limes at her side[or]'A mixture of different varieties can produce a well-structured blend, as with a Bordeaux,' she continues[cycling][or]currently playing streaming video of a rally by the Atlantean Anti-Babel Party[or]streaming news clips of the Serial Comma Day celebration[or]streaming a public service message about keeping letter tools out of the hands of children[or]currently playing a web game about jumping cats[or]currently playing a text-based web game[as decreasingly likely outcomes]."

The ape is a neuter animal.
The initial appearance is "The ape, which is of the menacing kind rather than of the sweetly charming kind, has [us] fixed in an unpleasant stare."
The description is "It is very large."

The description of the arc is "A bent bow of steel, but more decorative than functional." The heft is 3.

The arcsin is r-abstract. The description of the arcsin is "It's a simple midair graph: a piece of a sine wave stood on end. There is something deeply pleasing about the structure despite its simplicity, as with almost all mathematical abstracts."

[The ark is a vehicle. The heft of the ark is 10. The description of the ark is "From here, the width of the bow in cubits is hard to guess. It just looks huge, much too huge for the little space it's crammed into…". The scent-description of the ark is "newly sawn planks".]

The arm is a long thing. The description is "A girl's arm, by the look of it, amputated neatly at the shoulder joint. The fingernails are painted shell-pink." Understand "shoulder" or "joint" or "fingernails" or "nails" or "nail" or "wrist" or "elbow" as the arm.

The armband is a thing. The description is "A black armband with the letter I behind a universal negation sign. It is the symbol of those who believe that no one should be executed by being turned inanimate, a protest against judicial procedures on the island. Wearing it around a policeman is likely to provoke a very unpleasant response." The armband is floppy and wearable and illegal. Understand "black" as the armband.

The armchair is a chair. The heft of the armchair is 5. The description is "Covered with red and orange flowered upholstery."

The army-coat is a floppy wearable thing. It covers the torso-area. Understand "army" or "coat" as the army-coat. The printed name is "army coat". The description of the army-coat is "It doesn't look like much: blotchy camouflage cloth in the greens and browns of the Atlantean landscape, with a stitched-on name patch. But it's heavy and warm." The name patch is part of the army-coat. The description of the name patch is "It reads ROSEHIP in blocky embroidery."
	The scent-description of the army-coat is "something musty and slightly chemical".

[The description of the arousing lorgnette is "It's a very old-fashioned set of spectacles on a stick, the sort an 18th-century lady might hold to her eyes to stare at someone across the room. This particular lorgnette is compellingly attractive."

Instead of wearing the arousing lorgnette:
	say "It can be held in front of the eyes on a stick, but not actually worn."]

The ascot is a floppy wearable thing. The heft of the ascot is 1. The description of the ascot is "One of those wide silk tie/cravat things that groomsmen wear at fancy morning weddings. It's grey and ready for use."
	The scent-description is "lilac and white musk cologne".

The alterna-letter is an r-abstract thing. The description of the alterna-letter is "A giant floating letter A." The printed name is "letter". Understand "letter" as the alterna-letter. The heft of the alterna-letter is 0.

[The heft of the arctic is 12. The description of the arctic is "Enormous, frozen plains graced only by the occasional caribou, polar bear, and oil rig."]

The description of the airfoil is "A big, awkward metal piece from something; it looks like a propellor blade." The heft of the airfoil is 6.

[The heft of the alp is 12. The description of the alp is "It looks quite a lot like the Eiger from this angle."]

The armpit is a thing. The description of the armpit is "It's a relatively inoffensive armpit as these things go, but, because detached from the rest of a body, it still looks like something a serial killer would leave around."
	The scent-description is "deod[our]ant".

The description of the art is "It's a very plausible copy of [one of]'Still Life with a Bunch of Rapes' by the great feminist Atlantean painter Annamarie Rosehip[or]'A Portrait of Whistler's Other' by Thomas Whistler Rosehip[or]'Still Life with Bead Loaf' by the surrealist painter Lewis Rosehip[or]'Flight of the Turtledoes' by the surrealist great Lewis Rosehip[or]the narrative painting 'If Wishes Were Horses, Bears Would Ride' by the great Atlantean socialist painter Lawrence Rosehip[or]Théophile Rosehip's surprisingly erotic classical work 'The Aft of the Medusa'[sticky random]."
	The indefinite article is "some".
	The scent-description of the art is "linseed oil".

The description of an as is "[one of]It appears to be an as, a [or]A [stopping]Roman coin of very low denomination[one of]. (Your knowledge, not mine. Should I ask how you know such things?)[or].[stopping] It is made of copper and has the letters S C stamped on one side." Understand "as/roman/coin/copper" as an as. The heft of an as is 1. The printed name of an as is "as". Understand "asses" as the plural of as.

Instead of searching or looking under an as:
    say "We inspect both obverse and reverse, but come away none the wiser."
Instead of buying an as:
    say "We're much too late for speculation in this particular currency."
Instead of touching or squeezing or rubbing an as:
    say "The metal surface is dull and weathered, skin-temperature, all smooth edges."

Rule for initially listing contents:
    initially group ases together.

Rule for grouping together an as (called target):
    say "[listing group size in words] asses";
    let source be the holder of the target;
    now every as held by the source is mentioned.

Does the player mean doing something to the as-coin when the subcommand of the as-coin matches "as":
    it is likely.

Does the player mean doing something to the as-coin when action requires a carried noun and the player does not carry the as-coin:
    it is likely.

Does the player mean taking the as-coin when the player carries the as-coin:
	it is unlikely.

Does the player mean doing something when the second noun is the as-coin and the subcommand of the as-coin matches "as":
    it is likely.

Does the player mean doing something when the second noun is the as-coin and action requires a carried second noun and the player does not carry the as-coin:
    it is unlikely.

The fake-as is an as.

The ass is a rideable animal. The ass is portable. The initial appearance is "Adorably miniature, as donkeys on this island tend to be: he comes up to roughly knee-height and could be carried like a dog, though his sentiments on the experience might not be flattering. He does still have a bite, and back legs."
	The description of the ass is "He is not the ideal companion in a mission of stealth."
	The heft is 3.

Instead of mounting the ass, say "The ass is much too small to ride."

Instead of listening to the ass:
	say "[one of]The ass lets out an experimental half-bray[or]The ass brays[or]'HEE-haw,' says the ass[at random]."

The assart is r-abstract. The description of the assart is "The assart appears, from the flickering images, to do with digging up or stealing plant growth, and then being punished for it[if the thicket is not in church garden]. Disconcertingly, our little adventure with turning the THICKET into a TICKET is included in the parade of views[end if]."


An atlantida-shellfish is a thing [in the Oracle Project]. The printed name is "atlantida". Understand "atlantida" or "shellfish" or "scallop" or "clam" or "shell" as atlantida-shellfish. The description is "It's a sort of shellfish, vaguely like a clam or scallop but bigger and with different ridges." The introduction is "I recognize it only because of a documentary program about the local fossil record."
	Instead of tasting or eating the atlantida-shellfish:
		say "It's long dead, and all the substance turned to stone."

Report taking the atlantida-shellfish:
	say "[We] pick up the atlantida. It's heavy as a rock, because it has been fossilized." instead.

Section 2 - B

A ba is usually r-abstract. The description of a ba is "It's a representation of the soul as seen in Egyptian religion: a bird with a tiny human head. It has black hair and blue eyes and a knowing smile.

Something about the face looks a little like me [--] at least, I see it that way. You might see something more like you."

The bait is a thing. The description is "A slightly muddy collection of worms, ready to be stuck on a hook." The scent-description is "damp earth". The indefinite article is "some".

The ballad is r-abstract and noisy.
	The description is "The ballad doesn't look like much, but it certainly does sing on and on."
	Instead of listening to the ballad: say "The ballad is [one of]singing out something about Lord Randall[or]going on about Tam Lin[or]making quite a fuss about a dead highwayman[or]warning of the dangers of flirting with demon lovers[at random]."

The description of the ballcock is "It is a mechanism involving a floater ball on a hinge: the sort of thing used to regulate the water flow inside the tank of a toilet[one of]. I've seen them dozens of times before without ever really thinking about what they were called[or][stopping]."

The baklava is an edible thing. The indefinite article is "some". The description is "Sweet and positively dripping with honey and nuts."
	The scent-description of the baklava is "pistachios".

The ban is r-abstract. The heft is 0. The description of the ban is "The abstract concept of ban has become a cartoonish picture of a depressed computer moping for its friends.

This was a famous political cartoon a year or two ago; it represents in small the Bureau's legislation against unauthor[ize]d access to parts of the internet outside our firewall. The artist hasn't produced any further cartoons."

The alterna-band is a person. The printed name of the alterna-band is "band". Understand "band" as the alterna-band. The heft of the alterna-band is 8. The description of the alterna-band is "The band consists of a thuggish drummer, two guitarists, and a girl vocalist with straight black hair and chunky glasses. They bear a certain resemblance to The Anagrammists, an Altantean alternative band with a brief heyday in the late 90s before the members mysteriously dispersed."

The bandage is a wearable floppy thing. The description of the bandage is "It's made of thick strips of white gauze and looks like it ought to be worn by a WWI veteran. Happily, it manifested without any blood or bodily fluids already in evidence." The heft of the bandage is 1.

The bandana is a floppy wearable thing. The description is "It's a souvenir: bright Atlantean blue cloth patterned with the letter f, and a patch in one corner with the letters FRG, hon[our]ing the members of the Fricatives Research Group." Understand "frg" or "patch" or "fricatives" or "research" or "group" as the bandana.
	The heft of the bandana is 1.

The description of the banding is "It's a small field research tag on a band, suitable for attaching to the leg of a bird whose migrations and population fluctuations are under study."

[ Add something for trying to put this on a bird?]

Sanity-check putting the banding on the roc:
	say "The roc's leg is comically too large for the banding to fit." instead.

Sanity-check putting the banding on a bird:
	say "[The second noun] [hop] away disobligingly. There must be some ornithologist's trick to this, but I don't know it." instead.

Sanity-check wearing the banding:
	say "I suppose we could be said to be migratory, but surely the point is to avoid being tracked?" instead.

Sanity-check poking head through banding:
	say "Our head has, fortunately, a greater diameter than the leg of a sparrow." instead.

The bandit is a man. The description is "A lean and hungry gentleman, from the island's disreputable past. For a little while in the late 17th century Atlantis was something of a haven for criminals too lawless for the mainland." Understand "lean" or "hungry" or "thief" or "gentleman" as the bandit.
	The greeting of the bandit is "'Yarr,' says the bandit, with exhalations of onion."

The bantam is a bird. The description is "Inasmuch as a chicken can be considered adorable, this is an adorable chicken: half normal size, and with big fluffy white feathers."

The bar is a thing. The description is "Imagine a stereotypical prison cell in a stereotypical movie. Now imagine the round metal bars in the window of that stereotypical cell. This is one of those bars. It's a couple of feet long, maybe an inch in diameter, and fairly heavy." The bar is long.
	The scent-description of the bar is "iron".

The bard is a man. The description of the bard is "He's dressed like a strolling minstrel from a Renaissance fair[if bard sings is happening], and he's singing about as well[otherwise], and looks as though he's going to burst into a rendition of Greensleeves at any moment[end if]."
	The scent-description of the bard is "medieval hygiene".

Bard sings is a scene. Bard sings begins when the bard is enclosed by location. Bard sings ends when the bard is marked invisible.

Every turn during Bard Sings:
	choose a random row in the Table of Bard Song;
	say description entry;
	say paragraph break.

Singing is an action applying to nothing. Understand "sing" as singing.

Carry out singing:
	say "I prefer not to exercise my singing voice in company. (That includes you.)"

Instead of singing during bard sings:
	say "[We] attempt to sing louder than the bard and cover up his illicit lyricism, but it turns out that [one of]O Atlantida, Lead Thy People Free[or]Pop Goes the Phoenician Sailor[or]Paint Is A Pain and Pain is a Pan[or]The Chard Farmer's Anthem[or]In The Squid's Garden[at random] does not drown him out."

Table of Bard Song
description
"'Fowles in the frith,' sings the bard incomprehensibly, 'The fisshes in the flood, And I mon waxe wood.' He'd better not start waxeing his wood around here, that's all I have to say."
"'My lief is faren in londe, Allas, why is she so?' sings the bard. It's a good thing no police are around now, or he might be fined for singing in an archaic dialect without a permit."
"'Ewe bleteth after lamb, Loweth after calve cow,' intones the bard. 'Bulloc sterteth, bucke verteth, Merye sing cuckou!' He certainly seems keen on barnyard animals."
"The bard sings, 'Heo is coral of godnesse, Heo is rubie of ryhtfulnesse, Heo is cristal of clannesse, Ant baner of bealté.'"
"The bard sings bravely out, 'Mon in the mone stond and strit; on his botforke his burthen he bereth.' I don't know what a botforke is either, but it sounds to me like plumber's crack."
"The bard draws breath and sings, 'Mon mai longe liues wene, Ac ofte him lieth the wrench, Feir weder turneth ofte into reine, And wunderliche hit maketh his blench.'"

Instead of listening to the bard during bard sings:
	say "It is difficult to avoid hearing, really."

Instead of listening to the location in the presence of the bard:
	say "The bard is impossible not to listen to. Or rather: impossible not to hear."

The basin is a container. The description of the basin is "A stainless steel bowl, appropriate to cooking or perhaps surgery."

The basins are a container. The description of the basins is "A couple of nested metal bowls, so generic they might equally be used for mixing dough or for holding vital organs during a surgery. The irksome thing is that, having been made as a matched set, they cannot really be separated."

A basis is an r-abstract thing. The description is "It's abstract, but the best the abstraction was able to come up with in this case is something that looks like a small portable pedestal. Perhaps the base on which things are established? Something like that."

A bass is an r-abstract noisy thing. The description is "A deep resonant voice with very little visible at all, save a kind of trembling in the air. There's no mistaking where it is, though. It's currently [one of]reading out the nightly news, mostly about the great success of Serial Comma Day celebrations[or]singing a popular drinking song about synonyms[or]practicing scales[at random]." The heft of the bass is 0.

The bass-fish is a privately-named thing. The description is "A trophy of a fish. I don't know much about species, but it does look like it has a big mouth, certainly." The printed name is "bass". Understand "bass" or "fish" as the bass-fish. The heft of the bass-fish is 3.

The heft of the bat is 1. The description of the bat is "It's small and black-furred, and looks like it would rather be in a cool dark place alone with some fruit."


The Ben-wig is wearable. It covers the head-area.  The printed name is "Ben wig". Understand "wig" or "ben wig" as the ben-wig. The description is "It is a wig belonging to [--] at a guess [--] a Ben Franklin costume. A rubbery pate simulates baldness at the top of the head, while grey curls hang below. It would not, I fear, look remotely persuasive on our head."
	The scent-description is "the faintest whiff of dust".

The description of the bell is "It's a tiny golden bell with a tiny golden clapper inside." The bell is an instrument.

The description of the bill is "It itemizes the recipient's tax bill, payable to the Bureau immediately. This includes a list of 'estimated savings,' detailing what the tax-payer would have spent if, for instance, the Bureau maintained a full-service prison system."

The printed name of the abstract-bend is "bend". Understand "bend" as the abstract-bend. The description of the abstract-bend is "The bend is realized as a turn of about 60 degrees in a length of copper pipe." The abstract-bend is long and strong.

The bet is usually r-abstract. Understand "wager" or "betting book" as a bet. The description of a bet is usually "It's represented as a wager written down in a betting book, the current entry concerning [one of]this year's chard harvest[or]the number of days it's destined to be rainy in Atlantis this year[or]a horse inventively named Mare[or]the minimum possible driving time from Palm Square over to the Rust Tip Parking Lot in Maiana on the other side of the island[sticky random]."

[The heft of the big revel is 20. The description of the big revel is "It's the entirety of a party in true Gatsby style: dancers doing the Charleston, the orchestra to play the music, the waiters to circulate the canapes, some sculptures, a fountain or two, a fireworks display."]

The bind is r-abstract. The description of the bind is "It's represented as an old-fashioned illustration of Odysseus['] passage between the rock and the hard place. Scylla's heads poke out over the water, her doggy eyes red with wrath."

The heft of a bit is usually 1. The description of the bit is usually "One of those small metal pieces used to attach bridles to horses. One doesn't see them much in ordinary life, these days."

The description of the blest sandal is "A very worn and dusty relic, of which only a few leather straps and a filthy sole now remain. Yet, despite its state, it presents an aura of significance, even holiness." The blest sandal is wearable.

Sanity-check wearing the blest sandal:
	say "It just wouldn't seem right." instead.

The boa is a floppy wearable thing. The description of the boa is "Made of bright pink synthetic feathers. It [if the boa is worn]really livens[otherwise]would really liven[end if] up our look, I have to say." Understand "bright" or "pink" or "synthetic" or "feathers" as the boa. Understand "feather boa" as the boa. Understand "feather" as the boa when the hackle is marked invisible.
	The scent-description of the boa is "plastic".

The boar is a neuter animal. The description of the boar is "In the old days they used to hunt these animals, and I can understand the impulse. It's like a pig, but even uglier and bristlier, with long dangerous-looking tusky teeth coming out of both its top and bottom jaws." The heft is 7.

Report waving the letter-remover at the boar:
	say "Our hand is less than steady, but [we] manage to wave the letter-remover accurately enough.[paragraph break]The boar vanishes with a pop, and [a generated object] [fall] harmlessly to the [ground]. [run paragraph on]";
	now boar is marked invisible;
	try examining the generated object instead.

Boar Mating is a recurring scene. Boar mating begins when the boar is enclosed by location and the suid is enclosed by location. Boar mating ends in disappointment when the boar is enclosed by location and the suid is not enclosed by location. Boar mating ends in death when the boar is marked invisible.

Every turn during Boar Mating:
	say "[one of]The boar [if the roc is not in location]looks at [us] unpleasantly, but then [otherwise]seems to forget all about [us] and the roc, as [end if]the pleasing scent of the [suid] attracts it instead[or]The boar is circling the suid, as near as it can in this awkward space[or][one of]The boar follows the suid around, its nose at her butt[or]The suid is trying to walk away from the boar[or]The boar makes an attempt to mount the suid, but slips on the sand[or]The suid squeals indignantly and walks away from the boar, who follows[at random][stopping]."

[The boar attack is based on several accounts of real life attacks in which the boar repeatedly rushed the victim (often unprovoked), gashing in several places. The most serious wounds generally are abdominal wounds inflicted after the boar has knocked the victim down. It would be more true to life if the boar also left gashes in the protagonist's leg in the process of knocking her down, but I decided to skip that because I didn't want to deal with a game state in which the protagonist was wounded but not dead: it would have needlessly complicated the writing of the end-game to no interesting narrative effect.]

Boar Attack is a recurring scene. Boar Attack begins when the boar is enclosed by location and the suid is not enclosed by location and the roc is not enclosed by location. Boar Attack ends in relief when the boar is marked invisible. Boar attack ends in distraction when the boar is enclosed by location and the suid is enclosed by location. Boar Attack ends in fear when the roc is marked-visible. Boar Attack ends in death when the time since Boar Attack began is greater than 2 minutes.

When Boar Attack ends in fear:
	say "The boar stops abruptly at the sight of the roc."

Every turn during Boar Attack:
	if the boar is not in location:
		move the boar to location;
	let T be the time since Boar Attack began;
	let N be T divided by 1 minute;
	increment N;
	if N > 1 and the player is in a closed car (called current-car):
		say "[one of]With a grunt, the boar hits the side of [the current-car] headfirst; the door flies wide open from the impact[or]Annoyed, the boar headbutts the car door open again[stopping].[paragraph break]";
		now current-car is open;
	choose row N in the Table of Boar Attacks;
	say "[description entry][paragraph break]".


Table of Boar Attacks
description
"The boar gives us a very nasty kind of look, and then [--] without any provocation, I'm sure [--] starts running right at us[if the player is in the kayak], even into the surf[end if][if there is an open trap in location] (nimbly avoiding the trap)[end if]."
"The boar just barely misses giving us a long gash in [if the player is in the kayak]the stomach[otherwise]the thigh[end if], but it wheels around for another attempt. If it knocks us down, I am not too hopeful about our experience with those tusks."
"I try to get us out of the way in one direction, you in another, and the result is that the boar knocks [if the player is in the kayak]the kayak over[otherwise]us flat[end if] with the butt of its head. It stands over us snorting."

When Boar Attack ends in death:
	say "The boar decides to have another go, tearing viciously at the soft flesh of our abdomen. The whole thing is oddly slow and horribly precise[if the player is in the kayak], and the foam turns bloody around us...[otherwise], and the blood makes a rivulet down to the sea...[end if]";
	end the story saying "[We] have been mortally wounded, and there's no one around to help".

Instead of jumping during Boar Attack:
	say "[We] do our best to get out of the way."

Some tusks are part of the boar. The description of the tusks is "Let's not find out anything more personal about them."

Boar Hiding is a recurring scene. Boar hiding begins when the boar is enclosed by location and the roc is enclosed by location and the suid is not marked-visible. Boar hiding ends in attack when the boar is marked-visible and the roc is not enclosed by location. Boar hiding ends in distraction when the suid is marked-visible. Boar hiding ends in relief when the boar is marked invisible.

Every turn during Boar Hiding:
	say "[one of]The boar looks at [us] unpleasantly, but the roc flaps it wings and steps between [us] and the boar[or]The roc is pacing back and forth in front of the boar, which squeals with fear[or]The boar takes a tentative step toward [us], but one stern look from the roc makes it turn on the spot[cycling]."

The board is a thing. The description of the board is "It's a fairly generic plank [--] sort of pine, by the looks of it, though constructed things tend to be a little vague on niceties such as species [--] and looks like it's designed to be part of a new deck or somesuch thing." The board is long.
	The scent-description of the board is "raw wood".

The bock is a contained fluid edible thing. The description of the bock is "It's a bottle of strong German lager with a goat on the label."
	The scent-description of the bock is "beer".

The bond is r-abstract. The description of the bond is "It's an Atlantis treasury bond, one of the hundred-year wartime series. It's set to mature in 2042."

Sanity-check burning the bond:
	say "A bold political statement, but no." instead.

The description of the bondage is "Made of black leather straps and strong steel rivets, it is designed to hold the user's wrists and ankles immobile." The indefinite article is "some". The bondage is wearable.

Sanity-check cutting the bondage:
	say "That would undermine its intended purpose rather." instead.

Sanity-check wearing the bondage:
	say "There's no time for a fetish modeling shoot just now." instead.

The bot is r-abstract. The description of the bot is "A Bureau of Orthography web crawler, whose purpose is to search out negative or defamatory statements about Atlantis for censorship or 'remediation.' It's represented here by some sample lines of code."

The branding is r-abstract. The description of the branding is "A series of successful product logos sift through the air. This one comes with overtones of [one of]high-end luxury[or]self-pampering[or]virility[or]femininity[or]confidence[at random]; that one makes us think of [one of]affordability[or]reliable performance[or]superior business ethics[at random]." The scent-description of the branding is "[one of]new cars[or]upscale perfume[or]cinnamon[or]vanilla[at random]".

[Originally the brown tree / brown tee were colorless, but this meant that it was possible to do the TREE/TEE puzzle without ever picking up the officer's rifle. That was considerably less interesting.]

The description of the broken component is "A contraption of metal gears and ratchets. It's obviously bent now, but there's a slot that at one time was meant to hook onto the letter J."

The description of the breathable silky cotton is "A bolt of very pleasant material in cadet blue." The indefinite article of the breathable silky cotton is "some".

The description of the brown tee is "It's a brown wooden peg for putting a golf ball on. Apparently the All-Purpose Officer performed a little light R-insertion when he found himself in need of a portable tree." The heft of the brown tee is 1. The brown tee is scenery.

The bull is an animal. The description of the bull is "He looks as though he has just been transported away from his cows, and he isn't at all pleased about the change. As a matter of fact, he blames [us] personally."

[The heft of the bund is 10. The description of the bund is "An earthwork embankment designed to prevent oil repositories from bursting their banks and spilling into the surrounding waters."]

The heft of a fake-bus is 10. The description of a fake-bus is "It's one of the coaches that runs a regular service between here and Maiana. The windows are tinted to keep off the worst of the afternoon glare.". The printed name of the fake-bus is "bus". Understand "bus" as the fake-bus.

The heft of some buses is 10. The description of buses is "There's a whole fleet of them, perhaps every bus on the island. They're dusty: the road to Maiana gets dry in the summer."

Section 3 - C

The description of the cacao-nub-heap is "A conical heap about six inches high, made of bits of unprocessed cacao bean." Understand "nub" or "heap" or "cacao" as the cacao-nub-heap. The printed name of the cacao-nub-heap is "cacao nub heap". The cacao-nub-heap is edible. The scent-description of the cacao-nub-heap is "chocolate".

A cad is a man. The description of the cad is "He is dressed with careless elegance, as I would expect from a rake and betrayer of ladies. [one of]As [we] look in his direction, he smiles lazily.[or]His gaze smoulders.[or]He ignores you in a way that makes you long to know him better. I, on the other hand, am trying to roll our eyes.[or]He acknowledges our glance, then quite deliberately looks away. Do you really go for this stuff?[or]He looks [us] up and down as though calculating what exactly [we] look like under this modern attire.[as decreasingly likely outcomes]". Understand "gentleman" or "man" or "fellow" as the cad.
	The scent-description of the cad is "old-fashioned cologne".

Rule for writing a topic sentence about the cad:
	if a woman is mentionable:
		say "[one of][The random mentionable woman] steals a glance at [the cad][or][The cad] is looking [the random mentionable woman] up and down with a quizzing glass[or][The random mentionable woman] is blushing furiously thanks to something [the cad] just said to her[or][The cad] and [the random mentionable woman] are whispering among themselves[or][The random mentionable woman] clears her throat pointedly and moves away from [the cad][cycling].";
	otherwise:
		say "A gentleman in regency dress lounges against the far wall: evidently, this is the cad produced by [the letter-remover]. "

[In a much, much earlier version of the game, there were puzzles involving making the cad and then ordering him to do things (first bribing him with a kiss). As the game progressed and the tone shifted, this bit of silliness came to seem out of place; and, worse, it became obvious that having dozens and dozens of possibly persuadable, action-taking NPCs would significantly complicate implementation. So all the persuasion rules were axed and there are no puzzles involving getting generated NPCs to do things, aside from the mechanic in the garage.]

[A persuasion rule for asking the cad to try doing something:
	if the cad is amenable:
		now the cad is disinclined;
		say "'This one fav[our],' the cad allows. [run paragraph on]";
		persuasion succeeds;
	otherwise:
		say "[one of]'I will try [--] and only try [--] anything you ask me,' he says, 'but only at the cost of a kiss.'[or]'You know the forfeit by now,' he says.[or]'I accept payments only in advance.'[or]He smiles meaningfully.[stopping]";
		persuasion fails. ]

Instead of kissing the cad:
	say "[one of]Summoning your courage and all my patience, [we] stand on tip-toes and kiss the gentleman. He returns the fav[our], with all the skill that his nature requires.

You, I can tell, are enjoying this. I on the other hand am going to have to gouge it out of my brain with a power drill later.[or]You repeat your earlier performance, while I do my level best to stop you.[stopping]".


Every turn when the player carries the cad:
	move the cad to the location;
	say "[The cad] turns out to be a tall and somewhat muscular gentleman, and [we] are unable to support him during the transformation; by the time it is done, he is more standing in our embrace than actually being carried by us. I take a hasty, embarrassed step back.

He smiles knowingly, but merely straightens his cuffs."

[Unsuccessful attempt by the cad trying doing something:
	say "[one of]But [--] wouldn't you have expected it? [--] the cad is unable to fulfill his promise.[or][paragraph break]'I regret,' he says, with a bow, 'that I am unable to oblige.'[paragraph break]After trying for a moment, he says, 'Alas, I believe you have asked too great a fav[our].'[or][paragraph break]'This is harder than I expected,' he remarks after a moment. 'And insufficiently entertaining.'[or][paragraph break]'This is worse than the incident with Sally Jersey and her chambermaid,' he mutters under his breath, giving up.[as decreasingly likely outcomes]". ]

Instead of putting the restoration gel on the cad:
	say "As you approach, he takes a step backward and fends you off with one (finely-boned yet manly) hand. [one of]'I think not,' he says, in a low voice that thrills you deeply. 'I never would allow a lady to apply an unknown liniment to my person.'[or]Apparently he hasn't changed his stance on unidentified substances.[stopping]".

[Some camemberts are an edible thing. The description of the camemberts is "A whole stack of French camembert, which would be convenient if [we] were opening a black market deli, but may be hard to explain under other circumstances. Also not terribly portable." Understand "cheese" or "rind" as the camemberts. The heft of the camemberts is 10.
	The scent-description of the camemberts is "cheese rind".
	Sanity-check cutting the camemberts:
		say "A notch in the flesh of the nearest camemberts exposes the yellowish creamy interior." instead.]

The camembert is an edible thing. The description of the camembert is "A rich, soft, creamy cheese with a white rind. This one isn't too runny yet." Understand "cheese" or "rind" as the camembert.
	The scent-description of the camembert is "cheese rind".
	Sanity-check cutting the camembert:
		say "A notch in the flesh of the camembert exposes the yellowish creamy interior." instead.

Every generic-cap covers the head area. The description of a generic-cap is "A black sports cap with the logo of my father's fav[our]ite soccer team The Atlantis Cuttlefish.". The printed name of a generic-cap is "cap". Understand "cap" or "sports" or "logo" or "cuttlefish" as a generic-cap. The scent-description of a generic-cap is usually "sweat and hair wax".

The description of an alterna-cap is "A small plastic snap cap that looks like it used to sit on a tube or bottle of some kind."

Understand "cap" or "plastic" and "snap" as an alterna-cap. The printed name of alterna-cap is "cap".

Sanity-check putting an alterna-cap on the tube:
	say "The cap doesn't fit. It must belong to a different tube." instead

Sanity-check putting an alterna-cap on the bottle of vodka:
	say "The vodka bottle already has a cap." instead

Sanity-check putting an alterna-cap on the bottle of ouzo:
	say "The ouzo bottle already has a cap." instead

Sanity-check putting an alterna-cap on an contained fluid thing (called current bottle):
	say "[The current bottle] already [have] [if the current bottle is plural-named]caps[otherwise]a cap[end if]." instead.

The description of the cartage is "'Cartage' here takes the form of an invoice for the transport of farm products by cart."

The cassock is a floppy wearable thing. It covers the torso-area. The description is "An ankle-length robe of the most basic and severe black, the sort worn by priests of the more high-church denominations. It buttons down the front. In accordance with Atlantean traditions, white embroidery over the breast forms the letters A and Z."


Instead of rubbing a cat: say "I hear you can produce electricity that way, but I don't fancy being scratched in the attempt."
	Sanity-check squeezing a cat:
		say "What, to make it yodel? It seems a bit cruel." instead.

	[A little nod to a related line in Curses:]
	Sanity-check washing a cat:
		say "I can tell you've never owned a cat." instead.

Sanity-check pushing or pulling or turning a cat:
	say "[We] could do that, but cats are notoriously uncooperative about staying where put." instead.

Instead of smiling at a cat:
	say "[We] smile. [The noun] regards [us] with steady disdain."
Instead of frowning at a cat:
	say "[We] frown. [The noun] does not seem much abashed."

The printed name of a cat-army is "cat army". Understand "cat" or "cats" or "army" as a cat-army. The heft of a cat-army is 8. The description of a cat-army is "There are dozens of them, and they move in curiously regimented, uncatlike ways, as though the big tabby at the front of the group really is their commanding officer."

Rule for printing the plural name of cat-army:
	say "cat armies".

Check waving the letter-remover at a cat-army when the current setting of the letter-remover is "y":
	say "The device buzzes, puzzled. It has tried to create a 'cat arm': I guess the forelegs of cats don't really qualify as arms. Well, not to this letter-remover anyway." instead.

The printed name of the cat-mayor is "cat mayor". Understand "cat" or "mayor" as the cat-mayor. The cat-mayor is a cat. The description of the cat-mayor is "Like an ordinary cat, only wearing tiny black leather boots and displaying, if possible, an even greater regard for its own dignity than the average feline." The cat-mayor wears tiny black leather boots.

The description of the tiny black leather boots is "They are the tall sort with cuffs that roll over."

Sanity-check wearing the tiny black leather boots:
	say "They wouldn't fit." instead.

The catnap is r-abstract. The description of the catnap is "Much like an ordinary nap, but smaller and... more cuddly, perhaps?"

The cate is an edible thing. The description is "Rarely found in the singular, a cate is something delicious: it comes from the same roots as 'caterer'. At any rate, this lovely little confection, with its layers of shiny-smooth chocolate and sliced strawberry and rum-soaked cake, its chocolate-shaving frills and piped cream, could have come from the best Paris [i]confiserie[/i]." Understand "confection" or "frills" or "chocolate-shaving" or "chocolate" or "strawberry" or "layers" or "layer" or "rum-soaked" or "cake" or "piped" as the cate. Understand "cream" as the cate when the cream is marked invisible.

Instead of tasting the cate:
	say "[We] allow ourselves a lick of the lovely chocolate."

Instead of smelling the cate:
	say "The aroma is faint but enticing: bright strawberries, dark chocolate, and a boozy base note of rum."

[Instead of eating the cate when the crate contains something:
	say "Unable to restrain ourself, we eat two bites before remembering that there might well have been something in the crate: in which case, it would be foolish, possibly even dangerous, to consume the whole cate."]

[Instead of eating the cate:
	say "It is so luscious that you barely bother to worry about Slango's possible reaction. But crates are not that expensive. No doubt he has plenty more where that came from. No problem at all." ]

The climate is r-abstract. The description of the climate is "A tight ball of weather icons, mostly depicting Sunny, Hazy, or Partly Cloudy days. Anglophone Atlantis doesn't get a lot of your more exotic weather, your snow and ice storms and hail." The heft of the climate is 0.


A cart is usually portable. A cart is usually pushable between rooms. The description of the cart is "It's one of those red metal carts that I would have liked very much when I was about five. All my friends had them."

[The cardboard is floppy. The description of the cardboard is "A sheet of cardboard a little larger than a standard piece of paper. It's brown." The indefinite article is "some".]

The initial appearance of a cat is "A cat wanders around [if location is indoors]the room[otherwise]here[end if]."
	The description of a cat is "Full-grown, American, and a little stupid, as felines go." Instead of touching a cat, say "It rubs itself pathetically against our hand." The heft of a cat is 3.
	The scent-description of a cat is "clean animal".

[card]


[The aim here is to hint at an Atlantean tarot, focusing especially on cards that have something to do with the relationship of the state and the individual, or duality within the individual. Strength is traditionally shown as a young lady managing a tame lion, but there are no lions on Atlantis; whereas the squid is associated with Atlantean inventiveness and power but suggests tentacle porn as well; the squid is a subversive force. The figure of the Magician represents an All-Purpose Officer with his weird array of very special[ize]d tools; Justice is Atlantida herself, somewhat ironically.]

Card-style is a kind of value. Some card-styles are defined by the Table of Card-Styles.

Table of Card-Styles
card-style	article	image
Justice		""	"A woman in a blue dress sits on a throne. She carries a letter-remover in one hand and her eyes are blind-folded"
Chariot	"the "	"The vehicle is drawn by two prize horses, one black, one white. The driver appears to be having some difficulty keeping them together, to judge from the surly expression and the raised whip"
Magician	"the "	"A man stands over a table of delicate instruments: letter-razors, reversing mirrors, decapital[ization] disambiguators. He is putting on a performance, though we cannot see his audience"
Strength	""	"A young nude lady lounges in the embrace of a giant squid. They are apparently on very good terms"
Tower	"the "	"It represents the Tower of Babel being struck by lightning. The top of the tower is falling over, and terrified builders run in every direction. In Atlantean terms it is the end of the world"
Fool	"the "	"A shiftless young woman is carrying a loaded backpack, striding across a sunny landscape"
Twins	"the "	"They're male and female. A floating, sky-born figure above them is either choosing one or about to shoot one: it's hard to know what to make of the pointer or arrow it wields"
Temperance	""	"An official-looking man is operating a gigantic synthes[ize]r. Out of its bottom fall a flood of oxymoronic figures: a dwarf elephant, a giant mouse, a bittersweet cake.."

The description of the card is "Not a playing card, as I might have expected, or a calling card, or even an index card, but a Tarot card, representing [article of (card-style of the card)][card-style of the card]. [image of the card-style of the card]." The heft of the card is 1. Understand "tarot" as the card. [Rule for printing the name of the card while asking which do you mean: say "Tarot card".]
	The card has a card-style.
	Understand the card-style property as describing the card.
	The scent-description of the card is "crisp, snappy cardboard".

Carry out waving the letter-remover at something creating the card:
	reset the card.

To reset the card:
	if the bartender is in location:
		if the card-style of the card is not Magician:
			now the card-style of the card is Magician;
		else:
			now the card-style of the card is Temperance;
	else:
		if the Roundabout is not visited:
			now the card-style of the card is Chariot;
		else:
			now the card-style of the card is a random card-style which is not the card-style of the card.

Report waving the letter-remover device at something creating a seen card:
	say "[The second noun] [give] way to the [generated object]. This time it's [article of (card-style of the card)][card-style of the card]. [image of the card-style of the card]." instead.

The description of the carpet is "A substantial, rectangular rug of the sort on which Aladdin is typically seen flying. It has tasseled ends and a pattern that hints at Atlantis['] Moroccan connections." The heft of the carpet is 6.

Sanity-check launching or mounting or entering the carpet:
	say "It may look like a flying carpet, but I don't think it's actually magic." instead.

[carp]
The description of a carp is usually "A large Asian carp with red and white scales, the kind that might be seen in the indoor pond of an expensive hotel. Not that decorative in its current dead state." Understand "fish" and "goldfish" and "scales" as a carp.
	The scent-description of a carp is "faint fishiness". The heft of a carp is 3. A carp is edible.

Instead of eating a carp:
	say "That would be like eating a large goldfish."

Instead of smelling a carp:
	say "[We] have at least produced a fresh fish, rather than a rotten one: consider yourself lucky."

[carpi]
The description of some carpi is "Small, irregularly-shaped bones, taken from the hand of a human skeleton. They look as though they ought to be used in divination; why else would anyone keep them around?"

[CD]
The description of the CD is "To judge by the label, it is one of King Crimson's longer works."

[cement porno knob]

The description of the cement porno knob is "Imagine, if you will, a pornographic film set on the grounds of an aristocratic estate.

Further imagine that among the hedges and flowerbeds there are various cement lawn decorations, but instead of the cupids and pineapples one might expect, these lawn decorations resemble a certain portion of the male anatomy.

This cement porno knob comes from just such a production. Other memorabilia are not included, and the Naughty Groundskeeper outfit is nowhere to be seen."

[chad]
The description of the chad is "It's one of those little pieces of paper punched out when you mark a ballot, which makes it small and nearly useless, except as a potent symbol of election fraud." The heft of the chad is 1.
	The scent-description of the chad is "paper".

[char]

The description of a char is usually "It's a troutlike freshwater fish of the northern countries. It is dead, needless to say, and would make someone a nice supper if they knew how to clean and cook it. Our skills, however, are not up to the task." Understand "fish" as a char.
	The scent-description of the char is "faint fishiness".
	A char is edible.

Instead of eating a char:
	say "I require my fish to be cleaned before I eat it, if not cooked."

Instead of smelling a char:
	say "[We] have at least produced a fresh fish, rather than a rotten one: consider yourself lucky."

Understand "clean [char]" as a mistake ("I do not know a lot about the proper preparation of fish.").

Understand "cook [char]" as a mistake ("Even if you know how, there's no time for that now.").

The description of the charlie is "It's a quantity of very suspicious-looking white powder. Cocaine, I believe. Drug slang was never my strength." The charlie is illegal. The indefinite article of the charlie is "some".

Sanity-check  smelling the charlie:
	say "[We][']d prefer not to undergo this whole process while higher than a kite. We need our wits." instead.
Sanity-check cutting the charlie:
	say "Odds are it's already cut with corn starch or baby powder or whatever it is they put into drugs to make up the weight." instead.

[chart]
The description of the chart is "A beautiful nautical chart depicting the harbor of Anglophone Atlantis, safest approaches, etc."


The charring is r-abstract. The indefinite article is "some". The heft of the charring is 0. The description is "It's formed like a little stick of kindling, blacked at one end as though used to light a larger fire; but it's an abstract, implausibly light, because it has to convey the idea of charring rather than a charred thing itself."

[chat]
A chat is usually noisy. The description of the chat is "It looks a little like a recording device, except that there are no on or off buttons and you have no way to control what it replays. It has a small screen and tinny speakers." The heft of a chat is 1. The chat is usually r-abstract.

Instead of listening to the chat:
	say "Eerily, it plays back a little of our conversation the other day at the café."

The description of a chert is "It's a chunk of greyish rock which appears to have flaked off in layers. It looks as though there are some small fossils embedded in the stone." Understand "rock" as a chert when rock is marked invisible. Understand "stone" as chert.

A clack is an r-abstract noisy thing. The heft of the clack is 0. The description of the clack is "It's the sound of an old-fashioned train in motion."

Instead of listening to the clack:
	say "The clack clacks and clackety-clacks to its heart's content.".

A cack is a thing. The indefinite article is "some". The description is "It's a soft-soled baby shoe. I know, I was expecting something else too.". The scent-description is "baby powder".

A click is an r-abstract noisy thing. The heft of the click is 0. The description of the click is "It's a sporadic, repetitive clicking noise as though someone were playing obsessively with a mechanical pencil."

Instead of listening to the click:
	if a random chance of 1 in 2 succeeds:
		say "The clicking noise [one of]continues[or]persists[or]is not abating[stopping].";
	else:
		let N be a random number between 1 and 5;
		repeat with I running from 1 to N:
			if a random chance of 1 in 2 succeeds:
				say "CLICK ";
			else:
				say "CLICKETY ";
		say "clicks the click."


[clock]
The clock has a time called the current time. The description of a clock is "It appears to be one of those archetypal alarm clocks that crows at sunrise and generally makes a nuisance of itself. It shows the time to be about [the current time to the nearest five minutes in words]."

Every turn (this is the advance clock rule):
	increase the current time of the clock by one minute.

Understand "set [clock] to [time]" as setting it by time. Setting it by time is an action applying to one thing and one time.

Instead of setting the clock to something:
    say "[The noun] can be set only to a time of day, such as 8:00 AM, or midnight."

Carry out setting a clock by time:
    now the current time of the noun is the time understood.

Report setting a clock by time:
    say "[We] set [the noun] to [time understood]."


A cluck is an r-abstract noisy thing. The heft of the cluck is 0. The description of the cluck is "Genuine barnyard stuff, it sounds like."

Instead of listening to the cluck:
	say "'Cluck";
	let N be a random number between 0 and 3;
	repeat with I running from 1 to N:
		say " cluck";
	say "[one of]BACAW[or][at random]!' says the cluck.".


The chowder pro is a woman. The description of the chowder pro is "She's a proud Massachusetts chef, dressed in [a green apron] and ready to get to work, if only there were a kitchen for her."
	The chowder pro wears a green apron. The description of the green apron is "Spattered with the lifejuice of clams."
	The greeting of the chowder pro is "'Welcome to the show!' says the chowder pro brilliantly. Unclear what show that would be, of course."



[coasts]

[Some coasts are a thing. The heft of the coasts is 12. The description is "A vast rocky coastline too large to fit in the present confines."]

[cock]
The cock is a bird. It is noisy. The flexible appearance of the cock is "[if the cock is in the location]Wandering around nearby, for all the world as though this were a barnyard, is a large red-headed cock[otherwise]The cock [we] made earlier is regarding [us] suspiciously[end if]." The description of the cock is "It stares back at [us] malevolently through one eye." The heft of the cock is 3.
	The scent-description of the cock is "something objectionable".

Instead of listening to the cock:
	say "[one of]The cock clucks[or]The cock lets out a half-crow[at random]."

Test cockbug with "autoupgrade / wave l-remover at clock / open tub / gel cock" holding the clock and the tub.

[The cockpit is a thing. The heft of the cockpit is 10. The description is "It's a tiny room lined on every surface with buttons and instrument readouts and warning signs, altimeters and radios and all sorts of other gadgets I don't recognize." Understand "buttons" and "readouts" and "instrument" and "warning" and "signs" and "altimeters" and "radios" and "gadgets" and "button" and "readout" and "sign" and "altimeter" and "radio" and "gadget" as the cockpit.]

The cock-ring is a wearable thing. The printed name is "cock ring". The cock-ring is naughty-sounding. The description is "A sturdy ring of black rubber[one of]. Which reminds me [--] never mind[or]. And no, I'm not going to tell you about that memory[or]. Stop prying[stopping]."  Understand "cock" or "ring" as cock-ring.
	Sanity-check wearing the cock-ring:
		say "Er. Our combined physique doesn't allow for a place to put that." instead.

[My beta-tester Sam Kabo Ashwell is responsible for the inclusion of this nauseating substance, for which he helpfully provided the following recipe:

'Take 10 gallons of ale and a large cock, the older the better; parboil the cock, flay him, and stamp him in a stone mortar until his bones are broken (you must gut him when you flay him). Then, put the cock into two quarts of sack, and put to it five pounds of raisins of the sun-stoned; some blades of mace, and a few cloves. Put all these into a canvas bag, and a little before you find the ale has been working, put the bag and ale together in vessel. In a week or nine days bottle it up, fill the bottle just above the neck and give it the same time to ripen as other ale.']

The cock-ale is a contained edible fluid thing. The printed name is "cock ale". Understand "cock" or "ale" or "feather" or "parboiled" or "meaty" or "meat" as the cock-ale. The description is "A medieval style of ale which has been flavored with parboiled rooster. Is that a bit of feather? I think it might be." The scent-description of cock-ale is "meaty hops".
	Sanity-check tasting cock-ale:
		say "Are you insane?" instead.

The description of a cod is "There's about twelve pounds of fish-and-chips-in-waiting here [--] it's actually materialized as fillets rather than the fish itself, which is some indication of how Atlanteans think about cod, when they can get it. Overfishing, you know."

The code is scenery. The description of the code is "A bit of paper on which is written '305.'" Understand "bit" or "paper" or "bit of paper" or "numbers" or "number" as the code.


The heft of the complicit shark is 8. The description of complicit shark is "It's dead, naturally. And a shark carcass is a large and unwieldy sort of thing to have lying around. There's a gash in one side that must have notionally killed it. Is the shark complicit in its own death? Or was there some other bit of shark-cooperation at work here? Hard to tell."
	The scent-description of the complicit shark is "rotting fish".
	Understand "gash" as the complicit shark.

The conference-poser is a woman.
The printed name is "conference poser".
Understand "conference" or "poser" as the conference-poser.
The initial appearance is "The conference poser stands in the corner, cultivating an expression of keen intellectual skepticism."
The description of the conference-poser is "She looks back at [us] with a martial gleam of the eye, willing [us] to be impressed by her.".
	The greeting of the conference-poser is "The conference poser looks at [us] a little hazily and says, 'Oh, yes, didn't we meet at the [one of]Linguistics and Empire[or]Post-Colonial Consonants[or]Concept Formation Through Hiphop[or]Ecologically Responsible Linguistics[at random] [one of]symposium[or]workshop[or]conference[at random] in [one of]San Diego[or]Austin[or]Minsk[or]Liverpool[or]Lampeter[or]Montreal[or]Cologne[at random] [one of]last year[or]last winter[or]back in [a random number between 1995 and 2009][at random]?'"

Every turn when the conference-poser is in the location:
	say "[The conference-poser] says, '[one of]Yes, but we haven't really [i]problemat[ize]d[/i] [the random-visible-thing][or]I think a [one of]Marxist[or]Keynesian[or]Lacanian[or]Saussurian[or]Derridean[at random] understanding of [the random-visible-thing] is not, indeed cannot be, useful to us at this juncture[or]Statistical sentence analysis of a sufficient corpus of materials ought to reveal the likeliest forms for [a random r-abstract thing][or]What we have not considered is the possibility of synonym shame, for instance with words such as [i]donkey[/i] or [i]rooster[/i][or]My field is the study of hispanogerontology, the illicit practice of giving seniors a new lease on life by turning them into Señors[at random].'"

[The cop is a woman. The description of the cop is "A uniformed All-Purpose Officer with a grim countenance, her restoration-gel rifle aimed straight at [us]."]

[A dangerous construction rule for the cop:
	say "Before [we] have time to act, the All-Purpose Officer has called for backup. 'You better have a good explanation for this,' she says.[paragraph break]As it turns out, [we] don't.";
	end the story saying "That could have gone better";
	the rule succeeds.]

The description of the cord is "It's a thin glittery red cord of the kind used to tie up Christmas packages."

Table of Ultratests (continued)
topic	stuff	setting
"cordalt"	{ card, umlaut punch }	Generator room
"cordalt2"	{ card, umlaut punch }	Generator room

Test cordalt with "tutorial off/ autoupgrade / switch switch / put card in bucket / put curd in bucket / get cord / x cord / w / plug in dais / unplug cord / get cord / e / put cord in umlaut punch" [holding the card and the umlaut punch in the Generator room.]

Test cordalt2 with "tutorial off/ autoupgrade / put card in umlaut punch / get chord / switch switch / put card in bucket / put curd in bucket / get cord / x cord / wave h-remover at chord / w / plug in dais / unplug dais / e / put cord in umlaut punch" [holding the card and the umlaut punch in the Generator room.]

The heft of the cork is 1. The description of the cork is "One end is deep maroon from having been in a wine bottle for a time. On the side is stamped the logo of an Atlantean vintner."

A cot is usually not fixed in place. The description of a cot is usually "A portable bed, made up for sleeping." The heft of the cot is usually 4. A cot is usually long.

Test cotheft with "put coat in bucket / wave a-remover at coat / x cot / take cot" holding the coat in Generator Room.

The counter-alternate is a furniture counter. Understand "counter" as the counter-alternate. The printed name is "counter". The description is "It's formica-topped and suitable for a kitchen that hasn't been renovated in some years."

The description of the counterweight is "A very substantial hunk of metal with a ring at the top end, suitable for attachment to a hook or rope." The heft of the counterweight is 3. Understand "weight" as the counterweight.

The description of the countertop is "The result is a slab of good black granite, shiny and easy to clean, with a pre-cut hole, probably where a kitchen sink would be meant for installation." The heft of the countertop is 8.

The description of a CPU is "A central processing unit, the brains of pretty much every modern device. This one is stamp-sized, with rows of tiny legs on every side.". Understand "processing" or "central" or "unit" or "legs" as a CPU.

The cratering is r-abstract. The indefinite article is "some". The heft is 0. The description is "It's an image, light as air, of the moon and the craters on it."

The crew-group is a privately-named plural-named person. The description of the crew-group is "Five (or six? it's extremely hard to count) swarthy sailors. They look buff and burly and have very thick eyebrows." The heft of the crew-group is 6.  The printed name of the crew-group is "crew". Understand "crew" as the crew-group.

[Persuasion rule for asking the crew-group to try doing something:
	rule succeeds.]

The description of the cross is "It's handsome, made of metal, the sort of thing that might adorn a church altar; I don't have the expertise to say whether it's especially valuable."

The description of the crossbar is "[description of the crosspiece]".

The heft of the crosshair is 0. The crosshair is r-abstract. The description of the crosshair is "It's the vertical line from crosshairs; the horizontal line is missing, which makes it even less useful than disembodied crosshairs otherwise would be."

The crosspiece is a long thing. The description of the crosspiece is "A sturdy metal piece for bracing... something. The rest of the object is omitted."

The description of the crow rod is "This is so odd a thing that nothing but the high power of the anagramming gun could have produced it. It is a metal rod, about a me[ter] long, with a stamped metal crow shape at the end. I'm not sure whether it's meant to resemble a weather vane or a magic wand, but honestly it doesn't seem quite suited to either of those purposes."

The description of a CRT is "A rather generic computer monitor, or possibly television; it isn't equipped with much more than the ability to turn on and off[if the crt is switched on]. At the moment it is displaying [one of]a Red Sox game[or]the moon landing[or]the Mary Tyler Moore show[or]back episodes of Smallville[purely at random][end if]."

Instead of listening to a CRT:
	say "The CRT issues the sounds of [one of]cheering baseball fans[or]blaring theme music[or]a dramatic explosion[or]tinny laughter[at random]."

Carry out switching on a CRT:
	now the noun is noisy.
Carry out switching off a CRT:
	now the noun is quiet.

The description of the crumb is "It's just one tiny little flake from a croissant or pastry."

The description of the cud is "It's best not to look too closely.". The scent-description is "grassy manure".

The description of a generic-cup is "A black porcelain coffee cup, bearing the logo of my father's fav[our]ite soccer team The Atlantis Cuttlefish.". The printed name of a generic-cup is "cup". Understand "cup" or "coffee" or "black" or "porcelain" or "logo" or "cuttlefish" as a generic-cup.

The curd is edible. The description of the curd is "A soft white clump of cheese substance."

The curse advertisement is a fixed in place thing. The description is "It's an ad of the kind you sometimes find in the back of underground newspapers, offering to take off the rude-object restrictions off a letter-remover, 'for private home use only.'"

The description of the cut is "A rough oval of skin, on which there's a visible cut [--] nothing serious, something on the order of a paper cut."

Section 4 - D

The heft of the daft document is 2. The description of the daft document is "It's a long print-off from a website, concerning the virtues of the [i]toxi waste[/i] scheme, complete with instructions about how to forward your money and lobby your representatives, if any."

The heft of the dart document is 2. The description of the dart document is "The dart document provides a set of rules and regulations for professional dart competitions, including minimum distance to the board, heft and design of the darts themselves, size and arrangement of the dartboard point areas, and so on."

[The heft of the dig is 20. The description of the dig is "It's a large affair: not child's a dig in the garden or even an archaeological dig undertaken with shovels and picks, but a massive industrial dig, the kind that happens beneath a major American city that didn't have the foresight to put down subways in 1890 when it would have been cheaper.

There's mud and stone and a cylindrical drilling machine five stories high."]

The diva is a woman. The description of the diva is "Though I couldn't put a name to her, we both feel as though we've seen her face many times on the sides of many buildings[if the diva wears the meat-gown]. She is wearing a [end if][if boldening is true][bold type][end if][if the diva wears the meat-gown]glamorous dress[roman type] decorated with slabs of meat[end if][roman type][if the diva wears the bacon chapeau]. On her head is an eye-catching [bacon chapeau][end if]."

The diva wears a meat-gown and a bacon chapeau. Understand "glamorous" or "glamourous" or "dress" or "gown" or "meat" or "meat-studded" or "studded" as the meat-gown. The printed name of the meat-gown is "meat studded gown". The description of the meat-gown is "You could feed a dozen starving steak-enthusiasts with this."

The description of the bacon chapeau is "People sometimes pin bacon on turkeys to keep them moist during roasting. It looks as though the diva's milliner has taken the same approach to her head." The bacon chapeau is edible. Understand "hat" as the bacon chapeau.

Table of Ultratests (continued)
topic	stuff	setting
"diva"	{ dove, anagramming gun, bullets }	Generator Room

Test diva with "switch switch / put dove in bucket / x diva / x gown / x chapeau / load gun / shoot gown / shoot chapeau / x diva" [holding the dove and the anagramming gun and the bullets in the Generator Room.]

The doe is an animal.
The doe is female.
The heft of the doe is 6.
The initial appearance is "A doe lurks nervously nearby."
The description is "She looks very skittish."

The printed name of the secret-door is "door". Understand "door" as the secret-door. The description of the secret-door is "It is part of the north wall[if secret-door is closed], and currently closed[otherwise]. It stands ajar[end if]." The secret-door is an openable, closed, enterable container. It is fixed in place.

Carry out opening the secret-door:
	now the Oracle Project is mapped north of Wonderland;
	now Wonderland is mapped south of the Oracle Project;
	follow the compass-drawing rule.

Carry out closing the secret-door:
	remove door passage.

To remove door passage:
	now the Oracle Project is not mapped north of Wonderland;
	now Wonderland is not mapped south of the Oracle Project;
	now the secret-door is closed;
	follow the compass-drawing rule.

A dangerous destruction rule for the secret-door:
	remove door passage.

Report opening the secret-door:
	say "[We] open the door, revealing a room beyond." instead.

Instead of entering the secret-door:
	try going north.

Instead of inserting something into the secret-door:
	say "[The secret-door] can't contain things."

The doper crow is a bird. The description of the doper crow is "It looks much like any other crow, except for a lethargy around the eyes, and a despondent silence in place of the usual cawing."

Instead of showing the doper crow to Poe:
	say "From his disdainful expression, he can obviously tell it's just a crow and not a proper raven."

The dope cow is an animal. The heft of the dope cow is 7. Understand "purple spots" or "spots" or "lip" or "udder" or "piercing" as the dope cow. The description of the dope cow is "A cow, but with really bad-ass purple spots, and a knowing, cow-of-the-world curl to its lip. The udder piercing is an eye-catcher."

Check waving the letter-remover at the dope cow when the current setting of the letter-remover is "p":
	say "A doe cow would presumably be some interesting genetic chimera, but the letter-remover isn't up to bringing it about." instead.


Section 5 - E

The description of the ear is "Severed. It's not a nice thing to look at." The heft of the ear is 1. The scent-description of the ear is "wax".


The description of the earlap is "Just the severed [i]tip[/i] of the ear, for the really discerning psychopathic collector. Not the lobe, but the top end."

The eases are r-abstract. The description is "Foggy, shifting images of the idea of ease: now a pillow; now a cigarette; now a seat in first class for a change.".

The earl is a man.
The initial appearance is "An Earl stands a few feet away, decked in identifying [ermine]."
The description is "He looks anxious and out-of-place."

	The earl wears an ermine. Understand "coat" as the ermine. The description of the ermine is "It is a coat of soft white fur, legendarily associated with purity and nobility, but now rather more suggestive of the mistreatment of animals."
	The scent-description of the earl is "musty furs".
	The greeting of the earl is "'Pleasure to meet you,' says the earl. 'No need to stand on ceremony, eh? Excellent.'"

The earring is a wearable thing. The heft is 1. The description is "A twinkling diamond pendant earring, the sort rich women wear to deluxe evening occasions. It is your expert eye, not mine, that determines it is not zirconium, and notes the '950 platinum' stamp on the metal." Understand "diamond" or "pendant" as the earring.

Sanity-check wearing the earring:
	say "We appear to have manifested with unpierced ears." instead.

The earpiece is a wearable thing. The description of the earpiece is "It's a little bluetooth device for use with a telephone; the kind of thing you typically don't see around the island, with cell phones as limited as they are."

The earwig is an insect.
	The initial appearance is "An earwig is crawling about looking menacing and slightly alien."
	The description is "These things have always given me the creeps."

[The elongating trouser is wearable. The description of the elongating trouser is "A garment with a single gradually lengthening leg. [trouser-growth]."

To say trouser-growth:
	say "It's [one of]our size now, but it won't be for long[or]too tall for us already[or]suitable for a man at the moment[or]a good length for a basketball player[or]currently about right for a clown on stilts[or]too long for anyone but the most dedicated tall stiltwalker[stopping]";

Instead of wearing the elongating trouser:
	say "It would only trip us. [trouser-growth]."]

An eel is edible. The description of an eel is "Manifested dead, because out of its proper element, the eel nonetheless gleams with silver and stripes."
	The scent-description of an eel is "a sort of oily, fishy tang".

The eight-object is an r-abstract thing. The printed name is "eight". Understand "eight" or "8" as the eight-object. The heft is 0. The description is "It looks like an infinity symbol rotated ninety degrees."

The elegantly winter pilot is a woman. Understand "aviatrix" or "slim" or "alert" as the elegantly winter pilot.
	The initial appearance is "[The elegantly winter pilot] shows no sign of nervousness or impatience."
	The description is "A slim, alert aviatrix, dressed suitably for a biplane duel somewhere over the Ukraine. Her eyes are almond-shaped and her glance taunting."
	The greeting of the elegantly winter pilot is "The pilot's greeting is a series of unidentifiable syllables. But somehow they hint at black forests dusted with snow, and a single jingling sleigh."

The elf nun is a woman. Understand "pale" or "pointed" or "ears" or "white-blonde" or "hair" as the elf nun.
	The initial appearance is "The elf nun stands watching [us]. Her eyes are very dark, almost blue-black."
	The description of the elf nun is "Her expression is deep and unreadable. From under her wimple, pale pointed ears and a few whisps of white-blonde hair show."
	The elf nun wears a wimple. The description of the wimple is "Embroidered in white on white with Celtic crosses and patterns that speak of Kells."
	The greeting of the elf nun is "'Blessings of growth and renewal be upon you,' says the elf nun. Her accent is sing-song and alien."

Every turn when the elf nun is marked-visible:
	if a random chance of 1 in 3 succeeds:
		if a vegetable is marked-visible:
			say "[one of][The elf nun] sadly eyes [the random marked-visible vegetable][or][The elf nun] speaks a short prayer for [the random marked-visible vegetable][cycling].";
		otherwise:
			say "[one of][The elf nun] crosses herself and whispers a few words in a sibilant tongue[or][The elf nun] touches her lips, her forehead, and her chest[at random]."

The description of the ell is "It's a measuring stick for a no-longer-used measure. I wouldn't really be able to tell it from a cubit at sight."

The Ent is a man. The heft of the Ent is 6. The description of the Ent is "He looks like a tree: an olive tree, perhaps surprisingly, but a very large, conscious olive tree, regarding us through eyes as black as his fruits." Understand "tree" or "olive" as the Ent.
	The greeting of the Ent is "Hoom."


The era is r-abstract. The description of the era is "Across an imaginary screen there flicker images from a standard school film: the island of Atlantis being formed, back long ago in geological time, and some assorted dinosaurs enjoying its beaches."


Section 6 - F through H

[A fame is a thing. The indefinite article is "some". It is r-abstract. The description is "It looks like an enormous star in twinkle lights."] [The player in fact should never be able to make this, because the farmer and stall should clear out by the time the player has r-abstract abilities.]

The initial appearance of the filing cabinet is "A filing cabinet stands in the corner." Understand "files" or "file" as the filing cabinet. The heft of the filing cabinet is 8. The description of the filing cabinet is "There are numerous files, and they are organized chronologically, so that it's possible to look up a specific year. They appear to begin in 1942."

Instead of opening or searching the filing cabinet:
	try examining the filing cabinet.

Instead of consulting the filing cabinet about a topic listed in the Table of Filed Paintings:
	say "[one of]Checking through the filing cabinet for [topic understood] reveals some images of Atlantida. [or][stopping][description entry]."

Instead of consulting the filing cabinet about a topic:
	if the topic understood matches "[number]":
		if the number understood is less than 1942:
			say "No files appear earlier than 1942." instead;
		if the number understood is greater than 2003:
			say "There are no significant contents past 2003." instead;
		let prev be 1942;
		let promising list be { 1942, 1982, 1983, 2003 };
		repeat through the Table of Filed Paintings:
			if the number understood is greater than the num entry:
				let prev be the num entry;
			otherwise:
				say "There's nothing much for [the number understood], but there are files for [prev] and [num entry]. ";
				remove prev from promising list, if present;
				remove num entry from promising list, if present;
				say "Other promising-looking years are [promising list]." instead;
	otherwise:
		say "The files seem to correspond to years. Flipping through the year folders at random shows that some years were much more interesting than others. The folders for 1942, 1982, and 1983 are especially thick. 2003 has a number of memos as well."

Table of Filed Paintings
topic	num	description
"1942"	1942	"Here is her militant 1940s rendition, where she stands bestride Atlantis; Axis planes are flying past her at head level. At any moment she might bite one and shake it, like a bulldog"
"1951"	1951	"Atlantida sits on a throne, wearing a blue dress and an incongruous string of housewifely pearls. In her right hand is the letter A with atom-like rings around it [--] as though to claim that letters are the true atoms of the universe"
"1958"	1958	"A sassy, security-conscious spirit of Atlantis, wearing a secretarial suit and putting a finger to her lips: the wink apparently means that it's alluring not to tell things to spies"
"1964"	1964	"This dossier contains a political cartoon: under comic OOPS lettering, Atlantida is seen shoveling dirt back over an assortment of ancient-looking lamps, pottery, and broken columns. Probably a reference to that incident where a portion of a Roman villa came to light and caused a temporary disruption in the strength of local conversions"
"1968"	1968	"The 1968 edition was some sort of hippy goddess of plenty (flowing hair, flowing skirts, cornucopia of local fruits)"
"1972"	1972	"From the year of the SALT I treaty comes an Atlantida aiming a deplural[izing] cannon at the world's ballistic missiles"
"1975"	1975	"There isn't any official propaganda for this year, only a contemporary magazine cover showing Atlantida arm in arm with Uncle Sam. Hard to tell for sure whether that was praise or criticism"
"1982"	1982	"A business-like, formidable Atlantida in the early 80s, looking Strong On Def[ense] and Tough On Misspelling, with an olive branch in one hand and an anagramming gun in the other"
"1983"	1983	"This year's folder doesn't contain any official propaganda posters; instead it's bulging with copies of a protest leaflet that shows a bare-breasted Atlantida leading rebels against the Bureau of Orthography, carrying placards in fav[our] of more referenda and true individual liberty"
"1989"	1989	"Newspaper clippings only. In one cartoon, Atlantida is imprisoned, looking out of the bars of her cell at Czechoslovakian and Romanian revolutionaries in envy at their newfound freedom. Another is just a written column commenting on the bicentennial of the French Revolution and suggesting that the figure of Atlantida is now associated with the spirit of revolutionary change"
"1991"	1991	"Photographs of rioters in Deep Street show people carrying posters of a bare-breasted Atlantida"
"1994"	1994	"Print-offs from an the early days of the world wide web show Atlantida's Home Page, plainly an organ[izing] platform for those interested in overthrowing the Bureau and promoting the more frequent and rigorous use of referendum voting. One imagines the site was shut down shortly thereafter"
"2003"	2003	"The dossier for 2003 is very thick. Memos back and forth indicate that officials are increasingly concerned with Atlantida and feel it is time (for some reason) to reclaim the figure as a 'government-positive' image. Consultations with an expensive New York advertising firm and a series of tv spots do not seem to have had the desired effect, however, because the project was canceled"

The flint crepe cap is a wearable thing. It covers the head-area. The description is "It's a stiff cap made of layers of grey, rock-toned crepe, much crinkled. It's hard to imagine any self-respecting hatter constructing such a confection, but an anagramming gun is a powerful thing.

There is [--] really there is the faintest suspicion that it's trying to spell itself 'crêpe'. Must be the foreign ingredients."

A foal is a neuter animal. The description of the foal is "It has a brown coat, large nervous eyes, and wobbly legs that don't seem to want to hold it up." The scent-description of the foal is "newborn animal".

The metal-foil is a privately-named thing. The description is "A crumpled ball of that aluminum foil used for cooking." The heft is 1. The printed name of the metal-foil is "foil". Understand "ball" or "foil" or "aluminum" as the metal-foil. The indefinite article is "some".

A foil is a thing. The description is "One of those long springy swords used for fencing. It has a button at the tip to prevent harm to one's opponent." The foil is long. Understand "long" or "springy" as the foil. Understand "sword" as the foil when the sword is marked invisible.

The button is a part of the foil. The description is "A tiny cylindrical cap of red rubber." Understand "tip" or "rubber" or "red rubber" or "cap" or "cylindrical" as the button.

Instead of taking the button:
	say "It's not made to come off easily: this isn't a sword for actually hurting people."

Instead of waving the foil in Walltop:
	say "[We] slash through the empty air, defending the city from imaginary attackers."

Instead of waving the foil in Open-Air:
	say "I would, but with the crowds around here [we][']d put someone's eye out."

Instead of waving the foil:
	say "[We] feign a few attacks against no one."

Understand "use [foil]" as waving.

Understand "fight with [foil]" or "fence with [foil]" as waving.

Understand "fence" as fencing. Fencing is an action applying to nothing.

Check fencing:
	if the player does not carry the foil:
		say "There's an odd idea." instead.

Carry out fencing:
	try waving the foil instead.

The description of the flea is "It is speck-sized. But alive, I think. An unappealing thought."

Every turn when the flea is marked-visible:
	now the flea is nowhere;
	say "The flea jumps away. In search of a hospitable dog or cat, no doubt."

The description of the frozen dinner is "It's microwavable three-cheese lasagna."

A fuel is a contained fluid thing. The indefinite article is "some". The description is "It looks like a generic canister of unleaded gasoline." The scent-description is "volatile fumes". Understand "canister" or "unleaded" or  "generic" or "canister of" as the fuel. Understand "gas" or "gasoline" or "petrol" as the fuel when the gas is marked invisible.

The funded-tomcat is a cat. The printed name is "funded tomcat". Understand "funded" or "funded tomcat" as the funded-tomcat. Understand "tomcat" as the funded-tomcat when the tomcat is marked invisible.
	The description is "Like an ordinary tomcat, but very sleek and well-fed, with a diamond-studded collar."

Sanity-check wearing the diamond-studded collar:
	say "It wouldn't fit." instead.

The funded-tomcat wears a diamond-studded collar. The description of the diamond-studded collar is "It would be worth a good bit, if it weren't for being fake. [if the player wears the Britishizing goggles]Jewellers[otherwise]Jewelers[end if] do tend to check."

Instead of taking the diamond-studded collar:
	say "[The funded-tomcat] hisses ferociously when [we] come near."

The description of the gam is "It's a woman's leg, encased in a black silk stocking with a seam up the back." The scent-description is "sultry perfume". Understand "leg" or "stocking" or "silk" or "black" or "seam" as the gam.

The garage is an enterable container. The heft of the garage is 8. The initial appearance of the garage is "A garage has sprung into being between two high-end boutiques." It is fixed in place. The description is "It is a small but well-furnished garage for minor tuneups and repairs[if the mechanic is in the garage]. It even comes with a mechanic: a surprising touch, since letter-removers are usually legally prevented from creating living persons, but sometimes there are glitches if the people are not created directly[end if][if every car is unseen]. Alas, however, it doesn't come with a car in residence[end if].".

Instead of entering the garage:
	say "There's just room enough for the mechanic and his possessions. It's something of a garage/shack."

A ranking rule for the garage: increase the description-rank of the garage by 80.

The garage contains a fake alert man called a mechanic. The description of the mechanic is "I'm a little surprised that you were able to generate this guy, but he seems to have come as part of the implementation of the garage."

A gas is a contained fluid thing. The indefinite article is "some". The description is "It looks like a generic canister of unleaded gasoline." The scent-description is "volatile fumes". Understand "canister" or "unleaded" or  "generic" or "canister of" as the gas. Understand "fuel" as the gas when the fuel is marked invisible.

The geas is an r-abstract thing. The description is "It is an enchantment of some sort. It is pictured as a balled-up net of dark strands, ready to capture and bind some victim to the caster's will."
	The scent-description is "smoke and cured leather".

The description of the gem is "It's a [one of]bubble-gum pink[or]London blue[or]Swiss blue[or]yellow[sticky random] topaz, all faceted and ready to be set. Not a particularly high-end article, but you weren't concentrating too hard, and the materials weren't so great to start with." Understand "topaz" or "pink" or "bubble-gum" or "gum" as the gem.

Understand "set [gem]" or "facet [gem]" as a mistake ("I don't have any training in the jeweler's arts.").

The giant-pin is wearable. The description of the giant-pin is "An outsized plastic brooch, suitable only for circus performers and halloween costume-wearers. It features a giant letter A." The printed name of the giant-pin is "giant pin". Understand "giant" or "giant pin" as the giant-pin. Understand "pin" as the giant-pin when the pin is marked invisible.

The giant-pint is a container. The printed name is "giant pint". Understand "giant pint" or "giant" as the giant-pint. Understand "pint" as the giant-pint when the pint is marked invisible. The description of the giant-pint is "There might seem to be some inherent contradiction about having a giant version of some standard measure: might as well say 'a very long inch' or 'an extra-heavy pound'. Nonetheless, popular imagination apparently construes GIANT PINT to mean an outsized beer stein. Beer not included."

The gin is an edible contained fluid thing. The scent-description of the gin is "herbs and juniper". The description of the gin is "Though clear and inert, it gives off a dangerously heady whiff of alcohol." The indefinite article of the gin is "some".

The gin-crock is an openable closed container. The description is "It's a sturdy jug with a stopper, suitable for containing the results of home distillery." The printed name of the gin-crock  is "gin crock". Understand "gin crock" or "crock" as the gin-crock. Understand "gin" as the gin-crock when the gin is marked invisible. The heft of the gin-crock is 2.
	The scent-description is "alcohol".

The description of the god is "A small, heavy metal figurine representing some pagan deity. He carries a bow and quiver, and looks prepared to use them." Understand "metal" or "small" or "heavy" or "figurine" or "bow" as the god. The heft of the god is 2.

The description of the hackle is "A long feather, as from the neck of a giant bird." Understand "feather" or "quill" as hackle.

A strand is privately-named. The description of a strand is "A single strand, about a foot long, of blonde hair." The heft of a strand is 1. The printed name of a strand is "hair". Understand "strand of" or "strand" or "hair" as a strand.

Rule for printing the plural name of a strand:
	say "hairs".

The printed name of the red hair is "red hair". The description of the red hair is "A single strand, about a foot long, of red hair."

The description of the red hairpiece is "A fairly natural-looking extension, red in col[our], attachable by comb. It [if worn]comes[otherwise]should come[end if] to about shoulder length on us."

[Again, there is no gameplay reason whatever for the procedural element here, but it feels more in keeping with the rest of the game to have it this way than to use a static list.]
The genre apron is a wearable thing. The description of the genre apron is "It is an ordinary apron, but lists across the front [i][genre-list][/i] and so on."

To say genre-list:
	sort the table of genres in random order;
	repeat with index running from 1 to 8:
		if index is less than 5:
			choose row index in the table of genres;
			say "[noun entry] - ";
		otherwise if index is less than 7:
			let offset be a random number between 3 and index;
			decrease offset by 1;
			let old-index be index minus offset;
			choose row old-index in the table of genres;
			say "[one of][soft-adjective entry][or][adjective entry][at random] ";
			choose row index in the table of genres;
			say "[noun entry] - ";
		otherwise:
			let offset be a random number between 3 and index;
			decrease offset by 1;
			let old-index be index minus offset;
			choose row old-index in the table of genres;
			say "[soft-adjective entry] ";
			increase old-index by 1;
			choose row old-index in the table of genres;
			say "[adjective entry] ";
			choose row index in the table of genres;
			say "[noun entry] - ";

Table of Genres
noun	adjective	soft-adjective
"romance"	"[one of]wedding[or]true-love[at random]"	"romantic"
"erotica"	"[one of]kink[or]fetish[or]soft-core[at random]"	"[one of]sensual[or]erotic[or]pornographic[at random]"
"mystery"	"[one of]police-procedural[or]crime lab[or]medical-investigation[at random]"	"[one of]forensic[or]suspenseful[at random]"
"fantasy"	"[one of]unicorn[or]druid[or]dwarf[or]dragon[or]wizard[or]elf[at random]"	"[one of]pagan[or]magical[or]fantasy-based[at random]"
"steampunk"	"[one of]Babbage engine[or]airship[at random]"	"[one of]steampunk[or]Victorian[at random]"
"science fiction"	"[one of]space[or]time-travel[or]nanobot[or]cyberpunk[at random]"	"[one of]futuristic[or]high-technology[at random]"
"[one of]thrillers[or]horror[at random]"	"[one of]werewolf[or]vampire[or]paranormal[or]undead[or]zombie[or]ghost[at random]"	"[one of]suspenseful[or]scary[at random]"
"[one of]farce[or]comedy[or]satire[at random]"	"[one of]clown[or]mime[or]slapstick[at random]"	"[one of]satirical[or]parodic[or]comic[at random]"
"cookbooks"	"[one of]food-centric[or]cooking[at random]"	"cook's own"
"self-help"	"[one of]pop-psych[or]therapeutic[or]guardian angel[at random]"	"[one of]religious[or]inspirational[or]new age[at random]"
"travel guides"	"travel"	"travelogue-style"
"[one of]letter collections[or]biography[at random]"	"epistolary"	"epistolary"
"choose your own adventure"	"[one of]branching-narrative[or]choice-based[or]choose-your-own[at random]"	"choice-based"
"[one of]textbooks[or]nonfiction[at random]"	"documentary"	"[one of]illustrated[or]photo-illustrated[or]historically accurate[at random]"
"[one of]literature[or]novels[or]short stories[or]novellas[or]anthologies[or]fiction[at random]"	"[one of]modern[or]contemporary[or]urban[at random]"	"[one of]surrealist[or]cosmopolitan[or]urban[or]dark[at random]"

Test genres with "tutorial off / x apron / g / g / g / g / g" holding the genre apron.

[and since we're making this anyway, we might as well add the capacity to reference genres elsewhere...]
To say one-genre:
	if a random chance of 1 in 3 succeeds:
		choose a random row in the table of genres;
		say "[soft-adjective entry] ";
	if a random chance of 1 in 2 succeeds:
		choose a random row in the table of genres;
		say "[adjective entry] ";
	choose a random row in the table of genres;
	say "[noun entry]";

The gilet is floppy and wearable. It covers the torso-area. The description is "A sleeveless padded jacket made of some sort of shiny army green nylon. Not my preferred look, to be honest." The heft of the gilet is 3.

The description of the gilt is "It's a single very thin sheet of beaten gold, which seems to want to stick to everything it touches. Best be careful with it." The heft of the gilt is 0. The indefinite article is "some".

A gimlet-drink is a contained fluid edible thing. The printed name of a gimlet-drink is "Gimlet". Understand "gimlet" or "cocktail" or "drink" or "beverage" as a gimlet-drink. The description of a gimlet-drink is "Lime juice, gin, and simple syrup: it's limeade with a difference."

The description of a git is usually "His face is fixed in a permanent sneer."
	The greeting of a git is usually "'[one of]Oh, you is it?[or]You again?[or]Still with the face like a barn door, you,[or]Haven't you got someone else to annoy?[at random]' says the git unpleasantly."

The grin is r-abstract. The description of the grin is "It hangs, smiling, in mid-air, with none of the Cheshire Cat to go with it."

The description of the grit is "It's a small quantity of fine sand, the kind that might be used for grinding or polishing things." The indefinite article of the grit is "some".

The hairband is wearable. It covers the head-area. The description is "It's blue, with felt flowers and rhinestones. I think it may be meant for a younger wearer than ourself."

Every hairpiece covers the head-area. The description of a hairpiece is "A fairly natural-looking extension, the col[our] of our own hair, attachable by comb. It [if worn]comes[otherwise]should come[end if] to about shoulder length on us."

Sanity-check cutting a hairpiece:
	say "It's reasonably well-trimmed as it stands. I wouldn't trust myself to re-style it." instead.


The harp limit is an r-abstract thing. The heft is 0. The description of the harp limit is "In its presence, no more than two pedal harps can be played (or a single double chromatic harp)."

The hat is wearable. The description of the hat is "It's a top hat, but one that doesn't take itself quite seriously; the sort of hat used to conceal rabbits in magic shows."
	The scent-description of the hat is "damp felt".
	Every hat covers the head-area.

The description of the hatband is "Grosgrain ribbon, dark purple, with a purple and silver rosette pinned to one side. It looks like it belongs to someone from a barbershop quartet." The scent-description of the hatband is "pomade".

Sanity-check wearing the hatband:
	say "Setting aside the question of a suitable hat on which to wear it, this article is not exactly what one wears when trying to be inconspicuous." instead.

The initial appearance of a hart is usually "A hart stands a few feet from you, looking at you intently. There is nowhere in the room to hide, though I suspect both you and the creature would be grateful to get away, just at the moment." The description of a hart is usually "It's rather frightening, since it plainly isn't tame, but it doesn't seem to have any immediate intent to rush at us."

The hairstick is a wearable thing. It covers the head-area. The heft is 1. The description is "Formally indistinguishable from a chopstick, but intended to hold buns together and otherwise do exciting things with long hair. This would work better if we had two of them.

This particular hairstick is stamped at the end with a tiny but ornate DCL sigil, which puzzles me for quite a while until I recollect that DCL used to (and for all I know still does) have a very active trade partner in Hong Kong."

[Check wearing the wig when the player is wearing the hairstick:
	try taking off the hairstick;
	if the player is wearing the hairstick:
		stop the action. ]

The heel is a thing. The description is "Calloused and leathery, a gruesome souvenir of years of unhappy shoe-wearing."
Sanity-check digging in heel:
	say "Oh, I think we can both be stubborn when we have to." instead.


[The herds are a neuter animal. The heft of the herds is 8. The description of the herds is "Cows, goats, and sheep, all mingled together and furious to be thus manifested without a shepherd."

Every turn when the herds are enclosed by the location:
	say "Something spooks the herds [--] not surprising, as there is scarcely enough room here for them to move. The cows try to go one direction, the goats another, and [we] [are] dragged underhoof.";
	end the story saying "That was unwise".]

The ho is a woman. The description of the ho is "She looks to be in her mid-twenties and is wearing a tight, neon blue outfit that makes her look like a naked water nymph."

The hod is long and strong. The description of the hod is "It's a three-sided box on a stick, for carrying bricks and mortar. It looks grubby and not especially convenient, but manual lab[our] isn't my area of expertise."

The description of the hoe is "It's a common gardening implement, perhaps a little larger than usual."

The description of the hooting owl concealer is "A cunningly made blanket designed to fit snugly over an owl, camouflaging it and silencing its calls. The outside of the blanket is painted with a pattern of pine branches, and the inside is quilted with a thick, sound-muffling insulation."
	The scent-description of the hooting owl concealer is "pine forests and snow".

The hopping bag is an openable closed transparent container. The description is "A small bag made of fine mesh, used for dry hopping as part of the brewing process." The carrying capacity of the hopping bag is 2.

Check inserting something into the hopping bag when the heft of the noun is greater than 1:
	say "[The noun] won't fit into such a small bag." instead.

The description of the hoses is "There are at least a half-dozen of them, spraying water in every direction as though the concept of conservation had never been invented."

Test hoses with "wave r-remover at fountain / wave r-remover at horses / get hoses / wave r-remover at horses / get hoses / x fountain / wave s-remover at hoses / x hoe / get hoe / x fountain / wave e-remover at hoe / x fountain / autoupgrade / wave e-remover at hoe / x fountain / get ho" in the Park Center.

Some hot-pink-clan are a woman. The heft is 7. The printed name is "hot pink clan". Understand "hot" or "pink" or "clan" as the hot-pink-clan. The description is "A huddle of nearly-identical anime girls with hot pink hair. They are watching us with large eyes."
	The greeting of the hot-pink-clan is "'Hello!' 'Hi!' 'Greetings!' The clan choruses and giggles, and we can't pick who is who."

The hurt is r-abstract. The heft of the hurt is 0. The description of the hurt is "It's an abstract depiction of hurt, currently represented by [one of]a bruise[or]a broken collar bone[or]a splinted finger[or]a torn ligament[at random]."


Section 7 - I through L

The printed name of a self-object is "I". Understand "I" as a self-object. The description of a self-object is "It's an abstract that looks like a bit of mirror. When I look at it I see Alex, my proper self, and not just restored to my usual body, but photo-retouched to be that bit smarter and freer of skin-blemishes, well-dressed. It's like looking in the mirror at reality as it should be.

You doubtless see yourself too [--] as you are, or perhaps as you want to be." Rule for printing the plural name of a self-object: say "Is". The self-object is r-abstract.

Test ibug with "tutorial off / autoupgrade / wave p-remover at pill / wave l-remover at ill / wave i-remover at I" holding the pill.

The ice-wig japes are an r-abstract thing. The printed name is "ice wig japes". Understand "ice wig" as the ice-wig japes. The description is "It displays, in miniature, some kind of comedy routine involving a set of ice skaters passing back and forth a huge wig also made of solid ice, which they take it in turns to wear (no doubt uncomfortably) over their caps."

The plural-ills are r-abstract. The description of the plural-ills is "They manifest as little balls of malevolence, like the critters that flew out of Pandora's box in a children's illustration." The printed name of the plural-ills are "ills". Understand "ills" or "ill" as the plural-ills.

An ill is r-abstract. The description of an ill is usually "It manifests as a little ball of malevolence, like a palm-sized thundercloud. It looks like it's a generic item from phrases like 'to wish someone ill,' which is a mercy; I don't think I would have liked manifesting illness, even abstractly.

Periodically it gains cartoonish eyes and eyebrows, and glowers at [us]."

The in-object is r-abstract. The printed name is "in". The description is "It represents the idea of having a connection or a way in to a normally closed institution. What my father would call networking and Slango would call nepotism, and my mother would have no name for at all because to her it is the normal way of the world everywhere.

At the moment, this concept is embodied as a pair of giant hands clasped in a handshake."

A dangerous construction rule for the in-object:
	say "True to its nature, the in pulls [us] in like a moth to a flame, then crushes [us] to pulp in its firm grasp.";
	end the story saying "At least [we] [are] truly in now";
	rule succeeds.

The description of the inapt sign is "The sign reads WARNING: [one of]DO NOT CLIMB DOME DURING THUNDERSTORM[or]WATCH OUT FOR BEARS[or]CARS PARKED WITHOUT A STICKER WILL BE TOWED AT OWNER'S EXPENSE[or]MUSHROOMS MAY BE POISONOUS[at random]."

The inapt sin is r-abstract. The heft of the inapt sin is 0. The description of the inapt sin is "It displays a very puritanical man praying on a street corner: the sin of spiritual pride, perhaps."

The inapt tint is r-abstract. The description of the inapt tint is "The tint hovers in the air, a translucent vibrating cloud, and always manages to take on just the col[our] that will most disagree with its background."

An ink is usually contained and fluid. The indefinite article is "some". The description is usually "It's a blue-black ink derived from squid or cuttlefish, in a small bottle."

Understand "write [text] with [ink]" as a mistake ("Ink like this requires the right kind of pen; we neither have nor need such a thing.").

The infertile astrologer is a woman. The description of the infertile astrologer is "She's a woman with long grey hair, well past menopause." Understand "woman" as the infertile astrologer.
	The greeting of the infertile astrologer is "'This meeting was predicted in the stars,' the astrologer informs [us]."

Every turn when the infertile astrologer is in the location:
	if a random chance of 1 in 2 succeeds:
		say "[one of]'Mercury is in retrograde,' comments the astrologer. 'Expect disordered communications.'[or]The astrologer looks at [us]. 'Gemini, am I right?'[or]'Today is a day of revelation,' announces the astrologer.[or]'We concern ourselves with the colonial tents,' the astrologer admits.[in random order]".

The printed name of the intaglio-pins is "intaglio pins". Understand "intaglio" or "pins" or "pin" as the intaglio-pins. The intaglio-pins are wearable. The description of the intaglio-pins is "A collection of crystal pins, with cameos of great figures carved into the backs of the crystal to give the impression (from the front) of a bust in low relief. They look like elegant antiques, if not worth a hundredth the value of the paintings."


The printed name of the intaglio-pin is "intaglio pin". Understand "intaglio" or "pin" as the intaglio-pin. The intaglio-pin is wearable. The description of the intaglio-pin is "A single crystal pin, with a cameo of Jon Rosehip carved into the backs of the crystal to give the impression (from the front) of a bust in low relief."

The printed name of the intaglio-pint is "intaglio pint". Understand "intaglio" or "pint" as the intaglio-pint. The intaglio-pint is a container. The description of the intaglio-pint is "A crystal pint glass etched with the face of famous Atlanteans. It is the sort of thing you can by at a souvenir shop if you have a lot more money than sense."

The indefinite article of the ire is "some". The description of the ire is "It manifests as a jaggy haze, through which [we] can see images of [one of]the triplicate Bureau paperwork required to dig foundations for any new building (requiring firm proof that no unwelcome archaeological discoveries will result)[or]the price tag on a bottle of Russian vodka around here[or]a postcard from traveling friends, in which the Bureau has carefully black-inked out everything except the photo[at random].". The ire is r-abstract.


Rule for clarifying the parser's choice of the it-object: do nothing instead.

The printed name of an it-object is "it". Understand "it" or "pronoun" as an it-object. The description of an it-object is "It's itself. It. The epitome of itness. Philosophers would pay millions." An it-object is r-abstract. Rule for printing the plural name of an it-object: say "it".

When play begins (this is the setting up it and tit rule):
	let target be a random it-object;
	let second target be a random tit;
	move target to repository;
	move second target to repository. [This should guarantee tit/it toggles frequently.]

The jack is a long strong thing. The description is "A heavy-duty jack, suitable for raising cars or other substantial objects."

[add: use jack on portcullis, put jack under portcullis, jack portcullis, raise portcullis with jack, raise portcullis]

The jackass is a man. The description is "He's a middle-aged fellow with a yellowing smile. His eyes are unfriendly, and he's looking [us] over very suspiciously. Taking the least charitable possible view of everyone else's motives, then going off on offensive rants: that'll be his personal specialty."
	The greeting of the jackass is "'Suppose you brought me here to hit me up for money,' says the jackass sourly. 'Might've known.'"

The jigsaw is a device. Understand "tool" as the jigsaw. The description is "It's a portable power saw with the ability to slice right through wood or plastic. Dewalt cordless, if you're interested, with a massive battery."

Carry out switching on the jigsaw:
	now the jigsaw is noisy.

Carry out switching off the jigsaw:
	now the jigsaw is not noisy.

Instead of listening to the jigsaw when the jigsaw is switched on:
	say "There is a continuous angry whine from the jigsaw."

Table of Ultratests (continued)
topic	stuff	setting
"jigbug"	{ jigsaw, keycard }	Johnson Basement

Test jigbug with "tutorial off / unmonkey / w / turn on jigsaw / w / drop jigsaw / w" [holding the jigsaw and the keycard in the Johnson Basement.]


The jigsaw-piece is a thing. Understand "jigsaw piece" or "jigsaw" or "piece" as the jigsaw-piece. The printed name is "jigsaw piece". The description is "A single mislaid piece from a jigsaw puzzle. It shows part of a woman's face contorted with strong emotion."

The keycard is a passkey. It unlocks the small door. The description is "An electronic pass card with a powerful-looking stripe down the back side." Understand "electronic" and "card" and "powerful-looking" and "stripe" and "powerful" as the keycard. The heft of the keycard is 1.

Carry out taking the keycard:
	record "acquiring a keycard" as achieved.

The key-lime is an edible thing. The printed name is "key lime".  Understand "key" or "lime" as the key-lime. The description is "One of those small, extra-flavorful limes used as a flavor for pie."
	The scent-description is "sharp and citrusy".
	The flavor-description is "The outer layer of skin tastes a little bit sharp, but most of the flavor is locked inside.".

Instead of squeezing the key-lime:
	say "It is firm and juicy, but not squishy. Not over the hill yet."

The keyring is a portable supporter. The heft is 1. The description is "It's a souvenir keychain. The tag is printed with the outline of Anglophone Atlantis, and reads: 'Atlantis: Friendly to Business [--] Friendly to Life.'"

Instead of putting something which is not the gel on the keyring:
	say "Only keys can really be attached."

The kind ladies are a plural-named woman. The description of the kind ladies is "They appear extremely earnest and helpful [--] some maternal, others more like that well-meaning aunt who is always asking if you need help with things because she really hasn't got anything to do at home."
	The greeting of the kind ladies is "They look at [us] and wave, regal little waves."

The kudu is a neuter animal. The description of the kudu is "It's a creature like an antelope, with tall, twisting horns and a little white tuft of beard."

Every turn when the kudu is in location:
	say "The kudu looks at us cleverly for a moment and then bounds away.";
	now the kudu is nowhere.

[The heft of the lab-thing is 10. The description of the lab-thing is "A full-sized scientific research laboratory, one of the many owned by Dental Consonants Limited." The printed name of the lab-thing is "lab". Understand "lab" or "laboratory" as the lab-thing.]

The lam is r-abstract. The description of the lam is "The abstract image of being on the lam. A pair of leg-irons lies discarded on the ground. A man runs through the woods. A marshall organizes a team to follow him. There's a sequence involving a chase through sewers and another involving some fake identification. It all feels a little familiar for comfort."

The lamb is an animal. The description of the lamb is "He is white and fluffy, with a surprisingly long tail." The heft of the lamb is 3.

The description of the lamb-granulate is "One small nodule of dried concentrated lamb substance." The printed name of the lamb-granulate is "lamb granulate". Understand "granulate" as the lamb-granulate. Understand "lamb" as the lamb-granulate when the lamb is marked invisible.

The lap is r-abstract. It is an enterable supporter. It is portable. The description of the lap is "Reified rather non-specifically as 'something to sit on,' it comes out looking like an especially comfortable seat."

The lappet is a wearable thing. The description of the lappet is "A sort of collar with long droopy flaps, like women wear in very old photos."

The larger-pin is a wearable thing. The heft of the larger-pin is 2. The printed name is "larger pin". Understand "larger" or "larger pin" as the larger-pin. Understand "pin" as the larger-pin when the pin is marked invisible. The description is "It's a clunky, substantial piece of costume jewelry, made of brassy metal and enamel, ca. 1958. It depicts the letter T inset in a turning gear. At the bottom is the motto 'DCL WIFE.'"

The lass is a woman. The description of a lass is "She is dressed like a peasant and looks Scottish, but cheerful. She is not especially wee, but I suppose she answers to bonny."
	The greeting of the lass is "She says hello in her most Scottish possible accent."

The description of the leaf is "An outsized tapering metal leaf from the olive branch of the statue of Atlantida. If the edges were sharper, it would resemble a curved dagger blade from some nomadic culture.". The heft of the leaf is 2. The leaf is long and strong.

The leap is r-abstract. The description is "A slightly fuzzy moving image of a ballerina performing the final grand leap from 'The Builders of Babel,' right before the lightning strikes."

The description of the lease is "A contract describing the terms of rental for some waterfront property east of the docks. Apparently large portions of this land are owned by the Bureau and leased out as long as no one digs there deeper than ten feet."

The description of the leases is "A sheaf of paperwork that might have come from any realtor on the island, detailing terms for six-month, one-year, or two-year contracts for various homes and office buildings." Understand "paperwork" as the leases.

The description of the lee is "It is the sheltered place beside a wall, or under a rock, or in the shadow of a tree: a place where the sun is not too hot, the rain not too driving, the wind not too strong.

On its own, without any wall, rock, or tree, the lee is a pathetic-looking object, but nonetheless useful." A lee is enterable and open. A lee is not openable. A lee is r-abstract.

The flexible appearance of the lee is "The lee is only discernable as a kind of shadow and stillness of the air, but it is definitely here. ".

Report dropping the lee:
	say "[We] set up the lee against the nearest wall." instead.

Rule for describing the interior of the lee:
	say "From inside the lee, all the noises around us are slightly hushed, and the air close to body temperature."

The description of a leer is "It is a good-hum[our]ed, slightly-drunk kind of leer, but it's unmistakable." The leer is r-abstract.

Instead of giving or showing the leer to a woman:
	say "[one of][The second noun] looks very far from interested.[or]'In your dreams,' mutters [the second noun].[at random]".

Instead of giving or showing the leer to a man:
	say "Somehow the effect just doesn't work."

The description of the legend is "This legend has been edited from its original form, replacing the words like HOSPITAL and RESTROOM with other, hand-written labels: INVITE, GOT, 11, OUT, T-INSERTER, etc. By arranging the words of the legend in the order in which the icons appear along the main street, we arrive at GOT INVITE T-INSERTER DEMONSTRATION. OUT 11 AM."
	The introduction of the legend is "Of course, it's already long past 11 AM, so Brock's plan clearly went wrong. If we're going to go after him, we'll have to go to the T-inserter demonstration ourselves. The catch is that, as dangerous new technology, it's held inside the Bureau and requires a special invitation to enter.

I think I know where we can get one, though. My advisor Professor Waterstone [--] that's the person overseeing my graduate research [--] was also invited to this demonstration. We might be able to get his."

Understand "message" as the legend.

Before examining the legend when the legend is not introduced:
	complete "Find a way to understand the map";
	assign "Speak to Professor Waterstone and get his invitation to see the T-inserter";
	assign "Gain entrance to the Bureau";
	assign "Find Brock";
	continue the action.

To say figure-out-legend:
	complete "Trace Brock's movements at the antique shop";
	assign "Find a way to understand the map".



The lie is r-abstract. The description of the lie is "It changes from moment to moment. Currently it reads: [line break][relevant lie][if rule succeeded][one of][paragraph break]Hm. Awkward item, isn't it? Make a nice conversation piece for the home. Set it on the mantelpiece and watch it gossip about your guests.[or][stopping][otherwise][one of][paragraph break]Not very inventive of it.[or][stopping][end if]"

[Originally there were a bunch of other randomized lies that said untrue things about the world model. But these were not really as entertaining as the pointed, custom-rolled lies, so eventually I deleted the others.]
To say relevant lie:
	say italic type;
	say "[one of]This mission is going without a hitch[or]The two of you are very well-suited co-conspirators[or]Alex has no qualms about Andra's moral [if the player wears the Britishizing goggles]fibre[otherwise]fiber[end if][or]Andra is absolutely not thinking of stabbing Alex in the back later[or]Brock has no feelings for Andra[or]Slango is a wise and harmless father-figure[or]Andra misses her parents[or]There won't be any difficulty in separating you two later[or]Person-synthesis is completely safe[or]I am carried by a sensible person[or]You wouldn't ever believe any of the things I say[or]You will definitely get away with this[at random].[no line break]";
	say roman type;
	rule succeeds.


The printed name of the lime-powder is "lime". Understand "powder" or "lime" as the lime-powder. The description of the lime-powder is "A heap of dry chalky powder." The indefinite article of the lime-powder is "some".

The litotes is an r-abstract thing. The description is "A not unimpressive figure of speech."

	Sanity-check smelling litotes:
		say "The litotes is not unfree of scent." instead.
	Sanity-check drinking litotes:
		say "Litotes is not unlike a gas or form of air." instead.
	Sanity-check attacking litotes:
		say "Fine. Litotes is a wordy rhetorical structure and can come off sounding over-fancy." instead.


The description of the live branch is "A substantial branch torn from some living tree. The leaves are green, delicate and tapering."

The loaf is edible. The description of the loaf is "Fancy white bread with caraway seeds in."

The louse is an insect. The heft of the louse is 1. Understand "lice" as the louse. The description of the louse is "Ick."


Section 8 - M through O

The description of a ma is "She's a Norman Rockwell figure: smiling in her apron, trying to keep up a perfect appearance while visibly exhausted from all her housework. Doesn't actually come with a child attached." Understand "figure" or "apron" as a ma.
	The scent-description of a ma is "soap".

The description of a mama is "Like a ma, only longer ago: not Norman Rockwell, but a Victorian Mama, in a long dark dress hiding a corset, her hair in a tight bun at the back of the neck." Understand "dress" and "corset" and "lady" and "bun" as a mama.
	The greeting of a mama is "'Well, hello to you, Ma'am,' says the mama."
	The scent-description of a mama is "carbolic soap".

The description of the mag is "It's a glossy magazine for photography enthusiasts on the island. There's a several page spread of tilt-shift images of the harbor, making it all look like a child's toy set." Understand "magazine" as the mag.

The description of the mall figurine is "It's a painted porcelain representation of a shopping mall. Delicate brushwork delineates the spaces in the parking lot, and the pharmacy sign has been modeled with special care." The mall figurine is fixed in place.

Sanity-check taking the mall figurine:
	say "Look, it's bad enough we've transformed Higgate's fav[our]ite figurines without then stealing them." instead.

The mare is a female animal. The description of the mare is "She's a handsome creature, black all over, and with a long mane. She seems to belong to a novel for young girls, or possibly a music video."

[The description of the mart is "One of those small markets selling mostly convenience foods and things that don't spoil quickly." The heft of a mart is 12.]

The description of a mat is "A woven jute mat, the kind people leave outside their doors for cleaning your shoes. It's got a traditional border pattern of chard leaves and curling waves running around the outside."

Matt is a proper-named man. The description of Matt is "He's tall and lanky, apparently in his early thirties, casually dressed."

Mary is a proper-named woman. The description is "She's wearing a blue robe and a beatific expression."

The description of a May is usually "This is the sheet for the month of May, torn from a calendar. Someone has put a gold star on the 21st, and 'Dinner with the Shaplys' is penciled onto the 30th." The heft of the May is usually 1.

Meg is a proper-named woman. The description of Meg is "She's in her mid-sixties, with salt-and-pepper hair and a dry, rather sardonic smile."

The printed name of me-object is "me". Understand "me" as the me-object. The description of the me-object is "It's an abstract that looks like a bit of mirror. When I look at it I see Alex, my proper self, and not just restored to my usual body, but photo-retouched to be that bit smarter and freer of skin-blemishes, well-dressed. It's like looking in the mirror at reality as it should be.

You doubtless see yourself too [--] as you are, or perhaps as you want to be." The me-object is r-abstract.


The description of the member is "It is a figure of a member of the Committee to Establish an Orthodox Orthography. It is plastic, small enough to sit on our palm, and wears the stiff clothing and conservative hairstyle of about 1895. It looks slightly cross, as though the member has guessed its work would take another 15 years to complete."

The alterna-members are a naughty-sounding r-abstract thing. The printed name is "members". Understand "members" as the alterna-members. The description is "A group of floating green numbers, currently [a random number between 1 and 30], [a random number between 31 and 70], and [a random number between 71 and 100]: perhaps members of the set of integers, then?"

The alterna-member is a naughty-sounding r-abstract thing. The printed name is "member". Understand "member" as the alterna-member. The description is "A floating green number, currently [a random number between 1 and 50]: perhaps a member of the set of integers, then?"

The mile is r-abstract. The description of the mile is "It's odd: it doesn't look like anything, exactly, and moreover, it isn't even precisely [i]here[/i]. It's more as though we had an invisible mile-long measuring pole, extending from here into the far distance. The other end is not identifiable from here."

The mileage is r-abstract. The description of the mileage is "The mileage number stands at [car-mileage]."

The car-mileage is a number that varies.

Carry out going by a car:
	increase the car-mileage by 1.

The mirth pail is a container. The description is "A gleaming metal pail with a yellow smily face painted on the side. Just occasionally something like the sound of laughter issues from it." The scent-description of the mirth pail is "steel and daffodils". The mirth pail is noisy.

Check inserting something which is not the grin into the mirth pail:
	say "The mirth pail rejects [the noun]. Not funny enough, apparently." instead.

Report inserting the grin into the mirth pail:
	say "The grin grows, if anything, broader as we set it down in its new home. ";
	try listening to the mirth pail instead.

Instead of listening to the mirth pail:
	say "[one of]There's a disconcerting chuckle[or]The pail laughs uproariously[or]The pail giggles[or]Snickers come out of the pail[at random]."

The description of the modem is "It is just a single instance of the same sort of modem."

The monk corpse bonnet is a wearable thing. It covers the head-area. The description is "It's a pioneer-styled lady's bonnet, except that the cloth is patterned all over with images of hanged monks, their tongues lolling out."

The morning dress is a thing. It is floppy and wearable. The description is "An outfit of striped trousers and fancy coat, such as men sometimes wear to fancy weddings in the morning."

After waving the letter-remover at something creating the morning dress:
	now the morning dress is part of the clothing shops;
	continue the action.

The description of the mote is "Almost invisible, almost lost.". The heft of the mote is 0.

The description of the motes is "A stream of dust, as one sometimes sees caught in a shaft of sun.". The heft of the motes is 0.

The description of the motet is "It's some sort of choral piece. I can't identify it; but then I don't think Atlantis is long on music of this kind, and most of the foreign-language musical forms are carefully restricted on the island. Hard to become a Bach expert around here.". The motet is noisy. Understand "music" as the motet.

The description of the motets is "The music sounds like many people singing many different things at once.". The motets are noisy. Understand "music" as motets.

The motte is a portable supporter. The description of the motte is "Fortunately it has come in small model form rather than in full size. The motte is a substantial artificial hill, which would have been part of an early style of fortification. Artificial grass covers the mound." Understand "artificial grass" or "model" or "grass" as the motte.

Report putting the army on the motte:
	say "[We] arrange the army around the motte in position to storm up the sides of the hill.

It looks wrong, of course, because the motte belongs to medieval castle construction and the army is decorated for the late 19th, but this wouldn't trouble a small child, and we mustn't be deterred by it either." instead.

The mottes are a portable supporter. The heft of the mottes is 4. The description of the mottes is "A model of several artificial hills used for fortification. It looks like part of a children's toy-soldier set, but the other pieces are not included. Artificial grass covers the imaginary mounds." Understand "artificial grass" or "model" or "grass" as the mottes.

Report putting the army on the mottes:
	say "[We] arrange the army around the mottes in position to storm up the sides of the most significant hill.

It looks wrong, of course, because the motte belongs to medieval castle construction and the army is decorated for the late 19th, but this wouldn't trouble a small child, and we mustn't be deterred by it either." instead.

[In releases 1 and 2, there was an MRA, a men's rights activist, resulting from reversing the arm. But its description in retrospect seemed more mean-spirited than funny, and also not a very good fit for the story. So I took it out.]

The mu is an illegal r-abstract thing. The description is "μ. Just the letter, but that's a dangerous thing around here."

The mug is a container. The heft of the mug is 2. The carrying capacity of the mug is 2.
	The description of the mug is "It's a super-sized black mug with 'TEA INSERTION GROUP' on the side in crisp white letters. Must be an employee gift over at Dental Consonants Limited."

Check inserting something into the mug when the heft of the noun is greater than 2:
	say "[The noun] [are] too large to fit inside the mug." instead.

The multiple dropkick cancan is a noisy r-abstract thing. The description is "Legs. Kicking. Wild skirling music. Skirts. Rugby balls. Fragile objects. It's awfully frenetic. Left to itself it would do massive property damage.".

Instead of listening to the multiple dropkick cancan:
	say "[one of]The music of the multiple dropkick cancan starts out reasonably tame[or]The tempo of the multiple dropkick cancan is increasing[or]The multiple dropkick cancan emits a sound of crashing and banging in time to the music[or]Faster and faster the multiple dropkick cancan whirls through its tune[or]The multiple dropkick cancan ends with the sound of dozens of legs kicking dozens of fragile objects across dozens of Parisian night-clubs[or]From the multiple dropkick cancan comes a moment of silence[cycling]."

Mut is an r-abstract woman. The description is "She has the kohl-lined eyes and deep black hair of an Egyptian goddess; and if we look at her from the corner of the eye, we see not a woman, but a great white vulture, the mother of Egypt, the primordial force from which all else was made."
	The greeting of Mut is "She says something, but it comes out as much like bird cry as anything else."

The mutt is a neuter animal. The heft of the mutt is 3. The description is "It's a dog of no particular breed, covered in scruffy brown fur, slightly larger than the average Scotty dog."


The description of the mutual punch is "It's a very heavy, industrial hole punch, something from a 1950s office, for drilling through multiple pieces of paper at once."

Sanity-check inserting something into the mutual punch:
	if the noun is a notepad or the noun is a ream:
		try punching the noun instead;
	else:
		say "[The noun] will not slide into the punch." instead.

Punching is an action applying to one thing.

Understand "punch [something]" as punching when the mutual punch is marked-visible.


Sanity-check punching something:
	unless the noun is a notepad or the noun is a ream:
		say "[The noun] will not slide into the punch." instead.

Sanity-check punching the player:
	say "Our hand is too large to fit, which is just as well." instead.

Report punching something:
	say "[The mutual punch] drives a small, tidy hole through [the noun]." instead.

Table of Ultratests (continued)
topic	stuff	setting
"punching"	{ anagramming gun, bullets, jotter, preamps }	Equipment Archive

Test punching with "tutorial off / punch me / load gun / shoot punch / punch me / punch jotter / wave p-remover at the preamps / wave s-remover at the reams / put ream in punch" [in the Equipment Archive holding the anagramming gun and the bullets and the jotter and the preamps.]


The nap is a thing. The heft of the nap is 1. The description is "It's a wad of fuzz from cloth [--] similar to lint."

The description of a net is usually "A modest-sized net with a fine mesh, suitable for removing tropical fish from a tank."

The description of the nib is "Just the very end of an expensive fountain pen, the sort of thing important men used a hundred years ago. Something much like this must have been used on all those notes that went into composing the New Orthodox Orthography."

The description of a nit is "A little black dot almost too small to see. It'll be gone if we're not careful." The heft of the nit is 1.

The description of nitrate is "It's a soluble white powder and it smells funny, ammonia-like.". The indefinite article is "some".

The noted drum act is a noisy woman. The description of the noted drum act is "[if the drum kit is part of the noted drum act]The drummer has manifested along with her drum kit, all as some kind of unified construction[else]The drummer, deprived of her kit, is now standing around looking sullen and antisocial[end if]."

The drum kit is part of the noted drum act. The drum kit is an instrument. Understand "cymbals" or "high hat" or "drums" as the drum kit. The description of the drum kit is "There's at least one of every kind of drum I know of. I should add that I'm not an expert in drums."

A dangerous destruction rule for the drum kit:
	now the noted drum act is not noisy.

Instead of listening to the noted drum act when the drum kit is part of the noted drum act:
	say "[The noted drum act] [one of]offers an all-rhythm rendition of the opening to Beethoven's Fifth[or]beats a tattoo on the cymbals[or]makes startlingly inventive use of the high hat[at random]."

The description of the noted drum fact is "A half sheet of cream-col[our]ed printer paper on which someone has written, '[one of]A drum is a musical instrument that produces sound by means of vibration[or]Drums come in many shapes and sizes[or]A membranophone is a musical instrument whose sound comes from a vibrating stretched skin or membrane[or]The Blue Island Marching Band plays snare drums, tenor drums, and bass drums[at random].'"

The oaf is a man. The description of the oaf is "Great, blundering, with a big red nose and fists like hams."

The description of the oar is "It's a light sort of oar, almost a paddle. Still probably not the ideal thing for use with a kayak, but it'll do." The oar is long. The oar is strong. The heft of the oar is 2.
	The scent-description of the oar is "brine and wet wood".

The description of the oat is "A single dry cut oat." The heft of the oat is 1.

Instead of eating the oat:
	say "It's just one grain; not exactly a meal, even if I were feeling inclined to eat something right now."

The oboe is an instrument. The description of the oboe is "It's a reed instrument with a lot of metal hardware outside. Neither of us has ever learned to play, so lack the expertise to say whether it's a good or bad version of the instrument." Understand "instrument" or "reed" or "hardware" as the oboe.

Report performing something on the oboe:
	say "Untrained as we are, all [we] get to come out of the oboe is an embarrassing duck-blatt." instead.

Report improvising the topic understood on the oboe:
	say "Untrained as we are, all [we] get to come out of the oboe is an embarrassing duck-blatt." instead.

Understand "blow [oboe]" as performing vaguely on.

Table of Ultratests (continued)
topic	stuff	setting
"oboe"	{ anagramming gun, bullets }	Generator Room

Test oboe with "hard mode / y / tutorial off / load gun / look / open station / get balm / shoot balm / put lamb in bucket / x oboe / blow oboe / play oboe / play music on oboe / play chopsticks on oboe" [in the generator room holding the anagramming gun and the bullets.]

The description of the ode is "A short poem, letter-pressed attractively on a sheet of thick paper. It is entitled 'Our Ancestors, The Immortal Spirits of the Pyramids,' a fact which disinclines me to study the rest." Understand "poem" as the ode. The scent-description of the ode is "paper".

Instead of searching the ode:
	say "A quick survey of the first verse reveals two metrical errors and a very dodgy rhyme of 'Ra' and 'car.'"

Instead of listening to the ode:
	say "It is mercifully silent. I don't think reading it aloud would improve my opinion of the thing."

Instead of frowning at or laughing at the ode:
	say "Yes, I share your critical sentiments exactly."

Instead of smiling at the ode:
	say "[We] smile derisively."

The description of the odes-book is "A slender volume of poetry, bound between red covers. It looks completely harmless and is written in English.

The blurb on the back describes it as a 'collection of meditations' on Atlantis['] imaginary roots, which is to say, a lot of stuff about magic-wielding Phoenicians, Knights Templar, refugees from the Tower of Babel, space aliens, cabalistic mysticism, and Lully's combinatorics[one of].

It fits in perfectly with the rest of Lena's selection. Really amazing work. I've seen other people who were able to do impressive things with local field distortions [--] think hard enough and you can prejudice the outcome of a linguistic transition.

But this... this is detail work like I haven't seen. My hat is off.

Or would be if I were wearing a hat and taking it off didn't mean making you take it off too[or][stopping]."
	The scent-description of the odes-book is "old book".
	The odes-book is plural-named.
	Understand "poems" or "poetry" or "book" as the odes-book.

The odes-book is a book. The contents of the odes-book is the Table of Poem Listing. The printed name of odes-book is "odes". Understand "book" or "odes" as the odes-book.

Table of Poem Listing
topic	reply
"magic" or "magic-wielding" or "Phoenicians"	"The poet sees the invention of the alphabet as a magical feat, one that somehow tied in with navigation abilities and the capacity to read the stars. Thanks to these twin magical abilities (apparently), the Phoenicians were able to reach the shores of Atlantis."
"knights" or "templar" or "knights templar"	"I'm not quite sure, because the language is deliberately obscure, but I think it's meant as an indictment of religious avocation. But I'm not sure. It could mean just the opposite. There's a lot about shining arm[our] and metal and crosses and red and white."
"refugees/refugee" or "tower of babel" or "tower" or "babel" or "from" or "refugee/refugees from the tower of babel" or "refugee/refugees from tower of babel" or "refugee/refugees from the tower" or "refugee/refugees from tower" or "refugee/refugees from babel"	"Briefly explores the conceit that a few possessors of the true original language of the world escaped to Atlantis after the wreck of the Tower of Babel."
"space" or "aliens/alien" or "space alien/aliens" or "extraterrestrials" or "UFO/UFOs"	"I'm not sure the poet of this one was taking the task entirely seriously, because he suggests that UFOs landed in Atlantis and imbued the population with pseudo-telepathic abilities through a form of genetically modified chard."
"cabalistic mysticism" or "cabal" or "cabalism" or "mysticism"	"A long poem about golems and false things. I think it's meant for a warning about the result of calling abstracts to life."
"lully" or "lully's" or "combinatorics" or "combinatoric"	"Clever, strange, angular verse about the visions of Lully and the idea that all things could be constructed via a sequence of signs. The idea is that the universe was made by language, not vice versa."

The description of the officer is "A single, irate-looking officer."

The printed name of the offices-thing is "offices". The description of the offices-thing is "A whole configuration of cubicles: it's come out as small as possible and yet still takes up a substantial amount of space. It's frankly surprising that it didn't squash anyone."

[The heft of the Ohio is 20. The description of the Ohio is "All I get is a confused impression of factories and battleground politics before the whole thing comes crashing down on us."]

The description of an oil is "A can of what appears to be motor oil. It is sludgy and black." The indefinite article of the oil is "some". The scent-description of an oil is "mechanics and the summer time". The printed name of an oil is "oil". Understand "oil" or "can" or "motor oil" as oil.
	Understand "cans" as the plural of oil.
	Oil is fluid and contained.

Rule for printing the plural name of oil:
	say "cans of oil".

Instead of tasting oil:
	say "Once again, your ideas disturb me."

The description of the oil-painting is "Just one painting, but a very valuable one nonetheless, showing Ms Shaply as a young woman." Understand "oil" or "painting" as the oil-painting. The printed name of the oil-painting is "oil painting". The heft of the oil-painting is 5.

Check inserting the oil-painting into something which is not the t-inserter:
	say "[The noun] is much too large to fit inside [the second noun]." instead.

Check putting the oil-painting on something:
	say "The [oil-painting] is too large to fit on [the second noun]." instead.

The orc is a man. Understand "grunk" as the orc. The description of the orc is "He's dressed in the traditional smelly pants of an orcish pig farmer."
	The greeting of the orc is "'Orc happy to meet stranger,' says the orc."

The description of the orc-swords is "A heap of junky plastic fantasy swords, too flimsy to support even the most basic roleplaying purposes. Possibly they're a souvenir of some sort?" The heft of the orc-swords is 6. The printed name of the orc-swords is "orc swords". Understand "orc" or "swords" or "sword" as the orc-swords.

The orc-word is noisy. The printed name of the orc-word is "orc word". Understand "orc" or "word" as the orc-word. The description of the orc-word is "Something snarling and full of hard consonants. I think this must be from someone's constructed language, and whatever it is bears a resemblance to Klingon."

The heft of the ore is 3. The description of the ore is "A quantity of rough-hewn, rusty-col[our]ed rocks. Looks like we wound up with iron ore rather than anything more valuable." Understand "iron" or "rusty" or "rocks" or "rough-hewn" or "rusty-colored" or "rusty-coloured" as the ore.

An otter is a neuter animal. The description is "Sleek, black, whiskered, and somewhat out of place on dry land."
	The scent-description is "fish and sea air".

Every turn (this is the otter escape rule):
	if sea-view is marked-visible and the otter is in the location:
		now the otter is nowhere;
		say "The otter notices the proximity of watery freedom and makes a break for it. Your last glimpse is of a sleek black head bobbing among the waves."


The description of the oyster turd is "Gross.

Well, it's actually just a sort of slimy stuff, and there's not a lot of it, as oysters are small and have simple digestive systems.

But still, not what you'd put into a glossy brochure touting the miracles of letter manipulation."

The oyster turd is fluid.

Instead of taking the oyster turd:
	say "No way! It is too fluid to easily pick up anyway."

[The heft of the Oz is 20. The description of the Oz is "Look, we've got ourselves a shiny new Australia. It's pretty large, though, isn't it?"]

Section 9 - Pa

The description of a pa is "He's a Norman Rockwell figure: gruff, upstanding, honest, not a big talker. Doesn't actually come with a child attached, but it stands to reason he would have a large-ish brood of them." Understand "figure" or "man" or "guy" or "gentleman" or "pa-singular" as the pa.
	The scent-description of a pa is "shaving cream".
	The greeting of a pa is "'Hello there, young lady,' says the pa."

Rule for writing a paragraph about a pa (called item):
	say "[The item] you summoned is standing nearby, looking slightly confused";
	let N be the number of pas in location - 1;
	if N is 1:
		say ". Next to him is another pa";
	otherwise:
		if N is greater than 1:
			say ". Next to him are [N in words] other pas";
	let P be the number of plural-pas in location;
	if P is greater than 1:
		say ". Next to [if N > 1]them[otherwise]him[end if] are [P in words] entire groups of pas, standning separate";
	otherwise:
		if P is 1:
			say ". Next to [if N > 1]them[otherwise]him[end if] is a group of pas, standning separate";
	now every plural-pas in location is mentioned;
	now every pa in location is mentioned;
	say ".[paragraph break]".

After reading a command (this is the replace plurals rule):
	if the player's command includes "pas" and there is a plural-pas enclosed by location:
		replace the matched text with "plural-pas";
	if the player's command includes "pa" and there is a plural-pas enclosed by location and there is a pa enclosed by location:
		replace the matched text with "pa-singular";
	if the player's command includes "tents" and there are some plural-tents enclosed by location:
		replace the matched text with "plural-tents";
	if the player's command includes "tent" and there are some plural-tents enclosed by location and there is a tent enclosed by location:
		replace the matched text with "tent-singular";

Instead of subject-asking when the current interlocutor is a pa:
	say "'Huh,' he says, in that gruff pre-occupied way of his."

Instead of kissing the pa:
	say "[one of][We] plant a kiss on his slightly stubbled cheek. He reddens.[or][We] lean in for another, but he dodges.[or]Would you take off[ense] if I mentioned I find this all a bit uncomfortable?[or]He clearly does not want our girlish affections.[stopping]".

Instead of attacking the pa:
	say "If it came to a fight, he would win."

Report involuntarily-dropping a pa:
	say "[The noun] is very heavy, and winds up (before his transformation is complete) stepping on our feet, and then scrambling away (with apologies)." instead.

Test pas with "autoupgrade / wave l-remover at plans / wave n-remover at pans / wave a-remover at soap / put sop on spinner / put gel on sop / wave o-remover at soap / look"

Test pa with "autoupgrade / wave l-remover at plans / wave n-remover at pans / wave s-remover at pas / wave a-remover at soap / put sop on spinner / put gel on sop / wave o-remover at soap / look"

Table of Ultratests (continued)
topic	stuff	setting
"pas"	{ secret-plans, soap, tub }	Roget Close
"pa"	{ secret-plans, soap, tub }	Roget Close

The description of some plural-pas is "A whole fleet of gruff, upstanding, fatherly men." The printed name of a plural-pas is "pas". Understand "pas" or "pa" or "group" or "groups" or "fleet" as the plural-pas.
	The scent-description of some plural-pas is "shaving cream".
	The greeting of some plural-pas is "'Hello there, young lady,' one of them replies."

Rule for writing a paragraph about some plural-pas (called item):
	say "[The item] you summoned are standing nearby, looking slightly confused";
	let N be the number of plural-pas in location;
	if N is 2:
		say ". Next to them is another group of pas";
	now N is the number of pas in location;
	if N is greater than 1:
		say ". Next to them are [N in words] single pas, standning separate";
	otherwise:
		if N is 1:
			say ". Next to them is a single pa, standning separate";
	now every plural-pas in location is mentioned;
	now every pa in location is mentioned;
	say ".[paragraph break]".

Rule for printing the plural name of plural-pas:
	say "groups of pas".

Instead of subject-asking when the current interlocutor is a plural-pas:
	say "'Huh,' one of them says, in a gruff pre-occupied way."

Instead of kissing plural-pas:
	say "[one of][We] plant a kiss on the slightly stubbled cheek of one of them. He reddens.[or][We] lean in for another, but they dodge.[or]Would you take off[ense] if I mentioned I find this all a bit uncomfortable?[or]They clearly do not want our girlish affections.[stopping]".

Instead of attacking plural-pas:
	say "If it came to a fight, they would win easily."

Report involuntarily-dropping plural-pas:
		say "[The noun] are very heavy, and some of them wind up (before their transformation is complete) stepping on our feet, then scrambling away (with apologies)." instead.

[The description of PARC is "Palo Alto Research Center [--] tons and tons of 1970s-era high-tech plastic, steel, and concrete. At least the carp didn't turn into crap." The heft of PARC is 1000. The scent-description of PARC is "burnt electronics dust".]

[The description of the paint is "Bright blue paint, of the house-painting variety and not the thicker, more controllable kind used for art." The indefinite article is "some". ]

[Some pain is an r-abstract thing.
	The description is "It looks like the edge of a knife one moment, and the next like the red-hot element of an electric stove, and the next like a blunt object."

Instead of smelling the pain:
	say "It has the smell of burnt flesh."

Instead of touching the pain:
	say "It is searing, but only for a moment."]

The page-person is a man. The printed name is "page". Understand "page" as the page-person. The description is "A boy dressed in livery and a foolish bowl-shaped haircut."
	The greeting of the page-person is "The page bows to us obediently."

The description of the painting is "The painting shows Atlantida in a pose parodying the Mona Lisa: arms folded, mouth very faintly smiling. But her expression, far from being enigmatic or tranquil, is an alarming glare, and her eyes follow wherever [we] go."

The pal is a man. The description of the pal is "Our [pal] smiles back at us with friendly, boyish affection. There are whole summers' worth of innocence in that grin. [one of]Did you ever have a best friend, growing up? I didn't, really. I was more of a loner. But I liked that, I think.[or][stopping]".
	The greeting of the pal is "'Hey!' says the pal enthusiastically. 'You're here!'"

The i-pan is a container. The description of the i-pan is "An [if the player wears the Britishizing goggles]omelette[otherwise]omelet[end if]-sized skillet in cast iron."
	The printed name is "pan".
	Understand "pan" or "skillet" or "flat" or "cast" or "iron" as the i-pan.
	The heft of the i-pan is 3.

Some iron-pans are a plural-named container. The description of the iron-pans is "A set of flat skillets in cast iron: extremely bulky and heavy." The heft of the iron-pans is 5.
	The printed name is "pans".
	Understand "pan" or "skillet" or "flat" or "cast" or "iron" or "pans" as the iron-pans.

Report involuntarily-dropping the iron-pans:
	say "[We] can't reasonably carry the whole stack of pans, so they fall to the [ground] with a dramatic crash[if the Authenticator is in Fish Market and the location is Tin hut].[paragraph break]Let's hope she didn't hear that[end if]." instead.

Understand "put pans in/into pans" or "insert pans in/into pans" as a mistake ("Try as [we] might, their design and the placement of the handles makes stacking the pans nearly impossible.") when the iron-pans are marked-visible.

The pant is an r-abstract thing. The description of the pant is "The physical embodiment of a rapid breath of hot air. It looks like a cloud in your hand."

Some pants are a thing. The description of the pants is "They're khaki pants of a sexless, ageless variety." The pants are floppy and wearable. The pants cover the legs-area.

Instead of putting restoration gel on the pants when the player wears the pants:
	say "That might have some odd effects given that the pants are currently on our body."


The papa is a man. The description of the papa is "Like a pa, only longer ago: not Norman Rockwell, but a Victorian Papa, with lots of facial hair and a round stomach."
	The greeting of the papa is "'Well, well, bless me,' says the papa. 'Look who's here.'"
	The scent-description of a papa is "pipe smoke".

The papas are a man. The description of the papas is "A whole fatherly fleet, talking gravely and seriously among themselves about Gladstone and tobacco and the price of keeping a carriage these days."
	The scent-description of the papas is "pipe smoke".
	The greeting of the papas is "'Good to see you, my dear,' says one of the papas. 'But I doubt our conversation can interest you.' And he turns back to one of the others, speaking in a hushed voice of [one of]Disraeli[or]Mrs Brown[or]those scandalous deaths in Whitechapel[or]the installation of the underground trains[at random]."

The paper is a notepad. The description of the paper is "It contains a memo, dated to May of 1983. 'Attention,' it says. 'Due to subversive counter-propaganda altering the abstract concept, Subject A must NO LONGER BE GELLED AFTER USE, since reconstructing her may produce anomalous results. She will from now on be housed in a continuous living state in the historic apartments.'"
	The scent-description of the paper is "purple ink".

The description of the paper-model is "There's now just one model of the New Church remaining, but it's a doozy, with card stock pews and interior decor to assemble before you get to the outside portion." The printed name of the paper-model is "paper model". Understand "model" or "paper model" as the paper-model.

The description of the pastel bras is "They're pale pink with mint-col[our]ed straps, AA sized, and have tween girl's first foundation garment written all over them." The pastel bras are wearable. They cover the torso-area.

Sanity-check wearing the pastel bras:
	say "Please. I think we're a little more shapely than that." instead.

The patching roll is a long floppy thing. The description is "It's an industrial quantity of thin denim with a white backing. This stuff can be cut into pieces and ironed onto jeans and jackets to cover up rips."

The printed name of the patsy-woman is "woman". Understand "woman" as the patsy-woman. The description of the patsy-woman is "She looks timid and thoughtful, on her way to middle age, not extraordinary in any particular way. Certainly she doesn't look as you might imagine a criminal to look."

The description of the par is "Rather weakly represented by a golfing score-card, filled out." Understand "score" or "card" or "score-card" as the par. The par is r-abstract.

The description of the part is "It is made of dull grey metal, with an octagonal shape at one end and a hole for a screw or bolt at the other. I have no idea what it goes with. It might be a plumber's part, or a part for a car, or something else entirely.".
	The scent-description of the part is "metal".

The description of the pass is "A Bureau of Orthography visiting pass, a very valuable commodity in these parts. Forging one is grounds for imprisonment. There is a picture on the front that looks more or less like us, though with [if hardness is true]redder and [end if]considerably more hair."

The description of the passcard is "A variant of a passport: it has an image on the front that looks more or less like us, though with more hair, and the stripe on the back is imprinted, presumably, with various biometric information. It's no good at unlocking doors, however."
	The scent-description of the passcard is "plastic".

The passkey-thing is a passkey. The description of the passkey-thing is "Looks like an ordinary door key, but one which might be able to unlock every front door in some apartment building or residential area. There is no clue as to where, though, which makes it kind of useless."
	The printed name of the passkey-thing is "passkey". Understand "passkey" and "key" as the passkey-thing. The scent-description of the passkey-thing is "sweaty metal".

The passage is a notepad. The heft of the passage is 1. The description of the passage is "A short piece of prose neatly letterpressed onto a large sheet of paper. It reads:

[i]The alphabet is a system of interchangeable parts. The word form can be surgically revised, instead of rewritten, to become the word farm or firm or fork or fort or from, or with a little more trouble, to become the word pineapple. [--] Robert Bringhurst[/i]".

The passage-place is a thing. The printed name is "passage". Understand "passage" or "corridor" as the passage-place. The heft of the passage-place is 8. The passage-place is fixed in place. The description is "It's a dark, stone-lined corridor leading into the earth." The initial appearance is "A passage lined in stone descends into the face of [the programmable dais]."

[This portion is, again, totally an Easter egg: there's no need to go to the Shadow Chamber, and on the whole it's pretty unlikely that anyone will on the first try. However, it's a repository for palindromes and word puzzles of non-English and slightly superstitious origin, suggesting that some sense of magic attached to these even before linguistic efficacy was widely detected.]

The Shadow Chamber is a room. It is indoors, checkpoint and forbidden. The description is "Dim lights in the floor make it possible to navigate in here, though it's still fairly dark. The place is small and shabby, the air stale.[assign-amanda]".

[Somewhat hackish way to get an extra line break before the achievement message and avoid a double paragraph break after]
To say assign-amanda:
	let N be "Amanda Waterstone award for discovering cultic passages";
	unless N is a used achievement:
		choose a blank row in the Table of Possible Achievements;
		now the achievement entry is N;
		say "[paragraph break][first custom style]Achievement accomplished: [N]![roman type]";
		write File of Conclusions from the Table of Possible Achievements.

The Greek inscription is fixed in place in the shadow chamber.
The initial appearance is "Cold water flows from a crack in the wall. Above it, words are carved: ΝΙΨΟΝ ΑΝΟΜΗΜΑΤΑ ΜΗ ΜΟΝΑΝ ΟΨΙΝ."
The description is "The words are ΝΙΨΟΝ ΑΝΟΜΗΜΑΤΑ ΜΗ ΜΟΝΑΝ ΟΨΙΝ."
Understand "greek" or "words" or "carved" as the Greek inscription.

The sator inscription is fixed in place in the Shadow Chamber.
The initial appearance is "On the opposite wall is a square of inscribed letters, this time Roman."
The description is "It reads:

[fixed letter spacing]SATOR[line break]AREPO[line break]TENET[line break]OPERA[line break]ROTAS[variable letter spacing]".
		Understand "roman" or "letters" or "lettering" or "square" or "square of" as the sator inscription.

Rule for printing the name of the sator inscription when not shooting:
	say "Roman inscription".

Sanity-check examining a book in Shadow Chamber:
	say "There's not enough light to read comfortably." instead.

Sanity-check consulting a book about something in Shadow Chamber:
	say "There's not enough light to read comfortably." instead.

Some dim lights are scenery in the Shadow Chamber. The description is "They glow faintly orange and are just sufficient to give a sense of the place."

The secret-chamber-water is in the Shadow Chamber. The secret-chamber-water is fluid and scenery.
	The indefinite article is "some". The printed name is "water".
	Understand "cold/water" or "crack" as the secret-chamber-water.
	Instead of taking the secret-chamber-water, say "[We] can't, not having webbed fingers."
	Instead of drinking the secret-chamber-water, say "The water is cold and refreshing."
	The scent-description of the secret-chamber-water is "wet rock".
	The description of the secret-chamber-water is "The water flows from a crack in the wall and disappears down another crack in the floor. Above it, words are carved: ΝΙΨΟΝ ΑΝΟΜΗΜΑΤΑ ΜΗ ΜΟΝΑΝ ΟΨΙΝ."

The passage-exit is a door. It is scenery. The printed name is "passage". Understand "passage" or "exit" or "passageway" as the passage-exit. It is above Shadow Chamber. Through the passage-exit is the Workshop. The description of the passage-exit is "The way back to the Workshop is entirely dark."

Instead of entering the passage-place:
	say "[We] clamber down into the passage. It feels disconnected from other places, as though it didn't belong here at all. Soon we can't see the light from the door.";
	now the player is in the Shadow Chamber.

Instead of going down in Workshop:
	if passage-place is marked-visible:
		try entering the passage-place;
		the rule fails;
	if passage-place is seen:
		say "[The passage-place] is not here any more.";
	otherwise:
		continue the action.

Instead of going inside when passage-place is marked-visible:
	try entering the passage-place.

Table of Ultratests (continued)
topic	stuff	setting
"passage"	{ passage, anagramming gun, bullets }	Workshop

Test passage with "tutorial off / powerup / put bullets in gun / set switch to swap homonym / put passage on dais / turn on dais / enter passage / x nipson inscription / x sator inscription / shoot sator" [holding the passage and the anagramming gun and the bullets in the Workshop.]


[One of the things I set out to do with the puzzle system in my previous game Savoir-Faire was to duplicate all the dull standard puzzles I was sick of seeing, but with some new twist on them that hadn't been done before. The one that there was really no way to include, however, was the password puzzle: too many IF games use password puzzles as a form of riddle (where you have to guess on the basis of some hints) or a cheap search puzzle (where the answer is written down on a note near the computer). Never in IF does anyone follow good security protocols with strong passwords that aren't written down.

So, I thought, what about a puzzle where people do use fairly unguessable passwords, but the whole concept of a 'password' is compromised by some other law of the universe?]

The password-thing is an r-abstract thing. The description is "A glowing series of numbers and figures in the air, which changes and flickers now and then. Probably responding to its surroundings. It seems unstable just now." The printed name is "password". Understand "password" as the password-thing.

Report switching on a computer which is running a password-lock program when the password-thing is marked-visible:
	say "[The startup noise of the noun][paragraph break]";
	let target screen be a random screen that is part of the noun;
	try examining the target screen;
	say "[The password-thing] flickers meaningfully, then stabil[ize]s." instead.

Instead of examining the password-thing when a password-lock program (called target program) is marked-visible:
	say "Under the influence of [the random computer which is running the target program], the password has settled down and now reads '[password of the target program]'."

Understand "type [password-thing] on/into [keyboard]" as password-entering it on. Understand "type [password-thing] on/into [something]" as password-entering it on.

Understand "enter [password-thing] on/into [keyboard]" as password-entering it on. Understand "enter [password-thing] on/into [something]" as password-entering it on.

Understand "type [password-thing]" as password-entering it on. Understand "enter [password-thing]" as password-entering it on.

Understand "type" as password-entering it on.

Rule for supplying a missing noun while password-entering (this is the guess the password while password-entering rule):
	now the noun is the password-thing.

Rule for supplying a missing second noun while password-entering something on (this is the guess a keyboard while password-entering rule):
	if the player can touch a keyboard (called target):
		now the second noun is the target;
	otherwise:
		say "You'll have to specify what you want to type on." instead.

Setting action variables for password-entering something on something which is not a keyboard:
	if the second noun is a computer:
		let the relevant keyboard be a random keyboard which is part of the second noun;
		now the second noun is the relevant keyboard;
	otherwise if the second noun is something which is part of a computer (called the relevant computer):
		let the relevant keyboard be a random keyboard which is part of the relevant computer;
		now the second noun is the relevant keyboard;

Password-entering it on is an action applying to two things.

Check password-entering something on something which is not a keyboard:
	say "[The second noun] [are] not a keyboard." instead.

Check password-entering the password-thing on something which is part of a computer (called secondary target):
	unless the secondary target is running a password-lock program:
		say "[The secondary target] [are] not asking for a password." instead.

Carry out password-entering the password-thing on something which is part of a computer (called secondary target):
	let chosen program be a random password-lock program which is run by the secondary target;
	say "[We] carefully key in '[the password of the chosen program]'. [run paragraph on]";
	carry out the rewarding successful answering activity with the chosen program;

The pastis is edible contained fluid. The description of the pastis is "An anise-flavored liqueur. It's an acquired taste, but now that I've acquired it, I like to exercise the acquisition as frequently as possible. [one of][i]You[/i] may not be as big a fan, for which I apolog[ize] in advance.[or][stopping]".

The scent-description is "anise".

Instead of tasting the pastis: say "It is strong but somehow calming."

Instead of eating the pastis, try drinking the pastis.

Instead of drinking the pastis:
	say "[one of][We] sip a little of the pastis: delicious and cooling![or]Better not drink all of it. This stuff goes to my head, and [we] still have a lot of work to do.[stopping]".

The description of the pasts is "It looks from the side like a shard of glass, but seen straight on, it captures previous events. At the moment it is replaying us [randomly doing something]." The pasts are r-abstract.

Instead of searching the pasts:
	try examining the pasts.

Instead of touching the pasts:
	say "It feels cool, hard, and smooth, like a piece of window glass."

Instead of attacking the pasts:
	say "It is beyond destruction."

Instead of tasting the pasts:
	say "The surface is cold and flavorless[one of]. Also, you just licked the memory of my head. So ew[or][stopping]."

Instead of listening to the pasts:
	say "Except as a metaphorical gesture, that is wholly unproductive: the shard doesn't replay conversation or do sound effects."

To say randomly doing something:
	choose a random row in the Table of Past Actions;
	say the chosen action entry.

Table of Past Actions
chosen action
waiting
with 25 blank rows.

Every turn (this is the blank out Table of Past Actions rule):
	unless the remainder after dividing turn count by 2 is 0:
		if the number of blank rows in the Table of Past Actions is 0:
			choose a random row in the Table of Past Actions;
			blank out the whole row;
		choose a blank row in the Table of Past Actions;
		now the chosen action entry is the current action.

A pat is usually edible. The description of a pat is "Considering everything it could have been, [we] [are] lucky with this pat: it is only a pat of butter." Understand "pat of butter" or "butter" as a pat. The heft of a pat is 1.
	The scent-description of a pat is "cool butter".

Instead of tasting the pat:
	say "Unsalted, I think."

Sanity-check eating the pat:
	say "I don't like butter without bread." instead.

The description of the pate is "A bit of bald head, fringed with just the hint of greying hair."

The patera is a container. The description of the patera is "A broad, shallow drinking dish. It is made of bronze and looks extremely old, as though it had been brought up from one of the forbidden archaeological sites on the island. Roman, perhaps."

The description of some pat-items is "Pats of butter, still wrapped in gold foil." The printed name of the pat-items is "pats". Understand "pats" or "butter" or "pats of butter" as pat-items. The heft of some pats is 3. The scent-description of some pat-items is "cool butter".



Section 10 - Pe

The description of the pea is "Just a single green pea.". The pea is an edible vegetable. The heft of the pea is 1.

The introduction of the pea is "If I weren't violently allergic to peas, I'm sure that would be much less menacing."

After eating the pea:
	say "Did I never tell you about my legume allergy? My mother was always absurdly protective because I nearly died when I was two. I think that explains a few things about my personality.

[We] can just hope the synthesis process has changed that about our body.

Are those hives? I can't breathe; can you?";
	end the story saying "[We] have succumbed to an allergen".

The peacoat is a floppy wearable thing. It covers the torso-area.
	The description is "A short double-breasted coat in navy blue wool, of a handsome unisex cut that looks vaguely nautical."

The peacock is a noisy bird. The heft of the peacock is 3. The description is "A glorious blue-green animal, with feathered crest and long, spreading tail." Understand "crest/tail" as the peacock.
	The initial appearance of the peacock is "[if the peacock is in the location]A peacock struts nearby[otherwise]A peacock regards us brightly[end if]."

Sanity-check burning the peacock:
	say "It's a peacock, not a phoenix." instead.

Instead of listening to the peacock:
	say "[one of]The peacock trills self-importantly[or]The peacock makes a practice noise in its throat[or]The peacock ruffles its many feathers[at random]."

The description of the peacod is "The thin outer shell of a pod of peas, from which the peas themselves have already been extracted." The heft of the peacod is 1. The peacod is edible.

Sanity-check opening or closing the peacod:
	say "There's not much point to further manipulation, as the peas are gone." instead.

The peal is noisy. The description of the peal is "It is the embodiment of a peal of bells: no weight or size, but the impression of bronze, early Gothic spires in a spare English countryside, and clangor."

Instead of listening to the peal:
	say "[one of]When you pay attention, the peal really is nearly deafening[or]The bells go on and on like a cheap alarm clock I once had[or]Clang, clang! goes the peal[cycling]." The peal is r-abstract.

The description of the pearl is "It is small, slightly uneven, and pale blue in col[our]. Not worth very much, but genuine.". The heft of the pearl is 1. Instead of tasting or eating the pearl: say "I've heard you can tell real pearls from fake by tasting them for grittiness, but honestly with this one I can't tell."

The pearl-ring is a wearable thing. The printed name is "pearl ring". Understand "pearl" or "ring" or "gold" or "diamond" or "diamonds" as the pearl-ring. The description is "It's a little old-fashioned, a pearl mounted in a swirl of gold and accented with tiny chip diamonds."

The description of the peat is "Dirty and smelly. [one of]You've had better ideas, really.[or]I assume this is going to be useful for something?[at random]".
	The indefinite article is "some".
	The scent-description of the peat is "a deep earthiness redolent of compost".


The description of the pen is "One of those old-fashioned fountain pens[if the nib is part of the pen] with a fresh nib attached[otherwise], though lacking a nib[end if]."


Understand "attach [nib] to [pen]" as inserting it into.

Understand "screw [nib] into/onto [pen]" as inserting it into.

Instead of putting the nib on the pen:
	try inserting the nib into the pen.

Before inserting something into the pen:
	if the noun is not ink and the noun is not the nib:
		say "[We] can't get [the noun] into [the pen]." instead.

Instead of inserting the nib into the pen:
	if the nib is part of the pen:
		say "The nib is in the pen already.";
	otherwise:
		now the nib is part of the pen;
		say "The nib screws in easily and is now part of the pen.";

Understand "detach [nib] from [pen]" as removing it from.
Understand "unscrew [nib] from [pen]" as removing it from.

Instead of turning the nib when the nib is part of the pen:
	try removing the nib from the pen.

Instead of removing the nib from the pen:
	try taking the nib.

Before taking the nib when the nib is part of the pen:
	now the nib is in the location.

Instead of filling the pen with ink:
	try inserting the second noun into the pen.

Instead of inserting the pen into ink:
	try inserting the second noun into the pen.

The pen can be inked or uninked.

Instead of inserting ink into the pen:
	if the nib is not part of the pen:
		say "Without a nib, the pen can't safely be filled; the ink will just flow out again." instead;
	now the pen is inked;
	now the noun is nowhere;
	say "We fill the body of the pen with ink and reattach the nib."

Table of Ultratests (continued)
topic	stuff	setting
"pen"	{ pen, nib, page, jotter }	Public Convenience

Test pen with "wave s-remover at sink / get ink / write on jotter / write with pen / write with pen on jotter / write on jotter with pen / write Alice on jotter / put ink in pen / put nib in pen / get nib / put nib in pen / put ink in pen / write on jotter / write on page / write alice on jotter / write alice on page"  [in the Public Convenience holding the pen and the nib and the page and the jotter.]

Carry out writing on something:
	record "Horace Fingerstain award for jotting some notes" as an achievement;

The description of the pens is "A handful of cheap plastic pens, the kind that offices buy so that employees won't be tempted to steal them."

Instead of inserting ink into the pens:
	say "These are the sort of pens that are meant to be thrown away as soon as they run out or the ink clots or something goes wrong with the tip, which is to say, almost instantly; that is, if the hapless user doesn't fancy using them to try to carve words into the surface of a legal pad instead.

They cannot be refilled. They are unacquainted with liquid ink."

The perl is r-abstract. The description of the perl is "A flickering of regular expressions and .pl filenames, translucent in the air. I never could see the appeal, but I have a friend in linguistic engineering who swears that regular expressions are the language God speaks."

The pests are an insect. The description of the pests is "The pests are a motley assortment of chiggers, mosquitos, bedbugs, ticks, and midges."

The pestos are edible. The description of the pestos is "Glass jars contain three types of local pesto: a traditional basil and garlic variety, a sun-dried tomato pesto, and the infamous squid-flake pesto. The jars are packaged in a glossy cardboard box, suitable for taking back to one's mother-in-law after a trip to Atlantis."
Understand "glass" or "jars" or "local" or "basil" or "garlic" or "tomato" or "pesto" or "sun-dried" or "squid-flake" or "glossy" or "cardboard" as the pestos.

The initial appearance of a pet is usually "Our [one of]new[or]recently-acquired[or]familiar[stopping] pet frolicks nearby." The description of a pet is "Unspecific as to species, but it has soft and gleaming fur; small, well-formed paws with tiny rose-col[our]ed nails; a twitching, sensitive nose; a tail; a clever look." The heft of the pet is 2.
The scent-description of the pet is "warm fur".

Report something exiting when the person asked is a pet:
	if the container exited from is the T-inserter, say "With a nervous glance at the injectors and nozzles, the pet clambers awkwardly out of the basket of the T-inserter and lowers itself to the [ground].";
	otherwise say "The pet scrambles out of [the container exited from].";
	stop the action.

The description of the petal is "Lush and fragrant and pale-cream with gold streaks." The scent-description of the petal is "rosewater". The heft of the petal is 1.

The description of the petcock is "A valve for controlling the fuel supply on a motorcycle[if open]. It is currently open, but as it's not attached to any supply line, this makes little difference[end if]." The petcock can be openable. The petcock can be open. The petcock is openable and closed.


Section 11 - Pf through Pn

The description of a pi-object is "For something so simple, it is astonishingly beautiful: a perfect circle in translucent silver, absolute and unflawed.". pi-object is r-abstract. The heft of a pi-object is 0. A pi-object is proper-named and privately-named. The printed name of pi-object is "pi". Understand "pi" as a pi-object.

Rule for printing the plural name of pi-object:
	say "pi".

The pic is a thing. The description of a pic is "It's a snapshot of a happy family visiting Typo Land Funfair, the closest thing Anglophone Atlantis has to an amusement park."


Picard is a man. The description of Picard is "A bald man in a Star Fleet uniform. He looks confused, just like in that episode where the crew first encounters Q and the ship's letter-removers fritz out inexplicably." Understand "captain" or "jean" or "luc" or "jean-luc" as picard. The scent-description is "tea, Earl Grey, hot".
	Sanity-check searching Picard:
		say "Come now. Where in that uniform could he possibly be carrying anything, even a wallet?" instead.
	Sanity-check attacking Picard:
		say "His phasers are probably set on stun, but still, why risk it?" instead.

Every turn when Picard is marked-visible:
	say "[one of]Picard tugs at the bottom hem of his shirt[or]Picard tells us to make it so[or]Picard clears his throat[or]Picard attempts to turn on his communicator, but it doesn't work[or]Picard is giving us a side-long look as though he suspects we're some kind of alien life form[at random]."

The description of the picrate is "It's a reddish-brown powdery substance with high explosive capability. I think we had better treat it with some care." The indefinite article is "some".

Instead of burning the picrate:
	say "Even if we had the means, that would be a terrible idea."

The description of Picross is "It appears to be a Japanese puzzle game for hand-held consoles, but since [we] don't have one of those, it won't be much use."

The pict is a man. The description of the pict is "A long-boned man with a fair thatch of hair, [if the pict wears the coarse woolen clothing]dressed in [coarse woolen clothing][end if][if the pict wears the coarse woolen clothing and the pict wears the silver chain] and [end if][if the pict wears the silver chain]wearing a [silver chain] around his neck[end if][if the number of things worn by the Pict is 0]dressed only in a savage self-confidence[end if]. He doesn't look as though he speaks English, somehow."
	The scent-description is "barley and cattle".
	The Pict wears a silver chain and coarse woolen clothing.
	The greeting of the Pict is "'Yah!' says the Pict. Not clear whether this means 'Hello, friend' or 'I plan soon to cleave your skull with my axe,' but he's noticed we're here."

The silver chain is a wearable thing. The description is "The links are large and heavy."
The coarse woolen clothing is a floppy wearable thing. The indefinite article of the coarse woolen clothing is "some". The description is "It's drab grey-brown in col[our] and looks like it probably scratches a lot. Not exactly the thing for a Mediterranean climate, either."
	The scent-description is "its sheepy origins".

Report the Pict dropping something:
	say "[one of]With a faint air of bewilderment, the Pict tosses aside [the noun][or]The Pict bites the edge of [the noun] and then casts [them] aside in disappointment[at random]." instead.


Rule for writing a topic sentence about the Pict when the location contains at least one mentionable person:
	say "[The Pict] regards [the random mentionable person] as though sizing up [their] strength in battle, unfav[our]ably. "



The pie is edible. The description of the pie is "It's pumpkin pie, smooth and glossy on top, and smelling of spices. The crust looks homemade." Understand "glossy" or "spices" or "spice" or "cinnamon" or "nutmeg" or "clove" or "pumpkin" or "crust" as the pie.
	The scent-description of the pie is "cinnamon and nutmeg and clove".


The piece-alternate is a pistol. The printed name of the piece-alternate is "piece". Understand "piece" as the piece-alternate. The description is "A small, heavy handgun, the kind a gangster's girlfriend would turn out to have in her purse. It isn't loaded, though."

Instead of shooting something with the piece-alternate:
	say "The trigger clicks harmlessly."

The heft of the pier is 2. The description of the pier is "A weatherworn mass of wood posts and planks, stained by seawater and covered in barnacles. It is only toy-size, possibly thanks to the safety overrides by the letter machinery."

The printed name of the pig-tat-inn is "pig tat inn". Understand "pig" or "tat" or "inn" as the pig-tat-inn. The description of the pig-tat-inn is "Mercifully this manifested itself as a postcard from the supposed inn rather than the inn itself, or we might have been crushed at the moment of generation. The postcard is bad enough, though, as it depicts a bed and breakfast establishment in which every possible surface is dedicated to celebrating members of the Suidae family. There are pig vases and pig lamps, pig figurines and collectible pig plates, stuffed pigs, china pigs, carved pigs, embroidered pigs and tatted pigs, not to mention piggy banks and pig-shaped creamers. There are realistic pigs that might have come from a farm, and cartoon pigs that stand up on two legs and dance. In the background a somber oil painting depicts a miniature potbellied looking up into light bursting from the heavens, as though it were having a religious experience.

WE WELCOME YOU TO PORKERS, says pink cursive script along the bottom of the card.

Someone has applied a drop shadow to the lettering.".

The printed name of the pi-tat-inn is "pi tat inn". Understand "pi tat inn" or "inn" as the pi-tat-inn. The description of the pi-tat-inn is "The resulting postcard represents a hostelry devoted to the concept of pi. The symbol is painted on the walls and carved in three dimensions and stitched on pillows, and the bedspreads are quilted with the digits of pi, lest one forget them in one's sleep.".

The description of the pill is "It is small, round, and blue. There are no brand or generic markings to indicate what it might do." The pill is edible. The heft of the pill is 1.

Instead of tasting or eating the pill:
	say "[We] toss the pill into our mouths and swallow it dry.

There is no immediate reaction, but after a few minutes a cramping pain begins to spread through our stomach, and it becomes difficult to breathe. Yellow and blue spots float in front of our eyes. Our skin begins to itch. Dimly I wonder whether these symptoms correspond to any real medical condition or whether the pill is simply poisonous by invention; but [we] [are] not clear-minded enough to go on thinking these sorts of thoughts for long.";
	end the story saying "[We] have poisoned ourself"

Test pill with "tutorial off / wave s-remover at spill / take pill / take all / get pill / get a pill / take the pill / take pill" in the outdoor cafe.

After reading a command when the player carries the pill or the player carries the pills (this is the replace take pill with eat pill rule):
	if the player's command matches "take pill":
		if the player carries the pill:
			replace the player's command with "eat pill";
	if the player's command matches "take pills":
		if the player carries the pills:
			replace the player's command with "eat pills".

Some pills are a plural-named edible thing. The description is "A handful of miscellaneous pills in blue and yellow and green. They look like someone's illicit stash of prescription pain-killers."

Instead of tasting or eating the pills:
	say "[We] toss back the whole handful without giving it much thought. The effect doesn't hit for a few minutes, but then it feels like being suddenly extremely drunk. Then like being unconscious.";
	end the story saying "[We] have poisoned ourself"


[Understand "take [pill]" as eating when the player carries the pill.]

The pin is wearable. The description of the pin is "It is a yellow pin, the sort worn by people endorsing a political candidate or a science-fiction show. On the pin in big black letters is written 'TOXI WASTE'."

After wearing the pin, say "We put on the pin, despite your misgivings about the fashion statement[if the activist recollects environment-offer] and my serious doubts about the viability of the movement[end if]."

The pineapple-ring is an edible thing. The description of the pineapple-ring is "It's a single golden, juice-dripping ring of pineapple, the kind that typically comes in a can." The scent-description is "sweet fruit". The printed name is "pineapple ring". Understand "pineapple" or "ring" as the pineapple-ring.

The description of some pins is "Straight pins, the kind people use for pinning up alterations."

The description of the pint is "As pints ought to be, this one is a pint of dark beer."
The pint is edible and contained and fluid.
The initial appearance is "Someone has left a full pint of beer on the [ground]."
The scent-description of the pint is "malt and alcohol".

The description of the pints is "Enough beer to load up a barmaid at Octoberfest.". The pints are edible and contained and fluid. The heft of the pints is 6.

The pin-t-shirt is a wearable thing. The description is "It's part shirt, part torture device, made of straight pins all over." The printed name is "pin t-shirt". Understand "pin" or "t-shirt" as the pin-t-shirt.

A dangerous construction rule for the pin-t-shirt:
	try examining the pin-t-shirt;
	say "The backpacking girl shrieks. 'What the hell! Ouch! Did you do that? What kind of flipping sociopath are you?'

Swearing like a sailor, she collects her pack and flees the room.";
	now the backpacking girl is nowhere;
	now the heavy pack is nowhere;
	reset the interlocutor;
	rule succeeds.

The pirate is a woman. The description is "A woman pirate, and not the movie variety that is practically a variant of naughty nurse, but a raw-boned, muscular woman who wears pants when they aren't in fashion and has hands covered in callouses." Understand "woman" or "muscular" or "raw-boned" or "hands" or "callouses" as the pirate. The scent-description is "seawater and grog".
	The greeting of the pirate is "'Yarr,' says the pirate. Her voice is rough and she doesn't sound entirely sober."

[The pirate can be cowed.]

Instead of showing the spot to the pirate:
	[now pirate is cowed;]
	say "[one of]Her face pales. 'Yarks,' she says.[or]She seems to have taken the meaning the first time.[stopping]".

[Persuasion rule for asking the pirate to try doing something:
	if the pirate is cowed:
		persuasion succeeds;
	otherwise:
		say "She spits largely off to one side. 'That for your instructions!'" instead. ]

Table of Ultratests (continued)
topic	stuff	setting
"pirate-spot"	{ spot, pic, crate, tub }	Workshop

Test pirate-spot with "tutorial off / autoupgrade / powerup / wave c-remover at pic / wave c-remover at crate / put pi on platform / put rate on platform / set platform to synthesize / turn on platform / show spot to pirate"  [in the Workshop holding the spot and the pic and the crate and the tub.]

The pirate-crew is a man. The printed name is "pirate crew". The description is "A fiercer, blacker-browed gang of individuals you couldn't hope to meet. Half of them have scurvy and half are missing at least a finger, and when they talk (strictly among themselves), it's all growls from the corner of the mouth." Understand "pirate" or "pirates" or "crew" as the pirate-crew.
	The scent-description of the pirate-crew is "unwashedness".

The greeting of the pirate-crew is "'Yarr,' says the pirate crew. Their voices are rough and they don't sound entirely sober."

[The pirate-crew can be cowed.

Persuasion rule for asking the pirate-crew to try doing something:
	if the pirate-crew is cowed:
		persuasion succeeds;
	otherwise:
		say "They mumble defiantly." instead.]

Sanity-check washing the pirate-crew:
	say "Heaven knows they need it, but they don't look terribly cooperative." instead.

Instead of showing the spot to the pirate-crew:
	now the pirate-crew is nowhere;
	say "To a man, they turn white under their tans. Then they flee.";
	record "Lord Michael Rosehip award for showing the black spot to a pirate crew" as an achievement with break before.

Instead of smelling the pirate-crew:
	say "They smell like a group that's spent months on a ship, living on a diet of watered rum and tortoise meat. Which is to say, I wish you wouldn't make me breathe so deeply."

Test crew with "tutorial off / autoupgrade / test pirate-spot / wave s-remover at screws / put gel on crew / put screws on platform / wave s-remover at screws / turn on platform / show spot to crew" in the Workshop holding the screws.

The description of a pit is "The hard cen[ter] of a fruit, clean and dry of any flesh.". The scent-description  of the pit is "peaches".

Understand "pit" as a pit-hole. A pit-hole is usually open and fixed in place. The description of a pit-hole is "It's a cavity in the surface of [the holder of the item described], too small for our body to fit into."

[Test pit-hole with "put pit on dais / set switch to swap homonym / turn on dais" in the Workshop.

When play begins:
	now the player carries a random pit. ]

Some pit-items are a thing. The description of the pit-items is "Assorted stones from different fruits.". The scent-description of the pit is "apricot and cherry". The printed name of the pit-items is "pits". Understand "pits" or "apricot" or "cherry" or "stones" or "stone" or "pit" as the pit-items.

Understand "pits" as the alterna-pits. The alterna-pits are a fixed in place container. The printed name of the alterna-pits is "pits". The description of the alterna-pits is "It's a number of cavities in the surface of [the holder of the item described], all of them too small for our body to fit into."

The description of a pita is "A round, brown pocket-bread." The scent-description of a pita is "baked goods". Rule for printing the plural name of pita: say "pita".

Table of Ultratests (continued)
topic	stuff	setting
"pita"	{ pocket-bread, tub, secret-plans, monocle, as-coin, piece, pearl, wrap, ball, ring }	Public Convenience

Test pita with "tutorial off / autoupgrade / powerup / wear monocle / wave a-remover at soap dispenser / squeeze dispenser / get sop / gonear Sensitive Equipment Testing Room / put sop in t-inserter / wave s-remover at stop / test gun-ownership / shoot top / get pot / wave e-remover at piece / put pic in t-inserter / wave c-remover at pict / wave p-remover at pit / get it / wave w-remover at wrap / put rap in t-inserter / shoot trap / wave r-remover at part / get pat / wave n-remover at ring / put rig in t-inserter / shoot trig / wave r-remover at grit / wave g-remover at git / get it / gonear Generator Room / put pat in bucket / put pet in bucket / wave p-remover at pit / get it / switch switch / put pot in bucket / wave p-remover at pit / get it / gel as / wave i-remover at pastis / wave s-remover at pasts / wave t-remover at pat / unlegend / w / wave r-remover at pearl / wave e-remover at peal / wave l-remover at pal / wave s-remover at plans / wave l-remover at plan / wave n-remover at pan / put pa and it on dais / synthesize / get pita / put pa and it on dais / 2 / synthesize / get pita / put pa and it on dais / 2 / synthesize / get pita / gel pocket-bread "

The pit-trap is an open enterable container. It is fixed in place. The heft of the pit-trap is 8. The printed name of a pit-trap is "pit trap". Understand "pit" or "trap" or "deep pit" or "rocks" or "stones" or "stakes" or "sharpened" or "stake" as a pit-trap. The description of a pit-trap is "It's a deep pit lined with rocks, set into the surface of [the holder of the item described]. Sharpened stakes jut upward from the bottom, ready to stab any large mammal unfortunate enough to fall in."

Instead of inserting something into the pit-trap:
	say "That is not a good way to get rid of [the noun]."

Instead of entering the pit-trap:
	say "Against my better judgment, [we] slip over the edge into the pit trap and are impaled on the spikes below. It will be some considerable time before anyone thinks to check down here.";
	end the story saying "What made you think that was a good idea?"

The pit-traps are an open enterable container. They are fixed in place. The heft of the pit-traps is 8. The printed name of the pit-traps is "pit traps". Understand "pit" or "traps" or "pits" or "rocks" or "stones" or "stakes" or "sharpened" or "stake" as the pit-traps. The description of the pit-traps is "It's a number of deep pits lined with rocks, set into the surface of [the holder of the item described], close enough to each other to trigger some mild trypophobia. Sharpened stakes jut upward from the bottom, ready to stab any large mammal unfortunate enough to fall in."

Instead of inserting something into the pit-traps:
	say "That is not a good way to get rid of [the noun]."

Instead of entering the pit-traps:
	say "Against my better judgment, [we] slip over the edge into one of the pits and are impaled on the spikes below. It will be some considerable time before anyone thinks to check down here.";
	end the story saying "What made you think that was a good idea?"

[The heft of Pisa is 20. The description of Pisa is "It's manifested mostly as the leaning tower, but that's still quite a bit more than can fit into this environment."]

The printed name of the plain-plate is "plate". Understand "plate" as plain-plate.

A plan is a long floppy illegal thing. The description of the plan is "The number of pages in the original roll has been reduced, but this is still obviously a bit of DCL property. The only difference is that this version is incomplete."
	The scent-description of the plan is "crisp paper".

The description of the plant is "An aggressively healthy specimen with broad, glossy dark brown leaves and red flowers. I can't identify whether it's a real plant style or something wholly invented for the occasion, but it does resemble a poinsettia rendered by someone who doesn't quite remember what they look like."

The description of the plants is "A whole tray of little herb plants in pots. It's adorable."

The description of the plantage is "An array of bushes, shrubs, and small trees. There's enough here to relandscape my parents['] back terrace, if anyone had the inclination." The heft of the plantage is 8. The plantage is fixed in place.

The description of the plat is "A single map showing the land divisions of this immediate vicinity. The Bureau land abuts the park, of course, and a fair amount of land owned for offices by DCL. No private land is directly next to the Bureau land, presumably to make it more difficult for anyone to dig through or plant listening devices."

The description of the plats is "A roll of zoning commission plans, laying out the subdivision of some land at the outskirts of town. The Bureau is chary about allowing new housing developments because it worries about the limited land available on the island; so it is unsurprising that the housing areas allowed are designed for dense occupancy in the form of apartments."
The description of the plain-plate is "A commemorative plate celebrating the island's independence from England. It shows the Depluralized Marine [--] the single soldier captured after the island's deplural[izing] guns had a chance to work [--] chained unhappily to a wall."

The plea is r-abstract. The plea is noisy. The description of the plea is "A querulous voice is requesting a judge to go leniently on a first off[ense] of using unauthor[ize]d foreign words in public."

Instead of listening to the plea:
	say "[one of]When listening to it, I must say the plea makes kind of sense[or]The plea goes on like a crying violin[or]The plea actually sounds pretty convincing[cycling]."

The description of the pleat is "A bit of cloth from a schoolgirl's plaid skirt, complete with the stitched-in fold."

Poe is a man. Poe is proper-named. The description is "For a famed author, he looks unhinged. His clothes are shabby, his eyes wild, and he keeps looking around him as though he expects something to jump from the shadows."
	The greeting of Poe is "'Fascinating to meet you,' says Poe."

Report involuntarily-dropping Poe:
	say "[The noun] scrambles out of our embrace as rapidly as possible, looking extremely Victorian." instead.

Rule for writing a topic sentence about Poe when the location contains at least one mentionable person:
	say "[Poe] is looking [a random mentionable person] over and muttering to himself as though trying to find rhymes. "

The poi is an edible thing. The description is "It's a gloopy, purplish paste made of taro root according to Hawaiian traditions. It doesn't look overwhelmingly tasty, but perhaps that's just cultural difference speaking." The scent-description is "root vegetable". The indefinite article is "some".

The poll is r-abstract. The description of the poll is "A dim, grainy image of an early 20th-century referendum in progress, with men in funny hats and women in velvet jackets jerkily walking into the booths to cast their votes."

The printed name of the polling-chart is "polling chart". Understand "polling" or "chart" as the polling-chart. The description of the polling-chart is "A chart showing Atlantida's popularity in May of 1982. Different lines represent segments of the population such as DCL Employees, Farmers, School Children, etc."

A poppycock is a noisy r-abstract thing. The indefinite article is "some". The heft of the poppycock is 0. The description is "A great deal of drivel. Or, no: more the concept of drivel, as regarded by a previous age.

Curiously, therefore, the concept manifests itself as a little floaty cloud in which a flustered gent with Victorian [if the player wears the Britishizing goggles]moustaches[otherwise]mustaches[end if], verging on apoplexy."

Instead of listening to the poppycock, say "[one of]'POPPYCOCK!' shouts the little imaginary man[or]'ARRANT NONSENSE,' shouts the angry poppycock-gentleman[or]'STUFF! POPPYCOCK!' The imaginary man is beet red and his mustaches quiver[at random]."

The poppy-oil is a fluid contained edible thing. Understand "poppy" or "oil" as the poppy-oil. The printed name is "poppy oil". The indefinite article is "some". The description is "A bottle of culinary-grade poppy oil, though useless: I can't cook, and we haven't time."

	Sanity-check burning the poppy-oil:
		say "I think the idea is that you put the oil in a pan and cook with it. But never mind; it wouldn't help us now anyway." instead.

The pose is an r-abstract thing. The heft of the pose is 0. The description of the pose is "At the moment it looks like a tiny man [one of]emulating The Thinker[or]staring importantly into the middle distance while tiny cameras flash[or]waving and smiling, in best Atlantean politico fashion, with a sprig of olive leaves pinned to his vest and a blue 'just voted' smudge on his thumb[or]semaphoring the letter A[or]demonstrating first position in ballet[as decreasingly likely outcomes]."

The poser is a man. The description of the poser is "He wears an improbable hip-hop outfit."
	The greeting of the poser is "'Yo yo yo,' says the poser."

The potage is edible. The description of the potage is "It's a thick stewy mass that looks like it's been thickened with mashed potato. Beef broth and chunks of carrot may also play a part. I'm not sure." The scent-description is "boiled vegetables".

Instead of searching the potage:
	say "Is that a [one of]chunk[or]slice[or]sliver[at random] of [one of]carrot[or]leek[or]onion[or]turnip[or]yam[or]potato[or]Jerusalem artichoke[cycling]? No, it's sunk again."

The description of a pot is "It's a single ordinary cooking pot." The heft of a pot is 2.
Understand "singular-pot" as a pot.

The printed name of an alterna-pot is "pot". Understand "pot" or "marijuana" or "mary jane" or "mj" or "weed" or "dope" or "drugs" or "singular-pot" as the alterna-pot. The description of the alterna-pot is "An ounce or so of weed. I'm afraid I can't judge the quality of the product." The indefinite article of alterna-pot is "some".

Some plural-pots are a plural-named open container. The description of the plural-pots is "They're a selection of heavy, high-quality stainless steel cooking pots, like the ones my mother has at home." The heft of the plural-pots is 3. The printed name of the plural-pots is "pots". Understand "pots" or "pot" as the plural-pots.

The description of the potass is "Potassium, appearing under its older pharmaceutical abbreviation. It's a dull silverish metal whose surface rapidly oxidizes in the atmosphere. Beware dropping it in water." The indefinite article is "some".

Section 12 - Power Cord

[This section incorporates the power cord but also the rest of the socketry, etc. It would also have been reasonable to define this stuff in the Generator Room section, but it felt easier to keep all the relevant parts together in one place.

Plugging things into other things is one of those maddening verbs that needs a lot of implementation just to cover the standard idiomatic ways of talking about it. We want the player to feel there's enough depth here that they can unplug bits individually, but "PLUG IN DAIS" should also just work, given the presence of the power cord.

Another complication comes from the destructibility of objects; if the player gels or anagrams the power cord while it's plugged in, it should automatically unplug, or some weird results may appear when it's reconstructed.]

Test cord with "test chord / get cord / test plugging".

Test plugging with "look / x socket / x wall socket / x dais / x dais socket / get wall socket / get dais socket / plug cord into wall socket / plug plug into dais socket / unplug dais / unplug wall socket / plug wall socket into dais / unplug cord / plug cord into dais / take plug / unplug cord / plug in dais / test gun-ownership / shoot cord with gun / look / shoot crow with gun / x cord / unplug cord / plug in dais" in the Workshop.


Test gun-ownership with "load gun" holding the anagramming gun and the bullets.

A ranking rule when the power cord is mentionable:
	increase the description-rank of the power cord by 10;
	increase the description-rank of the programmable dais by 12.

Definition: a power cord is deeply dull if 1 is 2.


A dangerous destruction rule for the power cord:
	while a massive plug (called target plug) is plugged into something (called the other):
		now the target plug is not plugged into the other.


Rule for writing a topic sentence about the power cord:
	say "[A power cord] snakes across the [ground]";
	if a massive plug is plugged into the dais socket and a massive plug is plugged into the wall socket:
		say ", connecting [the dais socket] to [the wall socket]. ";
	otherwise if a massive plug is plugged into the dais socket:
		say ", plugged into [the dais socket] and with the other end dangling free. ";
	otherwise if a massive plug is plugged into the wall socket:
		say ", one end plugged into [the wall socket] but the other unattached. ";
	otherwise:
		say ". Neither end is currently plugged into anything. ";
	say run paragraph on.

The description of the power cord is "This is the heaviest-duty cabling I've ever seen in my life: thick as a snake, covered in yellow sheathing, with a [random massive plug] at each end."
	The massive plug is a kind of thing. The description of a massive plug is "There are [unless the player wears the Britishizing goggles]two flat parallel prongs and one round grounding prong[otherwise]two flat horizontal prongs and one flat vertical prong[end if]. The prongs look extraordinarily sturdy and determined." Understand "prong" or "prongs" as a massive plug.
	Two massive plugs are part of the power cord.

A power socket is a kind of thing. The description of a power socket is usually "[if something is plugged into the item described]A power socket, with one end of the power cord currently plugged in[otherwise]A power socket suitable for plugging a cord into[end if]."

A power socket called a wall socket is fixed in place in the Workshop.
The initial appearance is "A specialized wall socket is built into the east wall, clearly not part of the ordinary power system for the Bureau."

A power socket called a dais socket is part of the programmable dais.

After printing the name of the dais socket while examining:
	say "[if something is plugged into the dais socket and there is a free massive plug] (with power cord attached, though the cord connects to nothing)[otherwise if something is plugged into the dais socket] (from which a power cord runs to the wall)[otherwise] (with no cord currently plugged in)[end if]"

Attachment relates one massive plug to one power socket. The verb to be plugged into implies the attachment relation.

Sanity-check inserting the power cord into a power socket:
	if a free massive plug (called target plug) is part of the power cord:
		try plugging the target plug into the second noun instead.

Sanity-check inserting the power cord into something which is not a power socket:
	if a power socket (called target socket) is part of second noun:
		try plugging the power cord into the target socket instead.

Sanity-check inserting a massive plug into a power socket:
	try the actor plugging the noun into the second noun instead.

Sanity-check inserting a massive plug into something which is not a power socket:
	if a power socket (called target socket) is part of second noun:
		try plugging the noun into the target socket instead.

Sanity-check taking a massive plug:
	if the noun is plugged into something (called the target):
		try unplugging the noun from the target instead;
	otherwise:
		try taking the power cord instead.

Understand "unplug [a massive plug] from [a power socket]" as unplugging it from. Understand "unplug [something] from [something]" as unplugging it from. Unplugging it from is an action applying to two things. Understand "unplug [something]" as unplugging it from. Understand "detach [something]" as unplugging it from.

Does the player mean unplugging a free massive plug from:
	it is very unlikely.

Rule for supplying a missing second noun when unplugging something from:
	if the noun is a massive plug:
		if the noun is plugged into something (called target socket):
			now the second noun is the target socket;
		otherwise:
			say "[The noun] is not plugged in anyhow." instead;
	otherwise if the noun is a power socket:
		if something (called target plug) is plugged into the noun:
			now the second noun is the noun;
			now the noun is the target plug;
		otherwise:
			say "Nothing is plugged into [the noun] anyway." instead;
	otherwise if the noun incorporates a power socket (called target socket):
		if something (called target plug) is plugged into the target socket:
			now the second noun is the target socket;
			now the noun is the target plug;
		otherwise:
			say "Nothing is plugged into [the noun] anyway." instead;

Sanity-check unplugging something which incorporates a power socket from something which incorporates a power socket:
	try freeing the power cord instead.

Sanity-check unplugging something which is not a massive plug from something:
	say "[The noun] [are] not a plug to start with." instead.

Sanity-check unplugging something from something which is not a power socket:
	say "[The second noun] [are] not a socket." instead.

Sanity-check unplugging a free massive plug from something:
	say "[The noun] [are] not plugged in anyway." instead.

Carry out unplugging something from something:
	now the noun is not plugged into the second noun.

Report unplugging something from something:
	say "[We] unplug [the noun] from [the second noun][if two free massive plugs are part of the power cord]. Both ends of the power cord are now free[otherwise]. The other plug is still attached to [the random power socket which is not the second noun][end if]."

Understand "unplug [the power cord]" as freeing. Freeing is an action applying to one thing.

Understand "detach [the power cord]" as freeing.

Sanity-check freeing the power cord:
	if the number of free massive plugs is 2:
		say "The power cord is already detached." instead.

Carry out freeing:
	repeat with item running through massive plugs which are plugged into a power socket:
		if the item is plugged into a power socket (called target socket):
			try unplugging the item from the target socket.

Understand "plug [a free massive plug] in/into [a power socket]" as plugging it into. Understand "plug [a massive plug] in/into [a power socket]" as plugging it into.  Understand "plug [something] in/into [something]" as plugging it into.

Understand "plug in [a free massive plug] to [a power socket]" as plugging it into. Understand "plug in [a massive plug] to [a power socket]" as plugging it into. Understand "plug in [something] to [something]" as plugging it into.


Plugging it into is an action applying to two things.

Definition: a massive plug is free if it is not plugged into a power socket.

Sanity-check plugging something which is not a massive plug into a power socket:
	if the noun is the power cord:
		if a massive plug (called target plug) is free:
			if the second noun incorporates a power socket (called target):
				try plugging the target plug into target instead;
			otherwise:
				try plugging the target plug into the second noun instead;
		otherwise:
			say "The power cord is already fully plugged in." instead;
	say "That's not what the best electrical safety engineers recommend." instead.

Sanity-check plugging a massive plug into something which is not a power socket:
	if the second noun incorporates a power socket (called target socket):
		if a massive plug (called target plug) is free:
			try plugging the target plug into the target socket instead;
		otherwise:
			say "The plugs are fully plugged in already." instead;
	otherwise:
		say "Better look for a socket instead." instead.

Sanity-check plugging the power cord into something:
	if the second noun is a power socket:
		if a massive plug (called target plug) is free:
			try plugging the target plug into the second noun instead;
		otherwise:
			say "The plugs are fully plugged in already." instead;
	otherwise:
		if the second noun incorporates a power socket (called target socket):
			if a massive plug (called target plug) is free:
				try plugging the target plug into the target socket instead;
			otherwise:
				say "The plugs are fully plugged in already." instead;
		otherwise:
			try plugging a random massive plug into the second noun instead.

Carry out plugging a massive plug into a power socket:
	if the power cord is not in the location:
		silently try dropping the power cord;
	now the noun is plugged into the second noun.

Report plugging a massive plug into a power socket:
	say "[We] plug the power cord firmly into [the second noun][if a free massive plug is part of the power cord]. The other end remains free and not plugged into anything[otherwise]. Both ends of the power cord are now plugged in, so the dais is connected to the wall socket[end if]." instead.

Sanity-check going somewhere when the player carries the power cord:
	if the number of free massive plugs is less than 2:
		try dropping the power cord;
		if the player carries the power cord:
			stop the action.

Understand "plug [a power socket] into [a power socket]" as connecting it to.  Understand "plug in [a power socket] in [a power socket]" as connecting it to. Connecting it to is an action applying to two things.

Understand "connect [a power socket] to [a power socket]" as connecting it to.
Understand "connect [a power cord] to [a power socket]" as plugging it into.

Sanity-check an actor connecting something to something:
	[carry out the caching scope activity with the actor;]
	if the actor does not carry the power cord:
		if the the power cord is marked-visible:
			try the actor taking the power cord;
		if the actor does not carry the power cord:
			if the actor is the player:
				say "Some kind of power cord would be needed to bridge the gap.";
			stop the action.

Carry out an actor connecting something to something:
	unless something is plugged into the noun:
		let the target plug be a random free massive plug;
		try plugging the target plug into the noun;
	if a massive plug (called next target plug) is free:
		try plugging the next target plug into the second noun;

Understand "plug in [something]" as connecting.
	Connecting is an action applying to one thing.

Sanity-check connecting something:
	if the noun is a power socket:
		make no decision;
	if the noun is a massive plug or the noun is the power cord:
		if the location is not Workshop:
			say "There seems to be nothing suitable here to plug [the noun] into." instead;
		otherwise:
			make no decision;
	if the noun incorporates a power socket:
		make no decision;
	if the noun incorporates a massive plug:
		make no decision;
	if the noun is a device:
		say "[The noun] [appear] to be adequately powered already." instead;
	say "[The noun] [feature] no dangling power cords or sockets." instead.

Carry out an actor connecting something:
	if there is a massive plug plugged into the dais socket and there is a massive plug plugged into the wall socket:
		say "The plugs are fully plugged in already." instead;
	otherwise:
		try the actor connecting the dais socket to the wall socket.

[There's really no particular point to having the britishizing goggles change the compatibility of the plug. I just couldn't resist.]

[The heft of the massive pug is 10. The massive pug is a neuter animal. The scent-description of the massive pug is "puppies". The description of the massive pug is "With its  wrinkly, short-muzzled face, curled tail and fine, glossy coat of fawn fur, it would be adorable if not for the fact that it is large enough to fill the entire room."]

Section 13 - Pr - Pz

The description of the preamp is "It looks like just one specimen from the group from which you were working."

The description of a PTA is "About a half-dozen soccer moms in sensible shoes, and a lone father, who looks bemused." A PTA is noisy. Understand "soccer moms" or "moms" or "shoes" or "lone" or "father" as a PTA.

Instead of listening to a PTA:
	say "[one of]The PTA members are currently debating whether it is appropriate to include pre-English-language history of Atlantis in the curriculum[or]One of the PTA members is currently arguing to eliminate the story of the Tower of Babel from the literature curriculum on the grounds that it is unduly traumatic for the young[or]The PTA members are working out a bake-sale roster to pay for fresh copies of the New Orthodox Orthography to be placed in the school library[or]One PTA mother is explaining why she felt her daughter should have been given the part of the letter Y in the school play[at random]."

Rule for writing a paragraph about a PTA (called item):
	say "[The item] you summoned is nearby, engaged in lively debate";
	let N be the number of PTAs in location - 1;
	if N is 1:
		say " with the other PTA next to them";
	otherwise:
		if N is greater than 1:
			say " with one of the other [N in words] PTAs here";
	now every PTA in location is mentioned;
	say ".[paragraph break]".

The pun is r-abstract. The heft of the pun is 0. The description of a pun is "Currently the pun reads '[one of]Atheism is a non-prophet organization[or]The butcher backed up into the meat grinder and got a little behind in his work[at random].'"

Some puns are r-abstract. The heft of the puns is 0. The description of puns is "It's a booklet of Atlantean jokes, the kind of thing you might get in school as a child."

The heft of the fake-punt is 6. The description of the fake-punt is "It's a long, flat-bottomed wooden boat intended to be driven by pole rather than by a sail or oars. We don't have a lot of locations for punting around here, but I've read about the practice." The printed name of the fake-punt is "punt". Understand "punt" as the fake-punt.

[The heft of the punts is 10. The description of the punts is "An assortment of punts, which by the looks of them are usually driven from the Cambridge end."]


The puppy is a female animal. The description of the puppy is "She's a tiny black and white papillon with lots of fur and large, trusting eyes." Understand "papillon" as the puppy. The heft of the puppy is 2.

Test puppybug with "drop puppy / take puppy" holding the puppy.

The indefinite article of the pus is "some". The description of the pus is "It's malodorous and yellowish."


Section 14 - Q through R


The description of the quid is "A British pound coin, with the head of the Queen and everything. This one is [one of]the version with the Welsh inscription round the rim[or]the Scottish thistly variety[or]the one with the lion and unicorn on the reverse[at random]."

The description of Ra is "Tall and terrifying. These animate abstracts are often hard to look at straight on. His whole self shines with sunlight, and yet he is as sinister and constricting as the dark of a collapsed tomb."
	The greeting of a Ra is "Ra beams upon us."

Sanity-check putting something which is not wearable on the rack:
	if the noun is the gel or the noun is the tub:
		make no decision;
	else:
		say "[The rack] is for clothes; the pegs aren't really designed to hold anything else." instead.

Table of Ultratests (continued)
topic	stuff	setting
"rackbug"	{ ruck, tub }	Generator Room

Test rackbug with "tutorial off / put ruck in bucket / get rack / put gel on rack" [holding the ruck and the tub in the Generator Room.]

The rack is a supporter. It is not fixed in place. Understand "pegs" or "peg" as the rack. The description of the rack is "It's a wooden support with a few pegs for hanging up coats or jackets."

The rad worm is a neuter animal. The description of the rad worm is "It's an earthworm, but with bands of psychedelic col[our]."

The rale is r-abstract and noisy. The description of the rale is "It is the noise of breathing impeded by fluid in the lungs. It sounds as though someone is dying [if the location is indoors]in[otherwise]out[end if] here.".

The heft of a ram is 7. The description of a ram is "I can tell it's a ram because of those characteristic curly horns, and the mean look it has in its eye. Otherwise it's your basic sheep."

Some plural-rams are a neuter animal. The printed name of the plural-rams is "rams". The description is "A whole flock of rams, bleating and fleece-covered. They're really a bit frightening in this profusion."

The description of a ramcat is "[description of a tomcat]". Instead of touching or rubbing or squeezing a ramcat:
	say "It [one of]dodges out of reach[or]swipes at us, claws out[or]hisses[at random]."

The rampage is an r-abstract thing. It is noisy. The description of the rampage is "It doesn't look like much other than a ball of black angry energy."
	Instead of listening to the rampage:
		say "The rampage [one of]sounds like death metal and crashing cars[or]blares on[or]is screeching in the tones of tearing metal[at random]."

[The heft of the rampart is 12. The description of the rampart is "A mass of earthwork and stone."]

A rap is usually noisy. A rap is usually r-abstract. The description of the rap is "A pulsating ball of angry air."

[Originally the rap riffed on the lyrics of the handful of songs I know at all well.  The results were so painfully square that Sam Ashwell responded by schooling me with a 38K email entitled 'A Very White Person Summarises Hip-Hop Themes'.

The results do not live up to his efforts, but you should know that he really really tried and, hey, the Sir Mixalot joke is gone.]

Instead of listening to the rap:
	say "[if the rap  is the prior named object]It[otherwise]The rap[end if] ";
	if the player wears the Britishizing Goggles:
		say "[one of]devolves into a chap-hop diss on another man's tweed[or]sings the praises of one Professor Elemental[or]informs the listener that it doesn't like his tweed[or]asks the listener to fetch its trousers at once[at random].";
	else:
		let N be a random number between 1 and 3;
		if N is:
			-- 1: say "describes the pleasures of [one of]a swimming pool full of Cristal[or]driving a Chevy Impala down the coast road to Maiana[or]stealing a depluralizing tank for a joyride[at random].";
			-- 2: say "details the singer's dislike of [one of]gold-diggers[or]suspicious Bureau men who only give trouble to guys with accents[at random].";
			-- 3: say "includes the phrase '[one of]colder than a rifle shot, restoration to the real[or]my words hit like an anagram bullet[at random]'.";


The description of the rape is "The bright yellow flower of a plant grown for its oil [--] typically called canola oil in the US." The heft of the rape is 1.
	The introduction of the rape is "It's undoubtedly not the most obvious referent for the word, but there are certain concepts that people tend to recoil from, making them harder to generate. And just as well, really."

A rape-alternate is a thing. The printed name of the rape-alternate is "rape". Understand "rape" or "pope" or "rape of the lock" as the rape-alternate. The description is "It's a printed passage from Pope's The Rape of the Lock, complete with Beardsley illustrations."


The description of the rash is "A patch of skin [--] it looks like human skin at that [--] and all red and itchy." The heft of the rash is 1.

The description of a rat is "It looks cunning and not entirely friendly." The heft of a rat is 2.

The rate is r-abstract. The description of the rate is "A fluctuating, fizzing number with a dozen decimal points or more, never the same two times [we] look."

The rattle is a noisy thing. The description of the rattle is "Meant as a toy for a baby, it has a clear molded body full of clacking beads, each one of which is stamped with a letter of the alphabet." Understand "clacking beads" as the rattle.

Instead of listening to the rattle:
	say "The rattle shakes vig[our]ously."

The description of the ravens chili is "It's a can of chili [--] raven meat chili, to be specific. Three black birds glare balefully out of the label." The ravens chili is edible. The indefinite article is "some".
	The introduction of the ravens chili is "I hope it is heavily spiced, because I can't imagine that the taste of carnivorous birds is otherwise a very pleasant one."

Instead of showing the ravens chili to Poe:
	record "Mort Shaply award for showing Poe a raven-based foodstuff" as an achievement;
	say "He looks at the label, holds a hand to his gut, and murmurs, 'Nevermore.'"

The description of the raven chili is "To distinguish it from the ravens chili, this is an artisanal, single-raven-source varietal. Organic, tower-raised raven only." The raven chili is edible. The indefinite article is "some".

Every turn when the Pict is carrying something (called freight):
	try the Pict dropping the freight.

Instead of showing the raven chili to Poe:
	record "Mort Shaply award for showing Poe a raven-based foodstuff" as an achievement;
	say "He groans and starts searching about for his bust of Pallas."

Sanity-check writing the topic understood on the single ream:
	say "Writing on the paper by hand would ruin it for the printer." instead.

The description of the single ream is "One ream, which is to say 500 sheets, of generic printer or copier paper[if the single ream is proffered by the cream]. The sheets are an attractive milky col[our][otherwise], in white-white[end if]." The printed name of the single ream is "ream". Understand "paper" or "pages" or "printer paper" or "copier paper" as the single ream.

The description of the reams is "Since each ream is 500 sheets of paper, and there are many reams here, the collection is unhelpfully bulky."

The description of the red frozen inn is "A representative postcard shows a red-painted inn in the middle of a Vermont winter. It looks viciously cold. We should count ourselves lucky that the whole inn didn't manifest itself here and squash us."

The reflective widow is a noisy woman. The description of the reflective widow is "A tall woman with a widow's peak and a distant look in her eyes." Understand "widow's peak" or "distant look" as the reflective widow. The initial appearance is "[A reflective widow] [one of]perches[or]paces[at random] nearby, [one of]wringing her hands[or]sniffling faintly[or]dabbing her eyes[at random].".

Instead of listening to the reflective widow:
	[carry out the caching scope activity with the reflective widow;]
	if a random chance of 1 in 3 succeeds:
		if the mourning dress is marked-visible:
			say "[The reflective widow] admires [the mourning dress] wistfully.";
		if the morning dress is marked-visible:
			say "[The reflective widow] is moved to thoughts of her late husband by the sight of [the morning dress], just the sort of thing he used to wear.";
	else:
		say "[The reflective widow] murmurs to herself about her late husband's [one of]noble character[or]fortitude[or]forebearance[or]kindness[or]good dress sense[or]preference for scented pomades[or]distrust of foreign-made letter removers[or]implausible sweet tooth[or]socks that always needed darning[or]tendency to leave cigar ash everywhere[as decreasingly likely outcomes]."

Test reflective-widow with "tutorial off / n / autoupgrade / wave n-remover at reflective window / look / x widow / listen to widow / z / z".

The description of the reel is "A reel of film, unlabeled." Understand "film" or "reel of film" as the reel.

The printed name of the fishing-reel is "reel". Understand "reel" or "fishing" as the fishing-reel. The description of the fishing-reel is "The sort of reel used on a fishing rod, the kind with a handle you can crank to pull in the line. But as there is neither line nor rod available, it doesn't actually offer much functionality like this."

Instead of turning the fishing-reel:
	say "[We] crank the reel a few times, but as it's not attached to a line, nothing very interesting results."

A rig is a device.
The heft of the rig is 8.
The initial appearance is "A rig is set up here[if the rig is lit], lighting the vicinity[end if]."
The description of the rig is "A whole collection of lighting units on poles and stands."

	Carry out switching on the rig:
		now the rig is lit.
	Carry out switching off the rig:
		now the rig is unlit.

Report involuntarily-dropping the rig:
	say "The rig is too heavy to carry, and [we] almost drop it, but I manage to set the poles down mostly stably and, for a wonder, without breaking anything." instead.

The description of the rill is "A very tiny stream, little more than a trickle."

The alterna-ring is an r-abstract noisy thing. The heft of the alterna-ring is 0. The printed name is "ring". Understand "ring" as the alterna-ring. The description of the alterna-ring is "It sounds like the ringing of an old-fashioned telephone, piercing and abrupt."

Instead of listening to the alterna-ring:
	say "[one of]The sound of a phone ringing fills the air[or]The ring continues to sound[at random]."

The ringband is a wearable thing. The description is "A band of gold with six prongs for the setting of a solitaire gem, but the gem itself is missing." Understand "gold" or "band" as the ringband.

Sanity-check inserting the gem into the ringband:
	say "I think it requires a jeweler's tools and training to accomplish that." instead.


Test ringmaking with "set gem / put gem in ringband" holding the ringband and the gem.

The ripening-apple is an edible thing. Understand "ripening" and "apple" or "stem" as the ripening-apple. The printed name is "ripening apple". The scent-description is "tart fruit". The description is "Like any other apple, except this one has a bit more stem attached (as though still theoretically connected with the tree); and it's very green and crisp, with just the first blush of rosiness appearing on the sides."

The rite is an r-abstract thing. The heft of the rite is 0. The description of the rite is "Flickering images depict [one of]candles and incense carried by those processing around the New Church on a holy day[or]a woman kneeling before a small rustic shrine and placing the figure of a metal god on it[or]a man drawing the letter A on his son's forehead with a grease pencil[or]a bonfire at the beach, over which a pig is being roasted. The pig's ears, hoofs, and entrails have been set to one side[at random]."

Table of Ultratests (continued)
topic	stuff	setting
"rite"	{ pear, tub, anagramming gun, anagram bullets }	Sensitive Equipment Room

Test rite with "tutorial off / autoupgrade / wave p-remover at pear / put ear in t-inserter / get tear / gonear first aid station / put tear in bucket / get tier / load gun / shoot tier / x rite" [holding the pear and the tub and the anagramming gun and the anagram bullets in the Sensitive Equipment Room.]

[Originally the rock was described as "hefty and jagged, like the result of a volcanic event". Then I went to Kilauea on vacation and picked up some volcanic rocks, and they were the lightest rocks I've ever lifted. So that had to go. I decided that Brock's supportive personality, rather than his unpredictability, was the thing I wanted to express in the rock description.

geology.about.com lists basalt as a fairly dense rock, denser than limestone, marble, or granite; other weighty options included peridotite, gabbro, and diorite, but I figured those would be less familiar to the player.]

The description of the rock is "Heavy, dark, and roughly hexagonal, like a slice of basalt column.". The rock is essential.
	The scent-description is "mud".

Before putting the restoration gel on the rock when the rock is in a container (called the holder):
	say "(first removing the rock so Brock isn't squished by [the holder])[command clarification break]";
	try taking the rock;
	if the player does not carry the rock:
		stop the action.

Table of Ultratests (continued)
topic	stuff	setting
"rock-bug"	{ rock, tub }	Sensitive Equipment Testing Room

Test rock-bug with "put rock in t-inserter / open tub / gel rock" [holding the rock and the tub in the Sensitive Equipment Testing Room.]

The roc is a rideable animal.
The initial appearance is "[if the location is Sunning Deck or the location is Open Sea][roc-aboard][otherwise]The roc towers over us, looking smarter than a bird has the right to look.[end if]".
The description of the roc is "The bird is enormous[if the location is indoors], so big it seems incredible that it can even fit into the room[end if]. Brock is a tall man, but not nearly this tall. Its eyes are bright and (if [we] [are] not reading too much in) ironically clever; its plumage a little like an eagle's, if an eagle were made many times larger and feathered in a kind of sleek, luminous black."

[Every turn when the roc is in the Sunning Deck and the Sunning Deck is the location:
	say "[one of][The roc]'s claws dig into the cushions of the sunning deck as the yacht pitches[or][roc-igloo][or][The roc] stretches its wings out so that they reach the whole width of the yacht[or]I catch [the roc] looking at us[at random].".]

To say roc-aboard:
	say "[The roc][if the location is Open Sea] soars through the dark sky above[otherwise][one of]'s claws dig into the cushions of the sunning deck as the yacht pitches[or] stretches its wings out so that they reach the whole width of the yacht[at random][end if]. ".

[To say roc-igloo:
	say "[one of][The roc] takes an interest in the white igloo-shaped thing that handles the yacht wifi[or]A tap of beak on plastic tells you [the roc] is checking out the wifi further[if Slango can see the roc][queue Slango-irritation as postponed optional][end if][or][The roc] turns its head experimentally and tries to fit the whole wifi antenna into its beak[stopping]".]

Instead of waving the letter-remover at the rock creating the roc when the location is indoors:
	say "[We][']d probably best wait until [we] [are] outside; I don't know what the consequence would be of generating a massive legendary bird of prey inside a small office building."

Instead of waving the letter-remover at the rock creating the roc when the location is the Open Sea:
	say "A bird that size would sink a little tiny boat like this one."

Rule for writing a paragraph about Slango when the roc is mentionable:
	say "[Slango] has come out, though he's standing well back from [the roc], which is surveying the scene speculatively.[paragraph break]"

Roc-flight is a scene. Roc-flight begins when the roc is in location. Roc-flight ends when the player is held by the roc.

Sanity-check doing something when the location is Open Sea and the roc must be touched:
	say "[The roc] is too high up to reach." instead.

Sanity-check mounting the roc:
	if a heavy handled thing (called the encumbrance) which is not the roc is enclosed by location:
		say "[We][']ll want to bring [the encumbrance], but in [their] current state, [they] [are] much too heavy." instead.

Before mounting the roc:
	repeat with item running through things in the location:
		if the item is handled:
			try the player taking the item.
	[repeat with item running through visible handled things which are not enclosed by the player:
		try the player taking the item;
	repeat with item running through visible handled things which are not enclosed by the player:
		try the roc taking the item.]

Every turn during roc-flight:
	if the roc is in the location and (location is Precarious Perch or location is Abandoned Shore) and a random chance of 1 in 2 succeeds:
		say "[one of]The roc looks sharply at [the random-visible-thing][or]I am starting to find it unnerving how the roc looks at us[or]Are you sure that rocs don't eat people? It's giving us quite an intent stare[or]The roc paces up and down[or]The roc spreads its wings, then folds them again. It could really double as a pavilion if it would stay still in that position[or]The roc throws its head back and screams horribly for no obvious reason[or]The roc dips its beak in the water, then spits violently[at random]."

When roc-flight ends:
	say "It stares at us for a moment, and you climb carefully onto its back, putting our arms around its neck. A dozen drawbacks occur at the last minute. What if there isn't enough room for the takeoff? What if it needs to run [--] like a plane [--] before it can get airborne?";
	if the location is Precarious Perch:
		try the roc going down;
		silently try the roc going north;
	otherwise:
		if the location is Abandoned Shore:
			try the roc going north;
	follow the compass-drawing rule.

[After roc going down from Precarious Perch:
	try the roc going north;
	follow the compass-drawing rule.]

Report the roc going down from Precarious Perch:
	say "The roc strains its neck forward, spreads its wings and steps off the cliff. Its flaps are slow and [we] [are] losing altitude [--] so fast that at first I think [we][']ll fall into the ocean [--] but it takes only five or six flaps before the bird is able to glide straight out to sea." instead.

Report the roc going north from Abandoned Shore:
	say "The roc rather awkwardly climbs to the highest spot of rock it can easily reach. Then it strains its neck forward, spreads its wings and steps off the cliff. Its flaps are slow and [we] are losing altitude [--] so fast that at first I think [we][']ll fall into the ocean [--] but it takes only five or six flaps before the bird is able to glide straight out to sea." instead.

[The roc flies more or less the way condors fly, as documented by youtube. I'm not sure how far a bird of the roc's mass would need to descend before starting to glide, and almost certainly I haven't given it enough clearance, but text is at least more forgiving than an animation would be.]

Roc-soaring is a scene. Roc-soaring begins when roc-flight ends. Roc-soaring ends when the time since roc-soaring began is greater than 1 minute.

Understand "hang on" as a mistake ("That is about the only thing [we] can do.") when the player is held by the roc.

Sanity-check waiting during roc-soaring:
	say "That is about the only thing [we] can do." instead.

Sanity-check doing something other than acting fast during roc-soaring:
	say "I don't know about you, but I don't dare do anything but hang on." instead.

Sanity-check going somewhere during roc-soaring:
	say "I think [we] pretty much have to go where the roc wants to go." instead.

Instead of looking during roc-soaring:
	say "The ocean is many feet below; a fall might not kill us, but it would certainly hurt. The sun has nearly set, and the sky is a soft blue, tempered by the evening haze.

If [we] crane as far as [we] safely can over our left shoulder, [we] can see the coastline near my house, and the old city walls, and the last trails of sunlight on the water."

When roc-soaring ends:
	say "I keep thinking [we] [are] going to lose our grip, but somehow [we] manage to stay on. Sooner than I expect, [we] make out the shine of Slango's yacht, and in the next moment [we] [are] above it. The roc circles once or twice before coming to land on the Sunning Deck.";
	now the initial appearance of Slango is "Slango is out, having seen [us] landing. He looks nervy.";
	move the roc to the Sunning Deck;
	set the current interlocutor to Slango;
	[queue altern-where-is-Brock as immediate obligatory;]
	move the player to the Sunning Deck;
	follow the compass-drawing rule instead.

After looking when the location is Abandoned Shore and the roc is in Precarious Perch:
	move the roc to Abandoned Shore;
	say “A gust of wind lifts our hair, and [the roc] lands in front of [us].";
	continue the action.

After looking when the location is Open Sea and the roc is in Abandoned Shore:
	move the roc to Open Sea;
	say “The roc screams, and [we] look up to see it circling in the sky .";
	continue the action.

[The printed name of a fake-rock is "rock". Understand "rock" as a fake-rock. The description of the fake-rock is "It looks like a sedimentary stone."

The printed name of a plural-rock is "rocks". A plural-rock is plural-named. Understand "rocks" as a plural-rock. The description of the plural-rock is "A heap of fist-sized rocks suitable for cobbling a path."]

The rock-band is a person. The printed name of the rock-band is "rock band". Understand "rock" or "band" as the rock-band. The heft of the rock-band is 8. The description of the rock-band is "The band consists of a hot drummer, two long-haired guitarists, and a male vocalist with flame tattoos on his arms."
	The greeting of a rock-band is "[one of]The drummer waves, one of the guitarists gives [us] the finger, and the male vocalist offers to sign his name on the breast of our choice. 'Left, right, or I can do across both.'

[We] decline[or]The band offers another motley greeting[stopping]."



The rock-ballad is a noisy r-abstract thing. The printed name of the rock-ballad is "rock ballad". Understand "rock" or "ballad" or "song" as the rock-ballad. The description is "The rock ballad is a throbbing ball of guitar-filled air."

Instead of listening to the rock-ballad:
	say "Wailing guitars rend the air."

The description of the rodeo beaker is "A piece of scientific glassware with the usual measurement markings on the side, but also painted with the image of a bucking bronco."

The rollback-ad is privately-named. The printed name is "rollback ad". Understand "rollback" or "rollback-ad" or "rollback ad" as the rollback-ad. Understand "ad" as the rollback-ad when the ad is marked invisible. The description of the rollback-ad is "It's a two-page glossy magazine spread announcing that the Bureau of Orthography has ordered a rollback on the distribution of [Britishizing goggles], and therefore all current owners are 'advised and required' to exchange their purchase for a cash refund."

The description of the rood is "A substantial crucifix carved entirely of wood."

Ross is a proper-named man. The description of Ross is "He's red-headed and he looks oblivious to social cues. Someone you used to know, then, I take it?"

The description of the rotas inscription is
	"It reads: [paragraph break][fixed letter spacing]     P[line break]     A[line break]     T[line break]  A  E  O[line break]     R[line break]PATERNOSTER[line break]     O[line break]  O  S  A[line break]     T[line break]     E[line break]     R[variable letter spacing]". The rotas inscription is fixed in place.

Rule for printing the name of the rotas inscription  when not shooting:
	say "Roman inscription".
Understand "roman" or "letters" or "lettering" as the rotas inscription.
The initial appearance of the rotas inscription is "On the opposite wall is a cross of inscribed letters, this time Roman."

Some rucks and the ruck are r-abstract. The description of the ruck is "A fine fold in nothingness." The description of the rucks is "A row of creases in a piece of gossamer cloth."

The rump stabber is a man. The description is "He is of less than average height and carries a butterfly knife."

Every turn when the rump stabber is in the location:
	if a random chance of 1 in 3 succeeds:
		say "The rump stabber gets into a good position and then, before [we] can turn around, stabs [us] in the behind.";
		end the story saying "Ouch.";
	else:
		say "The rump stabber keeps trying to angle around and stand behind [us]."

The rum stabber is a woman. The description is "She looks around in bewilderment, but there's no rum for her to attack."

Table of Ultratests (continued)
topic	stuff
"rumper"	{ anagramming gun, bullets, rubber stamp }

Test rumper with "tutorial off / autoupgrade / open tub / load gun / shoot rubber stamp with gun / wave p-remover at rump stabber / gel rum stabber / shoot stamp / z / z / z" [holding the anagramming gun and the tub and the bullets and the rubber stamp.]

A rusty-nail-drink is a contained fluid edible thing. The printed name of a rusty-nail-drink is "Rusty Nail". Understand "rusty nail" or "rusty" or "nail" or "cocktail" or "drink" or "beverage" as a rusty-nail-drink. The description of a rusty-nail-drink is "A scotch-based beverage with Drambuie. It's tolerable, but not a fav[our]ite."
	Instead of burning the rusty-nail-drink:
		say "It's not quite pure enough alcohol to catch a flame, even if [we] had a good reason."


Section 15 - S

The heft of the sag is 1. The description of the sag is "The sag comes in the form of a doll-sized house with a distinct dip in the roof." Understand "house" or "doll-sized" or "dip" or "roof" as the sag.

The heft of the sagas is 0. The sagas are r-abstract. The description of the sagas is "Pages and pages of stories about the role of Atlantis during the battles between Spain and the Moors. There is a great deal of religious ferv[our] and emphatic feeling, but on the whole it sounds like a wretched, bloody time was had by all."

The description of the salt is "A quantity of fluffy, slightly blue sea-salt, such as is sometimes collected from the rocks down by shore." The salt is edible. The indefinite article is "some".

The description of the saltcat is "It's a mixture of salt, lime, and meal, used to catch pigeons." The indefinite article of the saltcat is "some".

The description of a sap is "A slack-faced fellow, eager to believe what he's told. The grin he's giving [us] right now is usually found only on golden retrievers."

The sap-dispenser is a closed container. The printed name is "sap dispenser". Understand "sap dispenser" or "dispenser" as the sap-dispenser. Understand "sap" as the sap-dispenser when the sap-liquid is marked invisible. It is fixed in place. The initial appearance of the sap-dispenser is "A sap dispenser hangs beside the mirror." The description of the sap-dispenser is "It's the kind where a squeeze will dispense sap into the sink[if the sap-liquid is not in the sap-dispenser]. It is also empty[end if]."

A sap-liquid is a fluid thing. The indefinite article is "some". The scent-description is "pine resin". The printed name is "sap". Understand "sap" as the sap-liquid. The description is "Sticky and yellow-col[our]ed goo from a tree, rather than 'sap' as in a person. But considering it comes from a dispenser, that was probably inevitable."

Sanity-check touching the sap-liquid:
	say "It would be sticky and be hard to get off." instead.

Sanity-check burning the sap-liquid:
	say "I think some resins might burn when dry, but I'm not sure that applies here, and in any case it wouldn't help." instead.

The sop-dispenser is a closed container. The printed name is "sop dispenser". Understand "sop dispenser" or "dispenser" as the sop-dispenser. Understand "sop" as the sop-dispenser when the sop is marked invisible. It is fixed in place. The initial appearance of the sop-dispenser is "A sop dispenser hangs beside the mirror." The description of the sop-dispenser is "It's the kind where a squeeze will dispense a sop into the sink[if the sop is not in the sop-dispenser]. It is also empty[end if]."

The satin-pin is wearable. The printed name of the satin-pin is "satin pin". Understand "satin" or "satin pin" as the satin-pin. Understand "pin" as the satin-pin when the pin is marked invisible. The description of the satin-pin is "It's a little brooch, probably not very valuable, bearing a blue and white satin rosette. The edges of the rosette have yellowed with age, and there is a blob of glue at the center that must once have held some additional decoration."

The satin-pint is a floppy container. The printed name of the satin-pint is "satin pint". Understand "satin" or "satin pint" as the satin-pint. Understand "pint" as the satin-pint when the pint is marked invisible. The description of the satin-pint is "It is a quilted satin version of a beer stein, though of course it would not be able to hold liquid at all reliably."

The saint-pint is a container. The printed name of the saint-pint is "saint pint". Understand "saint" or "saint pint" as the saint-pint. Understand "pint" as the saint-pint when the pint is marked invisible. The description of the saint-pint is "On the side of the pint glass is an image of [one of]Saint Arnold of Soissons, the patron saint of hop-pickers[or]Saint Brigit, who is said to have miraculously transformed her bathwater into beer[or]Saint Arnold of Metz, who cured his neighb[our]s of a plague by feeding them only beer[or]Saint Cuthbert, who lived exclusively on barley and is admired by maltsters[sticky random]."

The Scot is a man. The description is "The full stereotype: bagpipes, kilt, sporran. If you look at him too long, he says, 'Och.'" Understand "bagpipes" or "kilt" or "sporran" or "man" as the Scot.
	The greeting of the Scot is "'Och,' he says. A tic."

The scree is a fixed in place thing. The heft of the scree is 8. The description of the scree is "The scree is a sloping pile of small and mid-sized stones, most of them roughly broken off. They look like the result of a rockfall rather than of, say, long erosion by water." The indefinite article of the scree is "some". Understand "stones" or "stone" or "rubble" or "pile" as the scree.

Instead of climbing the scree:
	say "As soon as [we] get a step or two up the pile, [we] slide back down again."

A screwdriver-drink is a contained fluid edible thing. The printed name of a screwdriver-drink is "Screwdriver". Understand "screwdriver" or "cocktail" or "drink" or "beverage" as a screwdriver-drink. The description of a screwdriver-drink is "A cocktail of fresh-squeezed orange juice and vodka. It is hardly the grandest of cocktails, but it will do."
	The scent-description is "citrus and the faintest hint of alcohol".
	Instead of burning the screwdriver-drink:
		say "It's largely orange juice. It's not going to burn, and if it did, it would smell like Florida on fire."

The sed is r-abstract. The description of the sed is "A gibbering nonsense of streaming letters, slashes, and dots, which I am not able to interpret. Like perl, but older."


The setter automaton is a push-button device. The heft of the setter automaton is 3. The description of the setter automaton is "It's a little like one of those Japanese robot dogs, only substantially more old-fashioned and artisanal. Its bright glass eyes watch [us] expectantly and its ruddy fur is thick and lustrous.".

Instead of switching on the setter automaton:
	say "The setter automaton runs in a circle and barks several times, then sits expectantly and shuts off again."

The sere automaton is a push-button device. The heft of the sere automaton is 3. The description of the sere automaton is "The automaton is now reminiscent of a patrician Roman dried by long life and stoic practices. Its nose is narrow and aquiline, its lips thin, its eyelids heavy. Its only joy is self-denial.".

Instead of switching on the sere automaton:
	say "The lips of the face draw up into the faintest sneer. Nothing else occurs. After a moment the face relaxes again."

[A shit is a thing. The shit is crude.]


The sharp limit is an r-abstract thing. The heft is 0. The description of the sharp limit is "It displays a graph that drops off sharply when X reaches [a random number between 10 and 100] [one of]thousand[or]million[or]billion[or][at random]."

The shopping gab is an r-abstract noisy thing. The heft is 0. The description of the shopping gab is "Borrowed from some shopping channel, a perky announcer voice offers discounts on various Atlantean products."

Instead of listening to the shopping gab:
	say "The announcer currently wants [us] to know about [one of]cubic zirconia earrings in the shape of a squid plant[or]a juicer for making chard smoothies in your very own kitchen[or]the price of a pair of new hand towels[at random]."

The description of the shred is "Just a torn rag-end of cloth. It is white and blue, and bears every evidence of having been part of an obligatory-service uniform." The shred is floppy and wearable. The heft of the shred is 1.

Report wearing the shred:
	say "[We] try out the shred as [one of]an armband[or]a necktie[or]a hair-wrap[at random]. It can't be very attractive." instead.

Some shrimp cocktail is an edible thing. The scent-description is "tomato and seafood". The description is "A floridly red concoction of tiny shrimps in sauce[one of]. I've always considered it vulgar[or][stopping]."

Sanity-check tasting the shrimp cocktail:
	say "Honestly, I find that stuff vile, so please let's not." instead.

Instead of searching the shrimp cocktail:
	say "A few finger-prods through the red sauce verify that there are only more shrimps within."

Table of Ultratests (continued)
topic	stuff	setting
"alterna-shuttle"	{ power cord, pastis, shuttle }	Workshop

Test alterna-shuttle with "tutorial off / unlegend / plug in dais / e / put pastis in bucket / w / put shuttle on dais / pull lever / turn switch / pull lever / get in shuttle / drive e" [holding the power cord and the pastis and the shuttle in the Workshop.]

The alterna-shuttle is a car. The printed name of the alterna-shuttle is "shuttle". Understand "shuttle" or "car" as the alterna-shuttle. The heft of the alterna-shuttle is 6. The description of the alterna-shuttle is "It is predictably painted blue, but it's just a little patrol shuttle, not one of the big vans for carting away arrested citizens." The flexible appearance of the alterna-shuttle is "blank". The introduction of the alterna-shuttle is "".

The description of the shuttlecock is "A curious object used in badminton: it's a plastic projectile shaped like a ball into one half of which a circle of feathers has been stuck. This makes it fly less quickly and more visibly."

A sick is a fluid thing. The indefinite article is "some". The scent-description is "stomach acid". The description is "It looks like half-digested pepperoni pizza, plus miscellaneous juices."

The description of the sickest offal is "There's a pile of semi-recognizable animal bits, of which I think I might recognize a dried sheep's head. Back in my grandparents['] day Atlantis was quite a lot poorer and these kinds of foods were considered acceptable [--] even a delicacy [--] but you won't catch them in the markets these days." The indefinite article of the sickest offal is "the".
	The scent-description of the sickest offal is "warm blood".
	Understand "sheep" or "head" or "dried" or "sheep's" or "ram" or "bull" or "goat" or "testicle" or "foot" or "lung" or "giblet" or "appendix" or "gall" or "bladder" as the sickest offal.

Instead of searching the sickest offal:
	say "I'm not sure, but I think that thing there might be a [one of]bull[or]goat[or]ram[at random] [one of]testicle[or]lung[or]foot[or]ear[or]giblet[or]appendix[or]gall bladder[at random].

Then again possibly not."

The description of a sig is "It reads: [fixed letter spacing][line break][line break]-- [line break][if the item described is proffered by the garish sign]GREED IS GOOD. -- Gordon Gekko[otherwise if the item described is proffered by the carton-sign]Education is its own end[otherwise if the item described is proffered by the last-sign]The contents of the foregoing post may not be reproduced in any medium without the permission of the sender[otherwise]Decisions are made by those who show up.[line break] -- President Bartlet, by way of Harry S. Truman and/or Woody Allen. Or maybe Abe Lincoln.[end if].". A sig is r-abstract.

[carton-sign, garish sign, last-sign, otherwise]
The last-sign is a sign. The printed name is "sign". Understand "sign" as the last-sign. The description is "It reads: 'Reminder: the six-monthly security remodel is set for this coming Tuesday. Please be prepared to cooperate with the floorers as they install our new coprolite tiles, and leave no secure documents visible.'"

Instead of searching the last-sign:
	say "There is no point searching for a hidden meaning; the overt meaning is peculiar enough."

The signet is a wearable thing. The heft of the signet is 1. The description of the signet is "[signet-desc]." The signet has a number called the iteration.

To say signet-desc:
	if the iteration of the signet is:
		-- 0: say "It's an outsized signet bearing the crest of the Bureau of Orthography, now hanging around the statue's neck in the same way that the sign did a moment ago";
		-- 1: say "It's an outsized signet again, but there's no Bureau crest. This time it carries the symbol of the University of Atlantis. There are a lot of students in this crowd, and maybe the All-Purpose Officer's not able to exert much local field influence on the transformation with so many others around";
		-- 2: say "This time the signet depicts [--] well, I can't imagine this is a common heraldic device, but I think I'd call it a Squid Rampant. The crowd has taken control of the imagery";
		-- otherwise: say "The Squid Rampant is back".

The description of the sill is "It is a bit useless and disembodied without an accompanying window, but here it is: a board of white painted wood." The sill is long. Understand "board" as the sill when the board is marked invisible and the location is not the Rotunda. [bulletin board crossover]


The sillage is an r-abstract thing. The indefinite article is "some". The description is "There's nothing to see or hear, just an extremely powerful od[our] of someone else's perfume.". The scent-description is "jasmine".

A sin is r-abstract. The heft of a sin is 0. The description of a sin is "An abstract representation of [if the item described is proffered by the garish sign]greed: a nice classic sin represented in this case by the image of men counting money, wall street executives in private jets, pretty women marrying men three times their age, and an assortment of similar typical concepts[otherwise if the item described is proffered by the carton-sign]adherence to the abstract and the ineffectual, characterized by a woman in academic robes reading in the midst of a disaster zone, where bodies are scattered everywhere[otherwise if the item described is proffered by the last-sign]pettiness of spirit and obsession with needless detail, embodied by the image of a grey-skinned bureaucrat and a huge heap of papers[otherwise if the item described is proffered by a sink]carelessness with resources, represented by the wasteful use of a great deal of clean water[otherwise]willfully clueless meddling. Not a sin known to Dante, perhaps; but modern democracies have their own forms of wrong-doing[end if]."
	The scent-description of a sin is "[if the player wears the Britishizing goggles]sulphur[otherwise]sulfur[end if]".
Instead of listening to a sin:
	if the item described is proffered by the garish sign:
		say "There is the faintest chink like the stacking of coins.";
	otherwise:
		say "A gabble of voices, commenting and petitioning and commenting again.".

Sanity-check washing a sin:
	say "This is all endearingly metaphysical, but [we] can't." instead.
Sanity-check entering a sin:
	say "Entering a state of sin, do you mean? I have the impression you have that covered." instead.

[The description of the slack towel is "Not easy to tell from any other towel, though an argument could be made that it's especially limp and lacking in pile."

A dangerous construction rule for the slack towel:
	say "Unfortunately, having the towel swapped in for an electrical component starts a small fire, which soon sets off the alarms in the area.";
	end the story saying "That could have gone quite a lot better".]

The slat is a long thing. The description of the slat is "A board of white wood, probably extracted from a fence somewhere."

Table of Ultratests (continued)
topic	stuff	setting
"snapping"	{ snap, ring }	Drinks Club

Test snapping with "show snap to bartender / show ring to bartender / listen" [holding the snap and the ring in the Drinks Club.]

The alterna-snap is an r-abstract noisy thing. The heft of the alterna-snap is 0. The printed name is "snap". Understand "snap" as the alterna-snap. The description of the alterna-snap is "It sounds like a whole chorus of guys in hair gel, snapping for all they're worth."

Instead of listening to the alterna-snap:
	say "[one of]The snap makes an, er, snapping noise.[or]Snap! goes the snap.[or]The snapping continues.[stopping]".


The description of the snap is "A metal fastener for clothing, both halves. It looks ready to be sewn in."

The sock is a wearable thing. The description of the sock is "A single unpaired sock. It is sized for a girl's foot and striped in alternating bands of black, white, and pale blue."

A sod is a naughty-sounding thing. The heft of the sod is 4. The indefinite article is "some". The description is "A rectangular piece of dirt with fresh grass growing in it, ready to be laid down in someone's lawn."

The description of the sop is "A hunk of bread dipped in something red-brown.". The sop is edible. The heft of the sop is 1. The scent-description is "stale wine".

The description of a son is "He's the ideal image of Atlantean boyhood. Missing front tooth, tan from days in the sunshine, a spelling commendation patch sewn into his shirt." Understand "missing" or "front tooth" or "tooth" or "atlantean" or "boyhood" as the son.

The sord is a bird. The heft of the sord is 8. The description of the sord is "A whole flight of mallards; only not in flight at the moment. They're currently quacking and pecking the [ground]."

The sot is a man. The description is "He's wearing a stained wife-beater shirt and looks like he hasn't been sober since 1982. Note the four-day beard." Understand "wife-beater" or "shirt" or "beard" or "four-day" as the sot.
	The greeting of the sot is "The sot looks at us blearily but does not attempt speech."

The spa is fixed in place. The heft of the spa is 12. The description of the spa is "It's building given over to Atlantean care treatments: the conversion of warts, manicures and pedicures in clever col[our]s, the reshaping of hairdos."

The spat is r-abstract. The spat is noisy. The description of the spat is "It sounds like an argument, vehemently in progress, about [one of]whether the couple has missed the last bus down-island[or]which café serves the best Coffee Atlantic[or]whether it's wise leaving the bedroom window open while sleeping[or]whether to take the new DCL job though it involves a lot of travel[at random]."

The description of the splat is "An artful, bright white splat. Fortunately it looks more like paint than like bird droppings. At least, so I choose to interpret it."

The spillage is a fluid. The indefinite article of the spillage is "some". The description of the spillage is "It looks a lot like the spill, only possibly more toxic." The scent-description is "nauseating chemicals".
	Instead of burning the spillage:
		say "Whatever this is, spreading a toxic haze of it over the environment would be a terrible idea."

The spit is a long thing. The heft of the spit is 2. The description of the spit is "It's a long metal bar, the sort you might use to skewer a piece of meat for cooking over a fire."

The heft of the spot is 1. The description of the spot is "A small scrap of paper with a black spot on it[one of]. It has no obvious uses, other than perhaps to deliver sinister warnings to one's pirate acquaintances[or][stopping]."

The spouted cutoffs are wearable. They cover the legs-area. The description of the spouted cutoffs is "They're an extraordinary steampunk creation: trousers sawn off just above the knee, but along the outside of each thigh, where military trousers might have a smart red stripe, there is instead a length of copper tubing. They must be the world's only short pants with their own gutter system."

The stag is an animal. The description of the stag is "He's very large and very wild, the antlers alone filling more volume than you would have thought possible.".

The stage is r-abstract. The description of the stage is "The concept of a stage in development, represented by the image of an insect still in the larval form."

The starry veil is a wearable thing. The description of the starry veil is "A veil of fine black gauze, long enough to fall at least to the knee on a woman of average height. It is studded all over with gleaming silvery stars; not mere rhinestones, but ornaments that cast their own faint unmistakeable light. Some are pure white, some pale blue, some small and red-hued. But they are safe and cool and do not burn to touch."

The stats are r-abstract. The description of the stats is "Floating[one of][or] [primary-color][or] [secondary-color][or] [primary-color] and [secondary-color][at random] [one of]columns of numbers[or]pie charts[or]bar charts[or]maps[at random] describe the [stat-type].".

Definition: a thing is synthesizable:
	if it is the quip-repository:
		no;
	if it is a quip:
		no;
	if it is a fact:
		no;
	if it is a subject:
		no;
	if it is fixed in place:
		no;
	if it is scenery:
		no;
	if it is part of something:
		no;
	yes.

To say stat-type:
	say "[one of]average[or]mean[or]median[or]standard deviations in[or]modal distribution in[at random] ";
	let Custom be "size";
	let Generic be text;
	let P be a random seen synthesizable thing which is not r-abstract;
	if P is an animal:
		if P is a man or P is a woman:
			now Custom is "[one of]political affiliation[or]internet usage[or]education levels[or]incarceration rates[or]income[or]rates of listening to [music-type][at random]";
		else if P is a bird:
			now Custom is "[one of]migration distance[or]wingspan[at random]";
		else:
			now Custom is "feed price";
		now Generic is "[one of]height[or]weight[or]lifespan[or]weekly caloric intake[or]daily water consumption[or]birthrate[or]girth[at random]";
	else:
		if P is noisy:
			now Custom is "decibel level";
		if P is long:
			now Custom is "length";
		if P is fluid:
			now Custom is "specific gravity";
		if P is edible:
			now Custom is "[one of]caloric content[or]gluten content[or]glycemic index[at random]";
		now Generic is "[one of]diameter[or]height[or]thickness[or]depth[or]weight[or]price[at random]";
	say "[one of][Custom][or][Generic][at random]";
	if P is plural-named:
		say " of [P]";
	else:
		say " of [a P]";
	say "[one of] in [nationality] towns[or] worldwide[or] under varying weather conditions[or], with attention to possible seasonal variations[or] immediately following an important election[or] as measured on alternate Thursdays[as decreasingly likely outcomes]";

The steer automaton is a push-button device. The heft of the steer automaton is 3. The description of the steer automaton is "Real leather covers a framework of wood and steel, making an alert and surprisingly lifelike model steer.".

Instead of switching on the steer automaton:
	say "The steer bucks, snorts, and shakes its head very plausibly before falling still again."

The description of a stent is usually "I was expecting a length of clear plastic tubing, but no. This is made of a metallic mesh, and so designed that when inserted into an occluded part of the esophagus, it will expand itself automatically. It's a palliative measure for inoperable cancers of the digestive tract."



The street automaton is a push-button device. The heft of the street automaton is 3. The description of the street automaton is "It's an extremely elaborate representation of Long Street about a hundred years ago, with cuckoo-clock machinery and wood-and-glass storefronts."

Instead of switching on the street automaton:
	say "The clockwork makes tiny high-waisted women stroll up and down Long Street, and tiny boater-hatted men bow to them and pick up their handkerchiefs. The palm trees wave in an automated wind.".

[The stick is here as a back-up puzzle solution for propping up the trap door or hitting the pinata. Originally the warning was called a warning note instead and wasn't written on a sticky.]

The description of the stick is "It is about two feet long. It's stripped of its leaves and fairly sturdy.". The stick is long. The heft of the stick is 2.
	The scent-description is "sap".

The stickball is an r-abstract thing. The description is "A pick-up sandlot game, represented here by the image of small dusty figures running around, transparent as holograms." The heft of the stickball is 0.

[The description of the still is "A room-sized installation of glass tubes and boilers. Inside is some kind of illicit moonshine product." The still is a container. The heft of the still is 12. The still is transparent.
	The scent-description of the still is "heady quantities of ethanol".]

The stillage is a supporter. Instead of putting something on the stillage: say "The stillage is really designed to support kegs and nothing else." The description of the stillage is "It's a rack for supporting kegs in such a position that they can be easily tapped and used."

The description of a stink is "It's an intense, dizzying smell with great staying power and speed over distance." The scent-description is "over-ripe French cheese".

A stint is r-abstract. The heft of a stint is 0. The description of a stint is "The flickering images represent a period of time spent in the military: a young person in a uniform, smartly waving goodbye to hazily-sketched parents; a duration of training and mostly boring patrol work; release from service, with a few mild commendations and some pocket money."

The stoats are an neuter animal. The description is "There are several of them, and they look like brown weasels. The tips of their tails are brown, and their eyes are beady, and they look like a whole bag-ful of trouble."

The stock is an edible contained fluid thing. The indefinite article is "some". The description of the stock is "Pale brown stock, still warm and steaming slightly." The scent-description of the stock is "hearty fish reduction".

The description of the stop is "A metal key pried off a musical instrument. It's not much use by itself.".

The description of the stopcock is "A valve made of glass components, designed to be used with laboratory equipment and tubing. It looks very clean and very official[if stopcock is open]. The valve is currently open[end if]." The stopcock can be openable. The stopcock can be open. The stopcock is openable and closed.

The stoppage is r-abstract. The description of the stoppage is "It is the very essence of non-action, though you might not guess that, as it looks exactly like an angry man waving a bag of gnocchi.

There was a spot of trouble four or five years ago when some disgruntled customs workers refused to carry on applying linguistic realignment to licensed imports. A lot of foreign food rotted on the docks before it could be converted into English-language food." The scent-description is "aging foodstuffs".

The stopwatch is a device. The description is "One of those round, silvery stopwatches with a start/stop button on the top[if switched off]. It is currently stopped[otherwise]. It currently reads [stopwatch elapsed time]. The second hand sweeps around steadily[end if]." The stopwatch has a time called the time started. Understand "start/stop" or "button" as the stopwatch.

Carry out switching on the stopwatch:
	now the time started of the stopwatch is the time of day.

To say stopwatch elapsed time:
	let residual be the time of day minus the time started of the stopwatch;
	say residual in words.

Report switching off the stopwatch:
	say "[We] turn off the stopwatch at [stopwatch elapsed time]." instead.

The strolling cap is a floppy wearable thing. It covers the head-area. The description is "A cotton cap with an extra long peak, which might offer a strolling person some protection against the sun." Understand "peak" and "cotton" as the strolling cap.

The strolling chap is a man. The description is "He is walking back and forth, forth and back, at a leisurely pace, as though he had all the time in the world."

A suid is an animal.
The initial appearance is "The suid stands stupidly nearby, looking dazed."
The description is usually "A suid is any kind of pig; this one is a sizable domestic sow, as it turns out."

A sud is a thing. The heft of the sud is 1. The description of the sud is "A solitary soap-bubble, clinging iridescently to the nearest surface."

Instead of doing something when the sud must be touched:
	say "The sud pops on contact.";
	now the sud is nowhere.

The printed name of the sun-product is "sun". The heft of a sun-product is 1000. The description of a sun-product is "A mass of incandescent gas [--] well, plasma, really [--] fleeting thoughts and then".

The description of the sunlit ray is "Not a ray of sunlight. No. This is a dead sea-creature, perpetually lit as though by the sun." The sunlit ray is edible. The scent-description is "seawater".

Instead of eating the sunlit ray:
	say "There is something other-worldly about this thing. I don't think [we] should eat it."

Instead of smelling a the sunlit ray:
	say "The sunlight has not caused the ray to rot yet: consider yourself lucky."

[The skit is a man. The initial appearance is "A man wearing a tea-towel over his head is enacting some kind of skit about Gandhi." The skit wears a tea-towel. The skit is r-abstract. ]

The description of the swat is "The smack of a hand, or the slap of a rolled-up newspaper hitting a fly: you perceive it as a comic-book sound.". The swat is r-abstract and noisy.

Instead of listening to the swat:
	say "The noise is sudden and percussive, and repeats without a clear rhythm."

The description of the swath is "The width of a strip of cut grass, which is to say, the width of a scythe-blade. The distance is measured out in delicate blueprint lines in midair.". It is r-abstract. The swath is r-abstract.

The description of the swatch is "A worthless strip of cloth in a tartan print, a few inches wide and maybe a foot and a half long." The heft of the swatch is 1. The swatch is floppy and wearable.




The description of the sword is "It's a slender, flexible 18th-century sort of affair, rather than a medieval broadsword. Possibly a valuable antique: this might have been an exercise in smuggling.". The sword is long. The heft of the sword is 2.


The sword-arm is a thing. The printed name is "sword arm". Understand "sword" or "arm" or "inexplicable" or "tattoo" or "muscles" or "bicep" as the sword-arm. The description of the sword-arm is "A man's arm, muscled and scarred and thickly-furred with hair. A tattoo on the bicep portrays what looks like a Spanish galleon at full sail."

The description of the swordstick is "It's a stick the col[our] of rosewood with a sturdy, no-nonsense handle. The sword that is supposed to snap into it is, however, inexplicably missing." The swordstick is long. The heft of the swordstick is 2.
	The scent-description of the swordstick is "polished wood".


Section 16 - Tale


A tale is r-abstract. The heft of a tale is 0. The description of a tale is "It begins with 'once upon a time,' and the letters scroll up delicately through the air when [we] start to read. It weaves a narrative as follows: [paragraph break][i][generated tale][/i]".

Report examining the tale:
	record "Propper Rosehip award for talespinning" as an achievement;
	continue the action.

[Here we want to suggest a timeless but suitably Atlantean fairy tale, which we assemble in pseudo-Proppian fashion. Themes hint at rampant xenophobia, problems with distribution of wealth, concerns about class and democracy, the roles of science and superstition, ambivalence towards organized religion, layers of polytheistic as well as Christian culture, superstitions about language, uncertainty about the behavior of abstract objects and beings, and other problems that presumably were emerging over the past several centuries of Atlantean history.

At the same time, the tale is generative and intentionally reveals its generative nature, rather than just being a cycling set of pre-written tales, for two reasons. One, I like some of the curious themes or idiosyncracies that arise from the automatic generation (e.g., a hero who defeats a robber... using something he himself stole; the hero gives up literacy to appease an angry demon and is rewarded with a mask of words...); two, many of the game's abstracts do have a randomized, unstable quality and I wanted to continue that feel.

Conceptually, this passage also owes a little bit to Andrew Plotkin's Matter of the Monster and to some blog comments from Ian Millington about generating semi-random content. It's not, of course, a particularly elegant implementation of that concept.

Contrast the inciting fable, which needs to have one and just one form.]

A tale-apex is a kind of value. The tale-apexes are t-defeats, t-tricks, t-escapes, t-converts.
A tale-protagonist is a kind of value. The tale-protagonists are t-fisherman, t-farmer, t-banker.
A tale-villain is a kind of value. The tale-villains are t-foreigner, t-thief, t-monster, t-multiple-creature.
A tale-helper is a kind of value. The tale-helpers are t-squid, t-ass, t-beggar, t-spirit.
A tale-gift is a kind of value. The tale-gifts are t-sword, t-dagger, t-gun, t-disguise, t-diversion, t-grace.

The chosen tale-apex is a tale-apex that varies.
The chosen tale-protagonist is a tale-protagonist that varies.
The chosen tale-helper is a tale-helper that varies.
The chosen tale-gift is a tale-gift that varies.
The chosen tale-villain is a tale-villain that varies.

To say generated tale:
	let tale-resolution be text;
	let tale-donation be text;
	let tale-danger be text;
	let tale-intro be text;
	let tale-fight be text;
	[first assign values]
	now the chosen tale-apex is a random tale-apex;
	now the chosen tale-protagonist is a random tale-protagonist;
	now the chosen tale-helper is a suitable helper for the chosen tale-protagonist;
	now the chosen tale-gift is a suitable gift for the chosen tale-apex;
	now the chosen tale-villain is a suitable villain for the chosen tale-apex;
	[now assign text to values]
	let t-hero be text;
	let t-nom be "he";
	let t-obj be "him";
	let t-pos be "his";
	let t-activity be "doing the daily chores";
	if the chosen tale-protagonist is:
		-- t-farmer:
			let t-hero be the substituted form of "[one of]farmer[or][one of]wealthy[or]wise[or]poor[or]clever[or]brave[at random] [random vegetable]-farmer[or]seed farmer[or]farm-lab[our]er[or]harvester[or]citrus farmer[as decreasingly likely outcomes]";
			if a random chance of 2 in 3 succeeds:
				let t-activity be the substituted form of "[one of]watering the land[or]mending fences[or]getting rid of pests[or]tending the soil[at random]";
		-- t-fisherman:
			let t-hero be the substituted form of "[one of]fisherman[or]deep-sea fisher[or]clam-picker[or]shrimp-trapper[or]bivalve-farmer[or]pearl-diver[or]deep-sea scavenger[as decreasingly likely outcomes]";
			if a random chance of 2 in 3 succeeds:
				let t-activity be the substituted form of "[one of]cleaning the catch[or]working on some nets[or]repairing a boat[at random]";
		-- t-banker:
			let t-hero be the substituted form of "[one of]banker[or]money changer[or]money-lender[or]financier[as decreasingly likely outcomes]";
			if a random chance of 2 in 3 succeeds:
				let t-activity be the substituted form of "counting a [one of]tall [or]short [or][at random]stack of [one of]gold[or]silver[or]copper[at random]";
	if a random chance of 1 in 2 succeeds:
		let t-hero be the substituted form of "[t-hero]'s [one of]daughter[or]wife[at random]";
		let t-nom be "she";
		let t-obj be "her";
		let t-pos be "her";
	otherwise if a random chance of 1 in 2 succeeds:
		let t-hero be the substituted form of "[t-hero]'s son";
	let t-helper be text;
	if the chosen tale-helper is:
		-- t-squid:
			now t-helper is the substituted form of "[one of]giant [or]speckled [or]vast [or]white [or][at random]squid";
		-- t-ass:
			now t-helper is the substituted form of "[one of]miniature [or]brown [or]small [or]black-eared [or]hard-kicking [at random][one of]donkey[or]beast of burden[or]ass[at random]";
		-- t-beggar:
			now t-helper is the substituted form of "[one of]wrinkled [or]toothless [or]squinting [or]hardy [or]needy [or]starving [or][or][at random][one of]monk[or]mendicant[or]friar[or]beggar[or]vagabond[at random]";
		-- t-spirit:
			now t-helper is the substituted form of "[one of]secretive [or]wailing [or]powerful [or]cruel [or]subtle [or][or][or][or][at random][one of]spirit[or]god[or]demon[or]ghost[or]seraph[or]genie[or]representation of [one of]Poverty[or]Need[or]War[or]Charity[or]Justice[or]Purpose[at random][or]spirit of that town[as decreasingly likely outcomes]";
	let t-antagonist be text;
	if the chosen tale-villain is:
		-- t-foreigner:
			now t-antagonist is the substituted form of "[one of]man from a foreign land[or]woman speaking in a strange tongue[or]foreigner[or]sailor[at random]";
		-- t-thief:
			now t-antagonist is the substituted form of "[one of]brigand[or]thief[or]robber[or]bandit[or]wandering pirate[or]quack doctor[or]lying bishop[at random]";
		-- t-monster:
			if the chosen tale-protagonist is t-fisherman:
				now the t-antagonist is the substituted form of "[one of]kraken[or]leviathan[or]whale[or]wrathful spirit of the deep[or]disembodied power of the tides[as decreasingly likely outcomes]";
			otherwise:
				now the t-antagonist is the substituted form of "[one of]ferocious beast[or]great boar[or]ravaging beast[or][a random number between 2 and 12 in words]-[one of]headed[or]tailed[or]legged[or]tongued[at random] [one of]lion[or]dog[or]bull[or]unicorn[or]scorpion[or]tiger[at random][or]beast that can't be killed (because it has no name)[or]Epsilivore that walks the land eating the letter E[as decreasingly likely outcomes]";
		-- t-multiple-creature:
			now the t-antagonist is the substituted form of "horde of deplural[ize]d [one of]merchants[or]pirates[or]soldiers[or]spies from a foreign land[as decreasingly likely outcomes] traveling as one man";
	let t-tool be text;
	if the chosen tale-gift is:
		-- t-sword:
			now the t-tool is the substituted form of "[one of]rapier[or]cutlass[or]longsword[or]broadsword[or]shortsword[or]sword[at random]";
			if the chosen tale-helper is the t-ass:
				now the t-tool is "whip";
		-- t-dagger:
			now the t-tool is the substituted form of "[one of]poniard[or]dagger[or]knife[or]blade[at random]";
			if the chosen tale-helper is t-beggar:
				now the t-tool is the substituted form of "[one of]rusty[or]old[or]antique[or]much-used[at random] [t-tool]";
			if the chosen tale-helper is t-spirit:
				now the t-tool is the substituted form of "[one of]pure blue[or]shimmering spirit[at random] [t-tool]";
			if the chosen tale-helper is the t-ass:
				now the t-tool is "whip";
		-- t-gun:
			now the t-tool is the substituted form of "[one of]Spanish pistol[or]gun[or]musket[or]rifle[or]blunderbuss[at random]";
			if the chosen tale-helper is the t-squid:
				now the t-tool is "harpoon gun";
			if the chosen tale-helper is the t-ass:
				now the t-tool is "whip";
		-- t-disguise:
			now the t-tool is the substituted form of "[one of]mask[or]veil[or]robe[or]cloak[at random]";
			if a random chance of 1 in 3 succeeds and the player is wearing something:
				now the t-tool is the substituted form of "[random thing worn by the player]";
			if the chosen tale-helper is the t-spirit:
				now the t-tool is the substituted form of "[t-tool] made of [one of]cloud[or]darkness[or]words[at random]";
			if the chosen tale-helper is the t-squid:
				now the t-tool is the substituted form of "[one of]water[or]spume[or]black ink[at random]-col[our]ed [t-tool]";
			if the chosen tale-helper is the t-beggar:
				now the t-tool is the substituted form of "[one of]stained[or]torn[or]tattered[at random] [t-tool]";
		-- t-diversion:
			now the t-tool is the substituted form of "[one of]flashing toy[or]lantern[or]mirror[or]firecracker[or]sound of an avalanche[or]bottled wind[at random]";
		-- t-grace:
			if the chosen tale-helper is the t-spirit:
				now t-tool is the substituted form of "[one of]sonnet[or]song[or]poem[or]spell of persuasion[or]tale of how Atlantis was founded[or]story of the Tower of Babel[as decreasingly likely outcomes]";
			otherwise if the chosen tale-helper is the t-squid:
				now t-tool is the substituted form of "[one of]gift of lying[or]pair of spectacles that see through ink[at random]";
			otherwise:
				now t-tool is the substituted form of "[one of]wistful smile[or]bashful grin[or]raucous laugh[or]brilliant smile[or]beguiling tune[or]naughty limerick[at random]";
	let t-solution be text;
	if the chosen tale-apex is:
		-- t-defeats:
			now the t-solution is "[one of]destroy[or]kill[or]maim[or]attack[at random] the [t-antagonist]";
			if the chosen tale-helper is t-spirit:
				if the chosen tale-gift is t-sword or chosen tale-gift is t-dagger:
					let agonist be the substituted form of "[t-antagonist]";
					if agonist matches the regular expression ".(.*)":
						now agonist is the substituted form of "[text matching subexpression 1]";
					now t-solution is "slice off a piece of the [t-antagonist] so that only [agonist] was left";
			if t-tool is "whip":
				now t-solution is "[one of]beat the [t-antagonist] into obedience[or]make the [t-antagonist] into an obedient servant[at random]";
		-- t-escapes:
			if the chosen tale-gift is t-disguise:
				now the t-solution is "[one of]conceal [t-obj]self[or]hide[at random] from the [t-antagonist]";
				if a random chance of 1 in 2 succeeds:
					now the t-solution is "blend in with the landscape so that the [t-antagonist] did not notice [t-obj] though he passed only a few inches away";
					if the chosen tale-helper is the t-beggar or the chosen tale-helper is the t-ass:
						now the t-solution is "resemble a soldier too ferocious to attack";
			if the chosen tale-gift is t-diversion:
				now the t-solution is "[one of]distract[or]draw away the attention of[at random] the [t-antagonist]";
		-- t-tricks:
			if the chosen tale-gift is t-disguise:
				if the chosen tale-helper is t-beggar and the chosen tale-villain is t-thief:
					now the t-solution is "trick the [t-antagonist] into thinking the [t-hero] hand nothing worth stealing";
				otherwise:
					now the t-solution is the substituted form of "trick the [t-antagonist]";
			if the chosen tale-gift is t-diversion:
				now the t-solution is "trick the [t-antagonist] into going the wrong way and being trapped";
			if t-tool is "mirror" and the chosen tale-villain is t-multiple-creature:
				now the t-solution is "show the [t-antagonist] its reflection and drive it to madness";
		-- t-converts:
			now t-solution is the substituted form of "[one of]soothe[or]tame[or]make a friend of[at random] the [t-antagonist]";
	now tale-intro is "Once upon a time...";
	now tale-fight is "Using the [t-tool], [t-nom] was able to [t-solution]. ";
	now tale-resolution is "So the [t-hero] [one of]triumphed[or]succeeded[or]prevailed[or]was triumphant[or]won out[or]was successful[or]was victorious[at random]. ";
	if t-tool is "gift of lying":
		now tale-resolution is "'Is there anything this gift cannot achieve?' asked the [t-hero] of the [t-helper]; and the [t-helper] replied, 'No, it is the strongest weapon there is.' ";
	let V be a random number between 1 and 7;
	if V is:
		-- 1:
			now tale-intro is "Once upon a time, a [t-hero] was [one of][or]hard at work [or]busy [or]in the middle of [at random][t-activity]. ";
			now  tale-danger is "Then the [t-hero] met with a [t-antagonist]. [one of]For a time [t-nom] thought that [t-nom] and [t-pos] family were done for, so great was [t-pos] fear, when[or]Everything seemed lost until[or]The [t-hero] did not know what to do until[or]The [t-hero] was overwhelmed with terror until[at random] [one of][t-nom] remembered the [t-tool][or][t-nom] found the [t-tool] again among [t-pos] possessions[or]the [t-tool] spoke aloud saying USE ME, USE ME[at random]. ";
		-- 2:
			now tale-intro is "Once upon a time, the supreme councilor of the land was captured by a [t-antagonist][one of], and no one but a [t-hero] dared try to save him[or]. A brave [t-hero] set out to save him[or], and only a [t-hero] was willing to go after him[at random]. ";
			now t-activity is "searching high and low for the councilor";
			now  tale-danger is "At last the [t-hero] found the lair of the [t-antagonist]. ";
			now  tale-resolution is "[tale-resolution]The councilor was saved, and all rejoiced. ";
		-- 3:
			now t-activity is "[one of]treasure[or]a cure to the plague[or]fresh water[or]the world's hottest spice[or]the world's sweetest honey[or]the world's end[or]the Secret Word that Makes Men Live Forever[or]the 27th letter of the alphabet (which God gave secretly to the angels)[as decreasingly likely outcomes]";
			now  tale-danger is "When at last the [t-hero] [one of]discovered the hiding place of the [t-activity][or]tracked down the [t-activity] with subtle maps and a magic compass[or]uncovered the [t-activity] by asking every merchant and priest in the land[or]came to the hidden place where the [t-activity] could be found[at random], [t-nom] found it was being guarded by a [t-antagonist]. ";
			now  tale-resolution is "[tale-resolution]The [t-hero] and [t-pos] whole town lived [one of]healthily[or]wisely[or]joyfully[or]wealthily[at random] ever after. ";
			if t-activity is "the world's end" and chosen tale-villain is t-foreigner:
				now tale-resolution is "[tale-resolution]But all were amazed that the world's end should be in a foreign land rather than their own. ";
			otherwise if t-activity is "the 27th letter of the alphabet, which God gave secretly to the angels":
				now tale-resolution is "[tale-resolution]And the names of the people in that town cannot even be spoken by an ordinary person, because of the secret letter which is beyond pronouncement. ";
			now t-activity is "[if the chosen tale-protagonist is the t-fisherman]sailing the sea[otherwise]wandering the land[end if] in search of [t-activity]";
			now tale-intro is "Once upon a time, a [t-hero] was [t-activity]. ";
		-- 4:
			if chosen tale-gift is not t-grace and t-tool is not "whip":
				now t-tool is the substituted form of "[one of]golden[or]silver[or]ruby-studded[or]emerald-embossed[or]gilded[or]small electrum[at random] [random animal which is not plural-named]";
			now tale-intro is "Once upon a time there was a [t-hero] who had inherited a [t-tool] from [t-pos] father and [t-pos] father's mother before him. But the [t-tool], alas, was stolen; and as it was the most valuable thing the family possessed, [t-nom] went out in search of it. ";
			now t-activity is "scouring the land for what was lost";
			now tale-resolution is "After much struggle, the [t-hero] was reunited with the [t-tool], which the [t-helper] had stolen [one of]to give away as a bribe[one of]. And this is the danger of corruption when all tasks, even small ones, require a bribe[or][or][at random]. [or]out of hunger. [or]out of need. [or]out of a love of trouble. [purely at random]";
			if t-tool is "whip":
				now tale-resolution is "The [t-hero] took the [t-tool] away from the [t-helper] and began to beat him with it savagely, though he had stolen it in order to escape beating. So it is that disobeying a tyrant may only make him more tyrannical. ";
			now tale-danger is "'You have stolen this [t-tool]!' exclaimed the [t-hero] in rage. 'For it has belonged to my family for generations.' ";
			now tale-fight is "[one of]'What!' said the [t-helper]. 'Do you think there is only one [t-tool] in existence?' But the [t-hero] knew this was a lie. [or]'You did not truly need it,' protested the [t-helper]. [or]'All things belong to all people in common,' suggested the [t-helper], because he subscribed to a heretical philosophy of property ownership. But the [t-hero] snorted derisively. [or]Then the [t-helper] and the [t-hero] argued over the [t-tool]. [at random] ";
		-- 5:
			now tale-intro is "Once upon a time, [one of]a certain witch read in the cards that[or]there was a prophecy that[or]a prophetess saw in a dream that[at random] the island of Atlantis would sink into the sea. [one of]And no one believed this prophecy except for[or]But the prophetess did not tell everyone; she gave the secret only to[or]No one cared except[or]This message fell on deaf ears, except for[at random] a [t-hero], who set out to prevent it. ";
			now t-activity is "going door to door looking for anyone who could offer advice";
			now tale-danger is "The [t-hero] traveled for many nights and days into the cen[ter] of the island, until [t-nom] came to a deep hole in the earth. Going down in the deep hole, [t-nom] found stairs that descended for ten thousand steps. And at the bottom of the hole was a great cave; and there in the cave was a winch that held the island up. And beside it was a [t-antagonist] who was preparing to turn the winch and plunge the island into the sea. ";
			now tale-resolution is "Thus the island was saved from sinking. ";
		-- 6:
			now tale-intro is "Once upon a time, a [t-hero] was separated from [t-pos] beloved. ";
			now t-activity is "looking everywhere for the beloved's footprints";
			now tale-danger is "When at last the [t-hero] tracked down [t-pos] beloved, [t-nom] found that the beloved was living in thrall to a [t-antagonist][one of], beguiled by [t-pos] charms[or], and was even about to marry [t-obj][or], having forgotten the [t-hero] completely thanks to a magic spell[or][at random]. ";
			now tale-resolution is "[tale-resolution]Thus [t-nom] was reunited with [t-pos] beloved. ";
			if chosen tale-apex is t-converts:
				now tale-fight is "With the [t-tool], the [t-hero] wooed [t-pos] beloved back from [t-antagonist]. ";
				now tale-resolution is "[tale-resolution]From then on [t-nom] carried the [t-tool] everywhere, in case it should be needed again. ";
			otherwise:
				now tale-fight is "With the [t-tool], the [t-hero] was able to subdue the [t-antagonist] long enough for the spell on [t-pos] lover to fade, and the two were able to reunite. ";
		-- 7:
			now t-activity is the substituted form of "[random outdoors room]";
			now tale-intro is "Once upon a time, a [t-hero] was on a long journey to [t-activity] [one of]to fulfil the dying command of [t-pos] mother[or]at the wish of [t-pos] father[or]because the sheriffs of that land were searching for [t-obj][or]because of incurable wanderlust[or]because [t-nom] had heard such interesting tales of the place[or]in order to sell a cow[at random]. ";
			now t-activity is "heading for [t-activity]";
			now tale-danger is "Alas, on the way [t-nom] was set on by a [one of]ferocious[or]angry[or]hostile[or]blood-drinking[or]zombie[or]razor-toothed[or][at random] [t-antagonist]. ";
	now tale-donation is "help carrying a heavy burden";
	if the chosen tale-helper is t-beggar:
		now tale-donation is "[one of]a bowl of soup[or]a crust of bread[or]a hearty stew[or]something hot to drink[or][a random vegetable][or]a meal of spiced meats better than [t-hero] [t-obj]self got at home[as decreasingly likely outcomes]";
	if the chosen tale-helper is t-squid:
		now tale-donation is "[one of]aid cracking open some clams[or]assistance in re-building its home in the reef[at random]";
	if the chosen tale-helper is t-spirit:
		now tale-donation is "[one of]tribute[or]worship[or]obedience[or]aid in building a shrine[or]the abandonment of all scientific principles[or]that the [t-hero] willingly forget how to read[or]the burning of all the books in the land[or]prayers to cleanse the land of impurity[as decreasingly likely outcomes]";
	if V is 4:
		now V is a random number between 2 and 3;
	otherwise:
		now V is a random number between 1 and 8;
	if V is:
		-- 1:
			now  tale-donation is "In the midst of [t-activity], [t-nom] was interrupted by a [t-helper]. [one of]Though [t-nom] did not welcome the interruption, [or]Though [t-nom] was rude, [or]The [t-hero] said, 'Go away! Can't you see I'm busy?' But [or]The [t-hero] made a bad face, but [at random]the [t-helper] said, '[one of]I was sent to bring you this[or]A dream told me to present you this[or]I have brought you a[or]I wanted only to give you this[at random] [t-tool].' Thinking nothing of it, the [t-hero] went on [t-pos] way. ";
			if t-tool is "whip":
				now tale-resolution is "When the [t-helper] met with the [t-hero] again, the [t-helper] said, 'Now perhaps you see what sort of creature it is proper to beat!' [tale-resolution]";
			otherwise if t-tool is "mirror":
				now tale-resolution is "The [t-helper] returned to the [t-hero] and said, 'Now use your mirror on yourself, and see what kind of person you have become!' [tale-resolution]";
			otherwise:
				now  tale-resolution is "The next morning [t-nom] met the [t-helper] again. Astonished, [t-nom] said, 'Why did you give the [t-tool] to me after I treated you unkindly?' The [t-helper] replied: '[one of]I did this not for you, but for the whole island[or]I could not withhold that which it was your fate to receive[or]In payment, you and your family must always treat me well and bring me gifts[at random].' [tale-resolution]";
		-- 2:
			now  tale-donation is "[one of]While the [t-hero] was [t-activity][or]Just then[at random], a [t-helper] approached [t-obj] and demanded [tale-donation]. And though the [t-hero] was very busy, [t-nom] gave all the aid in [t-pos] power. In return, the [t-helper] gave [t-obj] a [t-tool]. ";
			if chosen tale-gift is t-diversion and t-activity is not "scouring the land for what was lost":
				now tale-donation is "[tale-donation]'I cannot imagine what use this will be,' thought the [t-hero]. But [t-nom] put it away in [t-pos] bag, because [t-nom] knew that there is a use for everything in its time. ";
		-- 3:
			now tale-donation is "A little while later, the [t-hero] was attacked by a [t-helper] and they fought almost to the death[one of]. When [t-pos] hands were around the neck of the [t-helper], [or], until growing desperate [at random]the [t-helper] begged for mercy. ";
			now tale-donation is "[tale-donation][one of]Out of soft-heartedness, the[or]Hoping for some reward, the[or]Reluctant to kill any creature, the[or]Though the red mist of rage was on [t-obj], the[or]Though afraid the [t-helper] might attack again as soon as [t-pos] back was turned, the[or]Eager to see whether it would lead to some reward, the[or]The[at random] [t-hero] agreed, and in thanks, the [t-helper] gave [t-obj] a [t-tool]. ";
			unless t-activity is "scouring the land for what was lost":
				if chosen tale-apex is t-tricks:
					now tale-resolution is "[tale-resolution]Thus we see that it is best to fight a weak enemy but deceive a strong one. ";
				if chosen tale-apex is t-escapes:
					now tale-resolution is "[tale-resolution]A weak enemy is the best preparation for a strong one. ";
		-- 4:
			now tale-donation is "While [t-activity], the [t-hero] met a [t-helper]. The [t-helper] begged of [t-obj] [tale-donation], but [t-nom] refused, saying [t-nom] had more important tasks at hand. ";
			if the chosen tale-villain is t-monster:
				now tale-danger is "At once the [t-helper] turned into a [t-antagonist] in its rage. ";
				now tale-fight is "Terrified, the [t-hero] cowered in apology. ";
			otherwise:
				now tale-danger is "A little later the [t-hero] was threatened by a [t-antagonist], and called out to the [t-helper] for aid. ";
				now tale-fight is "The [t-helper] came and at once defeated the [t-antagonist]. But when it was done, it turned angrily to the [t-hero]. ";
			now tale-resolution is "'Now you will not complete your task,' said the [t-helper], 'because you have not learned how easily one thing can turn into another. Go home, tell your story, and if your quest had any merit perhaps another will take it on.'";
		-- 5:
			if the chosen tale-helper is t-spirit:
				now tale-donation is "'Let me help you,' said a [t-helper]. 'If you are with me, I cannot fail,' replied the [t-hero].";
			otherwise:
				now tale-donation is "'Let me help you,' said a [t-helper]. 'I do not see how a [t-helper] can be any use at [t-activity],' replied the [t-hero]. 'But you are welcome to try.'";
			now tale-danger is "So the two of them were [t-activity] side by side, when they were approached by a [t-antagonist]. ";
			now tale-fight is "The [t-helper] brought forth a [t-tool] and used it to defeat the [t-antagonist], while the [t-hero] watched in astonishment. ";
			if the chosen tale-helper is t-spirit:
				now tale-resolution is "Then in a single moment, the [t-helper] completed the task that the two of them had set out to do. 'Why did you need me?' demanded the [t-hero], feeling hurt about [t-pos] lack of agency in the whole task. 'Because,' replied the [t-helper], 'I am forbidden to do anything unless there is a human who wishes it. But as your will was tested and proven, the deed too became possible.' ";
			otherwise:
				now tale-resolution is "'Alas, I have not succeeded in my task,' said the [t-hero]. 'No,' said the [t-helper] sharply. 'But at least you are not dead.' ";
		-- 6:
			now tale-donation is "While [t-activity], the [t-hero] came across a [t-helper] who possessed a [t-tool]. ";
			if chosen tale-helper is t-spirit:
				now tale-donation is "[tale-donation]'[one of]Whatever belongs to the spirits belongs to their people[or]What use is something like this to a spirit? I will take it[or]This is a strange thing for a shrine, and surely it would be more use to me[or]Someone must have left this here as an offering, but I have more need of it[at random],' said the [t-hero] to [t-obj]self. So [t-nom] took the [t-tool] away. ";
			otherwise if chosen tale-helper is t-squid and chosen tale-gift is t-sword:
				now tale-donation is "[tale-donation]'[one of]I don't suppose the squid has much use for this[or]What is one sword to a squid? He will need seven more[or]The squid can protect himself with clouds of ink, but I have no such def[ense][at random],' said the [t-hero] to [t-obj]self. So [t-nom] took the [t-tool] away. ";
			otherwise:
				now tale-donation is "[tale-donation]'[one of]I may have need of such a thing[or]That looks useful[or]Taking a thing is no crime if it is for a good cause[or]Possessions unguarded are no possessions at all[or]That would be a comfort to me where I am going[at random],' said the [t-hero] to [t-obj]self. And waiting until the [t-helper] slept, the [t-hero] took the [t-tool] and went away. ";
			if t-activity is "looking everywhere for the beloved's footprints":
				now tale-resolution is "[tale-resolution]And [t-nom] felt no shame about taking the [t-tool], because who would not go to the ends of righteousness in order to save a lover? ";
			otherwise:
				now tale-resolution is "[tale-resolution][one of]And the [t-hero] went to where the [t-helper] slept, and gave back the [t-tool] so that no one was the wiser[or]Cunning is truly the greatest weapon[at random]. ";
		-- 7:
			now tale-donation is "In the midst of [t-activity], the [t-hero] met a [t-helper]. ";
			if a random chance of 1 in 2 succeeds and the player is non-empty:
				now tale-donation is "[tale-donation]'Trade with me,' said the [t-hero]. 'I will give you my [random thing held by the player], and you can give me your [t-tool].' And so the [t-helper] agreed. ";
			otherwise:
				now tale-donation is "[tale-donation]The [t-helper] said, 'I have great need of [if the player is non-empty][a random thing held by the player][otherwise]a strand of your hair[end if], and will give you whatever you wish in exchange.' The [t-hero] thought long and hard, and then said, 'Give me your [t-tool].'";
		-- 8:
			now tale-donation is "While [t-activity], the [t-hero] found a [t-tool]. 'Take me with you!' said the [t-tool]. So [t-nom] shrugged and collected the [t-tool] for a later time. ";
			now tale-resolution is "[tale-resolution]Those who make the most of opportunities have the most successful results. ";
	say "[tale-intro][paragraph break][tale-donation][paragraph break][tale-danger][paragraph break][tale-fight][paragraph break][tale-resolution]";

To decide what tale-helper is a suitable helper for (P - a tale-protagonist):
	if P is:
		-- t-farmer:
			if a random chance of 1 in 2 succeeds:
				decide on t-ass;
			otherwise:
				decide on t-spirit;
		-- t-fisherman:
			if a random chance of 1 in 2 succeeds:
				decide on t-squid;
			otherwise:
				decide on a random tale-helper;
		-- otherwise:
			if a random chance of 1 in 2 succeeds:
				decide on t-beggar;
			otherwise:
				decide on t-spirit;

To decide what tale-villain is a suitable villain for (T - a tale-apex):
	if T is t-converts:
		decide on t-monster;
	otherwise:
		decide on a random tale-villain.

To decide what tale-gift is a suitable gift for (T - a tale-apex):
	if T is:
		-- t-converts:
			decide on t-grace;
		-- t-defeats:
			let R be a random number from 1 to 3;
			if R is:
				-- 1:
					decide on t-sword;
				-- 2:
					decide on t-dagger;
				-- 3:
					decide on t-gun;
		-- otherwise:
			if a random chance of 1 in 2 succeeds:
				decide on t-disguise;
			otherwise:
				decide on t-diversion;

To decide what tale-protagonist is a suitable protagonist for (T - a tale-apex):
	decide on a random tale-protagonist.


Section 17 - The rest of T

A tab is r-abstract. The description of the tab is "It lists the price of a tasting menu for two at the Literal Restaurant (seven courses not counting the intermezzo beet sorbet), with accompanying wine flights, espresso, and brandy to follow. Someone's expense account is shivering in a corner right now."

The heft of the tack is 1. The description of a tack is "The kind of thing used to stick papers to corkboards. It's pointy."

Sanity-check poking the player with the tack:
	say "That would probably be painful." instead.

Sanity-check poking a person with the tack:
	say "That would be unkind." instead.

Sanity-check poking a notepad with the tack:
	say "We have no need to pin [the noun] to anything (and no corkboard, either)." instead.

Sanity-check poking an r-abstract thing with the tack:
	say "[The noun] [aren't] sufficiently physical to interact that way." instead.

Sanity-check poking a fluid contained thing with the tack:
	say "Then [the noun] would dribble all over the place." instead.

Sanity-check poking something with the tack:
	say "There are no obvious advantages to making a very tiny hole in [the noun]." instead.

Table of Ultratests (continued)
topic	stuff
"tack"	{ tack, sord, fuel, era, jotter }

Test tack with "poke me with tack / poke sord with tack / poke fuel with tack / poke era with tack / poke jotter with tack / stick tack into fuel / poke tack into fuel / stick fuel with tack" [holding the tack and the sord and the fuel and the era and the jotter.]

The taco is edible. The description of the taco is "In the fried corn tortilla shell nestles a mixture of spiced pork, shredded lettuce, and sharp cheddar." The scent-description is "Mexican spices". Understand "spiced pork" or "pork" or "shredded lettuce" or "lettuce" or "sharp cheddar" or "cheddar" or "corn" or "tortilla" or "shell" or "fried" as the taco. The heft of the taco is 1.

A tad is r-abstract. The description of a tad is "A very small measure, like a teaspoon, of no particular substance."

The description of the tam is "A slightly baggy crocheted cap, striped in shades of blue and white." The tam is wearable. It covers the head-area.

The description of a tamale is "Still partly in its wrapper, and steaming hot. You seem to know what it is [--] some kind of traditionally Mexican or meso-American foodstuff? [--] but I can't say that it's familiar around here. The exterior looks like prepared corn dough of a fairly coarse kind. It smells like the interior may be spiced meat.". The tamale is edible.

Instead of searching a tamale:
	repeat with T running through tamales:
		now the description of T is "Still partly in its wrapper, and gradually cooling. You seem to know what it is [--] some kind of traditionally Mexican or meso-American foodstuff? [--] but I can't say that it's familiar around here. The exterior is prepared corn dough of a fairly coarse kind, and [we] have crumbled this enough to reveal the spiced meat inside.";
	say "Yup, definitely spiced meat."

Check waving the letter-remover at a tamale when the current setting of the letter-remover is "e":
	say "[The second noun] doesn't change, which is unsurprising considering a 'tamal' is just a different spelling of the same thing." instead.

A tan is an r-abstract. The heft of a tan is usually 0. The description is "The col[our] is the col[our] of bronzed skin as found on the beaches of Maui; a lazy, relaxed, year-round tan.". The scent-description of a tan is "coconut oil".

The description of the tat is "It looks like the end-of-day leftovers from a particularly ill-fav[our]ed yard sale. What's not broken is dirty, and what's not dirty is in terrible taste. There's a mechanical pencil with no lead, and a kiddie pool that leaks; a stained tablecloth that once had a pattern of squid tentacles on it; a bobble-headed doll representing the head player of the Maiana football team thirteen years ago."

The ten-object is r-abstract. The heft of the ten-object is 0. Understand "ten" or "10" as the ten-object. The description of a ten-object is usually "The concept of ten is here represented by [one of]the letter X[or]the digits 1 and 0[or]the digit A[or]an Olympic judge holding up a scorecard[or]two hands, exposing their fingers[or]two feet, spreading their toes[at random]." The printed name of a ten-object is usually "ten".

The description of the Tao is "It presents itself as one of those circular yin-yang symbols, part black, part white. I'm afraid Atlantean understanding of Asian philosophies is fairly simplistic."

A tap can be hot, unhot or undecided. A tap is usually undecided.

Before examining an undecided tap (called the target tap):
	if a random chance of 1 in 2 succeeds:
		now the target tap is hot;
	otherwise:
		now the target tap is unhot.

Understand "hot" as a hot tap. Understand "cold" as an unhot tap.

The description of a tap is "It's a tap marked [if the item described is hot]Hot[otherwise]Cold[end if][if the item described is not part of something], though not screwed into any sort of plumbing fixture at the moment[end if]."

The description of the tape is "It's clear tape, the kind you use to wrap presents or make childhood art projects. I made an entire paper village when I was small, at significant expense in sticky tape."

The description of the tar is "A pot of sticky black goo." The indefinite article of tar is "some".  Instead of tasting or eating the tar: say "Ugh." Instead of smelling the tar: say "It smells like roadworks on a hot day."

The tarpan is a neuter animal. The description is "A small, mouse-col[our]ed horse with black-barred legs. It looks wild and shows no signs of ever having been saddled or ridden."

Sanity-check mounting the tarpan:
	say "The tarpan isn't broken to bridle, and I don't think we're going to start today." instead.

The printed name of an alterna-tart is "tart". Understand "tart" as an alterna-tart. The description of an alterna-tart is usually "She's dressed to minimize coverage, but it's not completely clear whether she's an actual sex-worker or just a woman with a bold taste in clothing."

A tart is usually edible. The description of a tart is usually "It is sized for one, with a crisp crust on which sugar crystals glitter; through the indentations on the top [we] smell what might be gooseberry." The scent-description is "fresh pastry and gooseberries".

A tartar is a man. The description of the tartar is "A ferocious man in a fur cap, carrying eccentric weapons; he looks like a medley of popular depictions of Genghis Khan."
	The greeting of the tartar is "The tartar stares at us wild-eyed."
	The scent-description of the tartar is "Is that yak sweat I detect?"

The description of the tartrate is "A fine, just off-white powder. It doesn't smell like anything in particular." The heft of the tartrate is 1.

Sanity-check waving the tartrate:
	say "It's a powder, not a flag." instead.

Sanity-check climbing the tartrate:
	say "There would have to be a much bigger mound of the stuff before [we] could plausibly climb it." instead.

Sanity-check pushing, pulling, or turning the tartrate:
	say "At best [we] would just smear the stuff around." instead.

Rule for clarifying the parser's choice of the that-object: do nothing instead.

A tattle is r-abstract. The heft of a tattle is 0. The description of a tattle is "It is non-physical, but it sounds like a waspish playground voice recounting the sins of another child." The tattle is noisy.


Instead of listening to the tattle:
	say "[one of]A child's voice from the tattle says, 'Mom! Timmy took the R out of my rice!'[or]'Mommy, David put restoration gel on grandma but it didn't work,' tattles a childish voice.[or]'Daddy, Timmy turned my bread into a bead!' tattles a childish voice.[or]'Daddy, David says he's going to turn my cake into caca,' whimpers a small child.[or]An excited child's voice says, 'Freddy made an underage twine purchase.'[at random]".


The terracing is r-abstract. The description of the terracing is "It suggests a texture like the many-layered vinyards on the slopes of the Rhine. According to your recollection, anyway. I haven't been." The indefinite article of the terracing is "some".

The terse automaton is a push-button device. The heft of the terse automaton is 3. The description of the terse automaton is "Still an automatic seer in a glass box.".

Instead of switching on the terse automaton:
	say "'[one of]Outlook: OK[or]Don't worry[or]Be happy[or]Reply hazy[or]Rocks ahead[or]You: fine[at random],' announces the terse automaton."

The printed name of the that-object is "that". Understand "that" or "pronoun" or "demonstrative" or "object" or "object known as 'that'" as the that-object. The description of the that-object is "It's a curious little object, having no particular weight or size; whenever I look at it, I find myself concentrating on something else [--] like, say, at this moment, [the random marked-visible thing which is not the that-object]." The that-object is r-abstract.

The tea is an edible contained fluid thing. The scent-description is "[one of]bergamot[or]smoky Lapsang[or]toasted rice and green tea[sticky random]". The description of the tea is "It manifested steaming hot." Understand "earl grey" or "earl gray" or "lapsang" or "souchong" or "green tea" or "toasted rice" as the tea. [I was very very tempted to put the tea in a vintage tea cup, as I spent part of the summer collecting such cups on eBay for a party, and I had in the process cluttered my brain with a lot of useless information about historical tea cup brands and patterns that clearly needed to go somewhere. But then I realized that it would be kind of a bother implementing the cup separately from the tea, and inconsistent with how other liquid manifestations usually work, and so I decided not to after all. The reader is invited to picture her choice of Aynsley, Paragon, Wedgewood, Royal Albert, Limoges, Lomonosov, etc. tea cup as she wishes.]

Check waving the letter-remover at the tea when the current setting of the letter-remover is "t":
	say "For a flickering moment, the letter-remover very nearly manages to conjure some No Tea in its place." instead.

The description of a tear is "It looks like any other droplet of water."

Instead of tasting a tear:
	say "It wouldn't survive the experience."

The description of an alterna-tear is "A piece of cloth with a hole torn right through it. Much easier to transport than the other kind of tear." The printed name of an alterna-tear is "tear". Understand "tear" as an alterna-tear.

[A fake-ten is r-abstract. The heft of a fake-ten is 0. The description of a fake-ten is usually "The concept of ten is here represented by [one of]the letter X[or]the digits 1 and 0[or]the digit A[or]an Olympic judge holding up a scorecard[at random]." Understand "ten" as a fake-ten. The printed name of a fake-ten is "ten".]

The description of a tent is usually "It's one of those round pup tents, made of [one of][primary-color][or][secondary-color][at random] rip-stop nylon, big enough to sleep one diagonal adult or two children. There's a window of netting mesh so that it's possible to see inside." The tent is usually transparent openable enterable. Understand "tent-singular" as a tent.

The printed name of the plural-tents is "tents". Understand "tents" as the plural-tents. The description of some plural-tents is usually "They're half a dozen identical A-line tents in olive-drab, looking like a temporary encampment from a 20th century war." A plural-tents is usually plural-named transparent openable enterable.

Rule for printing the plural name of plural-tents:
	say "groups of tents".

The description of the tepal is "It appears to be the petal from a tulip (or perhaps tulips don't have petals, properly speaking?). The col[our]ation is vivid and baroque, lush impenetrable black streaked with red, like a love note from Amsterdam."

The thick sari is wearable. It covers the torso-area. The description of the thick sari is "It's a fine Indian garment, made of vivid purple and silver wool, and thick enough to offer some protection from the cold."

The description of the tic is "It is manifested as a bit of reflective mylar that flashes and changes shape spasmodically. I can't look at it without wanting to blink and rub my eyes." The tic is r-abstract.

The tick is an insect. The description of the tick is "A flat, black, blood-sucking insect. It's also tiny, smaller than a match. Considering its size I'd be surprised if [we] manage to hold on to it for two minutes without losing it.". The heft of the tick is 1.

[The tick is a problem. It's technically an animal, but it shouldn't act like animals act because it's too small. In fact, it's so small that it's not realistic for the player to be able to hold onto it for long. Fortunately, though, the stick isn't an essential item in the game, so we can afford to lose it. This is one place where I have Alex really step in and take some action if the player doesn't do something: either we change the tick back into the sticky in a few moves, or we lose it.]

Tick-removal is a scene. Tick-removal begins when the tick is marked-visible.

Every turn during tick-removal:
	say "[one of]That is, of course, if it doesn't attach to our skin and give us Lyme disease[or]I guess what I'm trying to say here is that I don't want our very own pet tick[or]...Okay, sorry, there's something I just need to do here. Blame my phobias if you will[stopping]."

Tick-removal ends painfully when the time since tick-removal began is greater than 2 minutes. Tick-removal ends well when the tick is marked invisible.

Instead of putting the tick on something:
	say "If [we] set it down, it'll be lost for good."

Instead of inserting the tick into something:
	say "If [we] set it down, it'll be lost for good."

Instead of dropping the tick:
	say "If [we] set it down, it'll be lost for good."

When tick-removal ends painfully:
	if the restoration gel is marked-visible:
		try putting the restoration gel on the tick;
	otherwise:
		now the tick is nowhere;
		say "Whoops, I seem to have lost our tick! Oh well. Here's hoping it has a juicy and eventful life."

The description of the ticket is "It reads ADMIT ONE in large black letters."

Table of Ultratests (continued)
topic	stuff	setting
"coffer"	{ anagramming gun, bullets }	Private Solarium

Test coffer with "load gun / shoot cup" [holding the anagramming gun and the bullets in the Private Solarium.]

The tidy puce coffer is a closed openable container. The description is "It's a heavy, old-fashioned box with a peaked lid, lacquered all over in a shade somewhere between dark pink and dusty purple. It is very clean and precisely crafted."

The tilting-piano is an instrument. Understand "tilting" or "piano" as the tilting-piano. The printed name of the tilting-piano is "tilting piano". The heft of the tilting-piano is 5. The description is "The legs on one side of the piano are very much lower than on the other side, so that the keyboard can only be played by someone with a very unconventional posture. To make the object look all the more carnivalesque, it is painted white and studded with blue and white rhinestones."

Table of Ultratests (continued)
topic	stuff	setting
"paintings"	{ anagramming gun, bullets, paintings, tub }	Sensitive Equipment Testing Room

Test paintings with "autoupgrade  / load gun / wave s-remover at paintings / shoot painting / put giant pin in t-inserter / shoot giant pint / wave g-remover at pig tat inn / shoot pi tat inn / put pin in inserter / shoot titan pint / open tub / gel tint / shoot paintings / wave g-remover at sign / shoot sin" [holding the anagramming gun and the bullets and the paintings and the tub in the Sensitive Equipment Testing Room.]


The titan-pin is wearable. The description of the titan-pin is "The pin depicts two giant-like figures locked in a wrestling match." The printed name is "titan pin". Understand "titan" or "titan pin" as the titan-pin. Understand "pin" as the titan-pin when the pin is marked invisible.

The titan-pint is a container. The description of the titan-pint is "It's a beer stein on whose side are two giant-like figures locked in a wrestling match. It's as though some Greek vase painters moved to Bavaria when the retsina ran out." The printed name is "titan pint". Understand "titan" or "titan pint" as the titan-pint. Understand "pint" as the titan-pint when the pint is marked invisible.

The tier is r-abstract. The description of the tier is "It's represented as a certificate hon[our]ing a donor in the fifth 'tier of giving'."

The description of the till is "An old-fashioned cash register with separate keys for half- and quarter-cents: Atlantean currency inflated more slowly than most, and still had absurdly low numbers until we went over to the Euro. Both sides are molded with a graceful female figure holding a cornucopia of goods from land and sea." The till is an openable closed container.

[The description of the tills is "There is a whole heap of them, mostly old-style cash registers from before they were computerized, but also some more recent sorts, and even one of those clever modern ones that is just a prop for a tablet computer." The heft of the tills is 10.]

The tillage is r-abstract. The description of the tillage is "The fleeting images show acres of rolling farmland being worked over in preparation for planting. It looks like more wide-open space than we usually have in one place here on Atlantis. Perhaps the farmland pictured is French or even American."

The printed name of a tin-can is "tin". Rule for printing the plural name of a tin-can: say "tins". Understand "tin" as a tin-can. Understand "tins" as the plural of tin-can.

The description of a tin-can is "Since the label has come off, there is no knowing what might be inside. Might be squid rings (popular hereabouts, packed in olive oil). Might be pickled mushrooms. Might be dog food." The heft of a tin-can is 2. A tin-can is closed and openable.
	Instead of opening a tin-can:
		say "We don't have a tin-opener, and these are not the type where the top peels back."

A tint is r-abstract. The description of a tint is "A [one of][primary-modifier] shade of [primary-color][or][pastel-modifier] shade of [pastel-color][or][secondary-modifier] shade of [secondary-color][at random], as though someone had watercol[our]ed that bit of air."

The tip is r-abstract. The description of the tip is "The tip at the moment reads, '[one of]Foreign water may cause indigestion[or]Wear earplugs in tourist areas to avoid accidental dialect contact[or]Standing in the midst of a herd can be dangerous[or]Never g-remove gall[at random].'"

The tippet is a floppy wearable thing. The description of the tippet is "It's a very long black scarf-like garment, made of plain material. The alpha and omega of the New Church are embroidered just where they would appear over the heart of the wearer." The scent-description is "liturgical incense".

Sanity-check poking head through tippet:
	try wearing the tippet instead.

The description of the tiptoes is "These severed toes insist on balancing on the tips. The effect is frankly disturbing."

Sanity-check standing up on the tiptoes:
	say "[stand-on-tiptoe]" instead.

The description of the tiptoe is "A single severed toe balanced on its tip."

Sanity-check standing up on the tiptoe:
	say "[stand-on-tiptoe]" instead.

To say stand-on-tiptoe:
	say "Going around on tiptoe is only stealthy in movies. And that's assuming you're using your own tiptoes and not someone else's."

The tiptop is r-abstract. The description of the tiptop is "At the moment it resembles [one of]the weathervane at the top of a spire[or]the topmost point of a star from a Christmas tree[or]the capstone of a pyramid[at random]."

The description of the tit is "A small, harmless-looking bird." A tit is naughty-sounding. The heft of a tit is 2.

The description of the alterna-tit is "It is indeed a severed breast. Maybe a C-cup, creamy-skinned and all, but the fact that it looks like it's been raggedly chopped off by a serial killer really decreases the appeal."
The printed name of the alterna-tit is "tit".

The titan of diarists is a man. The description is "He looks vaguely, non-specifically reminiscent of Pepys. At any rate, the kind of guy who would have gone through the London Fire."


The titer is a contained fluid thing. The description of the titer is "The most dilute detectable solution of some virus. I doubt it can be too dangerous."

The titter is r-abstract and noisy. The description of the titter is "It's just about invisible except as a ripple in the air, but it sounds like a girl laughing behind her hand. Repeatedly."

Instead of listening to the titter:
	say "The titter says, '[titter-noise]!'"

[There's really no need for the randomized laughter here, but then again, why not? It's in keeping with lots of other elements in the game.]

To say titter-noise:
	let sound be "[one of]hee[or]he[or]hu[at random]";
	let N be a random number from 2 to 4;
	repeat with index running from 1 to N:
		now sound is "[sound][sound]";
	replace character number 1 in sound with "t";
	say "[sound]" in sentence case.

Test titter with "z / z / listen to titter / g / g / g" holding the titter.

A TNT is a kind of thing. The indefinite article is "some". The description of a TNT is usually "It is almost comically dangerous, like something out of a cartoon. Fuses stick out of the tubes, ready for lighting."

Instead of burning the TNT:
	say "No, thanks, I'd rather not explode us both."

The toasts are an edible thing. The description of the toasts is "Two slices, one white, one whole-wheat. Both are brown around the edges and have been buttered lightly."
	The scent-description is "butter and warm bread".

The description of the toe is "It looks like a little toe raggedly cut off with a bread knife. It wasn't, of course." The heft of the toe is 1. The scent-description of the toe is "inner shoe".

The description of the toes is "It looks like a set of toes raggedly cut off with a bread knife. They weren't, of course." The heft of the toes is 1. The scent-description of the toes is "inner shoe". The toes are plural-named.

To say body-part-prank:
	say "[one of] (because after all it would be beyond the parameters of the change to generate the person whose body part it is). [They] [are] only moderately gory, and most locals get used to seeing this kind of thing as a prank from six-year-olds every halloween. Still, [they] might startle a tourist[or][stopping]".

The toe-ring is a wearable thing. The printed name is "toe ring". The description of the toe-ring is "A silver toe ring stamped with a celtic pattern."

The toil is r-abstract. The description of toil is "An utterly typical Atlantean image of an older man sweating in the sun and brushing sweat out from under his hat as he sees to the olive harvest. It could be taking place any time in the last century."

[It was, unsurprisingly, Graham who suggested TS Eliot as an alternate anagram for toilets/litotes.]

[The description of the toilets-collective is "Enough toilets to stock a public restroom for a thea[ter] or a small sporting venue[one of]. And here I was hoping we'd get T.S. Eliot.[or].[stopping]". The heft of the toilets-collective is 10. The toilets-collective is plural-named. The toilets-collective is a toilet.]

The description of the toll is "It's a list of prices for taking the high road across to Maiana. Different prices pertain depending on whether the toll-payer is driving a horse-cart or a donkey-cart."

The tom is a bird. The heft of the tom is 3. The description of the tom is "It's a boy turkey. At least, I assume it must be: I don't know how to sex a turkey. But it corresponds to the name."

[Guernsey tomato wine is a real thing. I quote from a BBC article:

"He explained that they called their wine Aztecato after the Aztecs who it is believed first cultivated the crop, but he did admit that his brew "didn't taste too good", but that did not stop them selling all 250 gallons."]

The tomato sauterne is a contained edible fluid thing. The heft of the tomato sauterne is 2. The description of the tomato sauterne is "The clear glass bottle contains a dessert wine [--] not the golden col[our] one might expect of a sauterne, but virulently red. Curly Edwardian script on the label declares the wine to be a tomato sauterne, vintage 2003, based on 'a unique recipe from the Channel Islands.' It also describes the winery as 'the Château d'Yquem of Guernsey.'"
	The scent-description is "sticky-sweet marinara sauce".

Some toms are a bird. The heft of the toms is 8. The description of the toms is "A whole collection of male turkeys."

The tomcat is a cat. The printed name is "tomcat". The heft of the tomcat is 3. The description of the tomcat is "It's angry and feral and looks like it is ancestrally part bobcat or something else mad and wild."

Some tomcats are a cat. The heft of the tomcats is 8. The printed name is "tomcats". The description of the tomcats is "Wrathful, hissing, un-herd-able. They don't like one another and they don't like us. Let's get rid of them as soon as possible."


The description of the tome is "Extremely dusty, learned, and virtuous, such that I almost fall asleep just looking at it. It's nothing so ribald as the Bible, or even a hymnal or order of service: it appears rather to be a selection of sermons of the 18th century, from a period when it was considered somehow exciting to sit through a three-hour discourse on theology."

The description of the tomtit is "A small bird with some of its markings in black. I don't claim to be expert in these things, and neither, it seems, are you." The tomtit is a bird.

[The description of the ton is "A big block of concrete. A fatal big block of concrete, as it turns out." The heft of the ton is 12.]

The top-toy is privately-named. The printed name of the top-toy is "top". Understand "top" or "toy" as the top-toy. The description of the top-toy is "One of those painted wooden tops given to small children. The plants and animals on the side are typically Atlantean: an Atlantean miniature donkey, a squid, an olive tree, and a humble chard plant. In fact, each of these is subscribed with a word; these are, respectively, PERSISTENCE, INVENTION, FRUITFULNESS, and RESOURCE."

Instead of turning the top-toy:
	say "[We] give the top a spin, and it whirls until the virtuous plants and animals blur together into a ribbon of green and grey. The [one of]donkey[or]squid[or]olive[or]chard[purely at random] lands face-up."

The torc is a wearable thing. The description of the torc is "It's a very heavy ring of precious metal, open at the front, so that it could just about be worn around the neck. The metal is worked in a pattern of braids and Celtic knots, and the ends of the torc are shaped like the heads of dragons." The heft of the torc is 3.

The tort is an r-abstract thing. The heft of the tort is 0. The description of the tort is "It shows a tiny image that, after enough squinting, I recogn[ize] as Tortious Linguistic Interference: a tiny man is converting his tiny neighbor's [one of]lawn into a local area network[or]house into a hose[at random]."

[More like Sacher torte than other kinds, probably.]

The torte is an edible thing. The description of the torte is "It is a thickly iced chocolate torte with a fine dusting of confectioner's sugar and layered with apricot jam." The heft of a torte is 1. The scent-description of the torte is "cocoa and apricots".

The heft of a tot is 3. The scent-description of a tot is "carbolic soap". The description of a tot is "An angelic little girl with big blue eyes and golden curls. She looks as though she was recently rescued from a Victorian orphanage by a kindly gentleman of means."
	The greeting of a tot is "'Hewwo thew,' lisps the tot[if the player carries an edible thing]. Her eyes fix hopefully on [the random edible thing carried by the player][end if]."

[The normal response to pushing people in some direction says that only very small children can be manipulated that way; but of course, the tot *is* very small…]
Sanity-check pushing the tot to a direction:
	say "[We] give them an encouraging push; the girl starts to whimper. Perhaps [we] should lead rather than herd." instead.

Sanity-check washing the tot:
	say "I know you think on general principles that all small children are by definition sticky and need a bath, but this one seems to have manifested in a treacle-free condition." instead.

Some plural-tots are people. The printed name of the plural-tots is "tots". Understand "tots" as the plural-tots. The heft of the plural-tots is 3. The scent-description of the plural-tots is "carbolic soap". The description of the plural-tots is "An angelic boy and girl, looking like an illustration for Hansel and Gretel. They are wide-eyed and very small, and don't actually look capable of any independent thought. Sometimes one sucks its thumb."

Sanity-check pushing the plural-tots to a direction:
	say "[We] give them an encouraging push; the girl starts to whimper. Maybe they're more inclined to follow than to be shoved?" instead.

Sanity-check washing the plural-tots:
	say "I know you think on general principles that all small children are by definition sticky and need a bath, but these seem to have manifested in a treacle-free condition." instead.

 The printed name of a tote-bag is "tote". Understand "bag" or "tote" as a tote-bag. The description of a tote-bag is "A two-handled sack for shopping purposes. It's made of sturdy canvas and looks resistant to stains."

Some totes are a container. The description of the totes is "A selection of shopping sacks. They are made of sturdy canvas and look resistant to stains."

The description of the totem is "A raven, carved in wood and painted in red and black."

The description of the totems is "A series of animals, carved in wood. The carving style is vaguely northwestern American, but it seems a bit slapdash [--] I suspect because it's a culture not well known here."

The description of the towering chute is "This is the lower end of a tremendous chute, perhaps from a fun-fair slide or a waterpark. The top of the chute is not clearly visible."

Instead of entering the chute:
	say "Down here we're at the bottom end of the chute, so it's not as though we could slide anywhere on it."

The description of the toy rug is "It is a tiny but intricately woven oriental rug suitable for an expensive doll house." The toy rug is floppy. The heft of the toy rug is 1.

The track is long and strong. The description of the track is "It's a piece of the old tramcar track that used to run up Long Street from the docks. The original has mostly been pulled up now, of course, but there are still a few bits in the street. This length is about a half [if the player wears the britishizing goggles]metre[otherwise]meter[end if] long. And heavy." The heft of the track is 3.

The description of a tram is "A toy model of one of the old-style trams that used to putt up Long Street from the docks back in the 20s and 30s. It is olive-col[our]ed and very detailed, with wood flooring and sideboards, and carries a tiny advertisement for ladies['] hair rinse on the side." The heft of a tram is 1.

The description of the tramcar is "One of the old-style trams that used to putt up Long Street from the docks back in the 20s and 30s. This one is full-size. It is olive-col[our]ed, with wood flooring and sideboards, and carries an advertisement for ladies['] hair rinse on the side.". The heft of the tramcar is 8. The tramcar is an enterable container.

The trampet is an enterable portable supporter. The description of the trampet is "It's a very small trampoline used for therapeutic exercises." The trampet allows seated and standing.
	The trampet has a number called the jump count.

Instead of jumping when the player is on the trampet:
	if there is a thing which is not the player on the trampet and a random chance of 1 in 3 succeeds:
		let random-thing be a random thing which is not the player on the trampet;
		say "[The random-thing] [bounce] off the trampet.[paragraph break]";
		move random-thing to the holder of the trampet;
	say "[one of]We get a pretty good height this way. [or][stopping]";
	increase the jump count of the trampet by 1;
	say "B";
	repeat with index running from 1 to the jump count of the trampet:
		say "o";
	repeat with index running from 1 to the jump count of the trampet:
		say "i";
	repeat with index running from 1 to the jump count of the trampet:
		say "n";
	say "g!".

Before getting off the trampet:
	now the jump count of the trampet is 0.
Before exiting when the player is on the trampet:
	now the jump count of the trampet is 0.

Test trampet with "tutorial off / drop trampet / stand on trampet / jump / jump / jump / out / jump / stand on trampet / jump / jump / jump / jump / get off trampet / get on trampet / jump" holding the trampet.

The description of the traveller tips is "It's a pamphlet of suggestions for traveling in Atlantis. It recommends such standard precautions as having your name analyzed for possible weaknesses, leaving a photocopy of your passport with your embassy, and ridding your luggage of anything purchased in a non-English-speaking country."

The description of a traveller tip is "Now just a single piece of paper suggesting that one look at State Department reports on Atlantis to see whether the current political unrest is especially severe."


The description of the trap is "If you were expecting something small and harmless like a mousetrap, you're wrong: this is big and iron and would do a respectable job on a bear. [if open]Currently the jaws are forced apart, ready to spring on the unwary[otherwise]The jaws of the trap are closed, at least[end if]." The trap can be openable. The trap can be closed. The trap can be open. The trap is openable and closed. Understand "jaws" as the trap.

Understand "set [trap]" as opening.

Sanity-check opening the trap:
	if the trap is open:
		make no decision;
	if the trap is not in location:
		say "It would be difficult, not to mention unsafe, to try to set the trap when it's anywhere but on the [ground]." instead;
	if Boar Attack is happening:
		say "[one of][We] briefly struggle to get the trap open, but the boar goes straight for [us] and [we] have to run[or][We] [are] not going to try that again[stopping]." instead;
	if the location is nautical:
		say "In this cramped space, one of our friends is going to step on the trap, if [we] don't do it first." instead.

Report opening the trap:
	say "[We] apply a great deal of pressure to the levers of the trap and finally manage to get the jaws open";
	if Atlantida-woman is in location:
		say ".[paragraph break]'Did you really expect me to walk into that?' Atlantida asks, with genuine surprise in her voice." instead;
	if further guards is happening or guard-imminence is happening or portcullis-threat is happening or atlantida-refreshed is in location:
		say ". I really don't want to think about what will happen if somebody steps on it";
	if atlantida-refreshed is in location:
		say ".[paragraph break]'That seems brutal,' Atlantida remarks";
	say "." instead.

Sanity-check taking the open trap:
	say "[We] don't want to get our hand anywhere near the trap while it's still set to spring." instead.

Sanity-check pushing or pulling or turning the open trap:
	say "[We] don't want to get our hand anywhere near the trap while it's still set to spring." instead.

Sanity-check closing the trap:
	say "It's not something one can just casually close back up. [We][']d have to spring it with something, preferably not one of our own limbs." instead.

Understand "spring [something]" as springing it with. Understand "spring [something] with [something]" as springing it with. Understand "spring [trap] with [a long thing]" as springing it with. Springing it with is an action applying to one thing and one carried thing.

Understand "poke [something] with [something]" as attacking it with. Understand "prod [something] with [something]" as attacking it with.

Instead of inserting something into the trap:
	try springing the trap with the noun.

Rule for supplying a missing second noun while springing something with:
	if the player carries a long strong thing (called means):
		now the second noun is the means;
	otherwise:
		say "[We] would need something long and sturdy to spring it with." instead.

Check springing something with something:
	if the noun is not the trap:
		say "[The noun] [are] not susceptible to springing." instead;
	if the trap is not open:
		say "[The noun] [are] closed already." instead;
	if the second noun is the trap:
		say "We need some second object to spring the trap with." instead;
	if the second noun is not long:
		say "[The second noun] [are] not long enough to provide protective distance." instead;
	if the second noun is floppy:
		say "[The second noun] [are] too floppy to apply the necessary pressure at a distance." instead.

Carry out springing the trap with something:
	now the second noun is nowhere;
	now the trap is closed.

Report springing the trap with something:
	say "[We] prod [the trap] with [the second noun]. The trap snaps violently shut, shattering [the second noun]." instead.

Sanity-check attacking the trap with something:
	try springing the trap with the second noun instead.

Understand "step into/in [thing]" as entering.
Understand "step on/onto [thing]" as standing up on.

Instead of standing up on the trap:
	try entering the trap.

Instead of entering the open trap:
	say "Even you don't have the self-control [--] or the suicidal inclination [--] necessary to break your own leg with forty pounds worth of cold iron bear trap."

Test trap with "open the trap / spring the trap / set the trap / drop the trap / set the trap / enter the trap / put stick in trap" holding the trap and the stick.

The description of the trash is "[one of][card-intro][or]A collection of random bits [--] sweet wrappers, tissues, bus tickets. Nothing of any value[stopping]." Understand "sweet" or "wrappers" or "bus" or "tickets" or "tissues" as the trash. The indefinite article of the trash is "some".

The appointment card is a thing. The heft of the appointment card is 1. The description of the appointment card is "It's a card from Arbot Maps & Antiques: FINE GOODS BY APPOINTMENT ONLY. The time scribbled at the bottom is 9 AM this morning. Looks like Brock's handwriting. There's also a tiny heart drawn in the corner." The scent-description of the appointment card is "mingled lavender and ash".

A description-concealing rule when Kate carries the appointment card:
	now the appointment card is not marked for listing.

Instead of turning or searching or looking under the appointment card:
	say "The reverse of the card is completely blank."

Instead of frowning at the appointment card:
	say "We contemplate the card, but it doesn't reveal its secrets any faster than before."

To say card-intro:
	assign "Trace Brock's movements at the antique shop";
	complete "Check the dead drop at the public convenience";
	record "visiting the dead drop" as achieved;
	move the appointment card to the player;
	say "Most of it's junk: sweet wrappers, tissues, bus tickets. But there's also, suggestively, an appointment card for Arbot Maps & Antiques".

The trig is r-abstract. The description of the trig is "A constantly moving figure of circles, triangles, sine and cosine graphs."

The description of the trigram is "It's a bone tile representing [one of]earth[or]mountain[or]water[or]wind[or]thunder[or]fire[or]swamp[or]heaven[at random]."

The triple varlet is plural-named. The triple varlet is a man. The description of the triple varlet is "Three inseparable page-boys, all with sneering, dishonest faces. They would plainly as soon rob you as look at you."

Check waving the letter-remover at triple varlet when the current setting of the letter-remover is "r":
	say "Nice try, but a drinks-trolley-pushing TIPPLE VALET would need another P." instead.

The trill is an r-abstract noisy thing. The heft of the trill is 0. The description of the trill is "It sounds like an opera singer got stuck on repeat."

Instead of listening to the trill: say "The trill keeps on trilling."

The troll is a man.
The initial appearance is "The troll [one of]grunts[or]looks at [us] with an unpleasantly hungry expression[or]picks its nose[or]pounds one of its meaty fists into the opposite palm[or]looks around for something to smash[at random]."
The description of the troll is "Grey skin, yellow teeth, green eyes. Legs as big around as tree trunks. A pot belly, speaking of a great deal of food eaten raw."
The greeting of the troll is "The troll grunts."

[A lightweight nod to tv tropes, focusing on the artifact section, both because it places us very early in the count and because it seemed like a suitable place to start for this game.]
The trope count is an r-abstract thing. The description of the trope count is "The count is currently only up to '[one of]applied phlebotinum[or]amulet of concentrated awesome[or]amulet of dependency[or]amplifier artifact[or]antimatter[or]artifact of death[or]artifact of doom[or]artifact of attraction[cycling].' This could take a while."

The truck is a car. The heft of the truck is 6. The description is "The cab of the truck is beaten and rusted, and the flatbed bears the stains of many years['] solo farming." Understand "cab" or "flatbed" or "car" as the truck. The flexible appearance of the truck is "blank". The introduction of the truck is "".

The tub is an essential closed openable container. The description is "Now a handsome, giant-sized tub with RESTORATION GEL prominently emblazoned on the front." A restoration gel is in the tub. The description of the restoration gel is "The [holder of the restoration gel] contains a clear, sticky gel that restores objects to their original state, before any letter changing. This is a valuable item in your line of work." The indefinite article is "some".

Instead of smelling the tub, try smelling the restoration gel.

Some tubas are an instrument. The heft of the tubas is 5. The description of the tubas is "A shiny, cacophonous mass."

Instead of listening to the tubas:
	say "Mercifully, they didn't materialize with a complement of tuba players, so there's nothing to hear." instead.

A tuba is an instrument. The heft of the tuba is 2. The description of the tuba is "I associate instruments like this entirely with band practice misery."

The tuck is r-abstract. The description of a tuck is "A neatly-stitched fold in a piece of cloth."

[The heft of the tun is 12. The description of the tun is "It's a huge vat for wine, about 250 gallons worth: the equivalent of three puncheons or four hogsheads or 14 rundlets."]

The description of two-added-nutmegs is "The nutmegs do somehow convey an air of superfluity. They're embarrassed. They've made their dish too spicy. They feel ashamed." The printed name of two-added-nutmegs is "two added nutmegs". Understand "two" or "added" or "nutmegs" or "spice" as the two-added-nutmegs.


Section 18 - U through Z

The untalented naval polecat is a neuter animal. Understand "weasel" or "uniform" or "admiralty" as the untalented naval polecat. The description is "It looks a lot like a weasel wearing an admiralty uniform."

Test polecat with "shoot needle" holding the anagramming gun and the catalan punt volat needle.

The wand batch is long and strong. The description is "The batch is a set of half a dozen wooden wands, bound together."

Instead of waving the wand batch:
	say "Glittery [one of][metallic-color][or][secondary-color][at random] sparks fly from the ends of the wands, but they don't otherwise do anything."

The warm rods are long and strong. The description of the warm rods is "Lengths of rebar, warm as though left out in the noonday sun."

[The heft of the war-object is 20. The printed name of the war-object is "war". Understand "war" as the war-object. The description of the war-object is "I suppose we were hoping for some abstract conceptual representation, but what we get is loud noises and screaming and a yellowish gas all around, and something screaming down from the sky".]

The description of the warm rod is "Heavy, metal, slightly warm. Not radioactive, I hope?". The warm rod is strong and long.

The description of the warp is "It's a small hand-loom with the warp thread put on but none of the weaving actually completed yet. There is no string or yarn to make the weft."

[The description of the wart-object is "Disconnected from any body, it is just a bulb of soft brownish skin-flesh with a couple of hairs sticking out." The heft of the wart-object is 1. The printed name of the wart-object is "wart". Understand "wart" as the wart-object.]

Sanity-check writing the topic understood on the wart recipe:
	say "There's barely any blank space remaining." instead.

The description of the wart recipe is "This scrap of paper with its old-fashioned, grandmotherly writing offers a folk treatment for wart removal, which involves copious amounts of apple cider vinegar, salt, lemon slices, and thinly cut banana peel."

The watchword is r-abstract. The description is "At the moment when [we] look at it, the watchword is apparently '[one of]Change[or]Experience[or]Quality, not Quantity[or]Haste Makes Waste[at random].'".

The description of the watchband is "Because it doesn't have a watch face to go in it, this isn't even wearable: it's just two strips of leather attached by a buckle, and points at each end to attach to the watch face."

Sanity-check poking head through the watchband:
	say "We would have to be very pin-headed indeed." instead.

Sanity-check opening or closing or wearing the watchband:
	say "Because there's no watch face to hold the straps together, it's impossible to wear the watchband, and pointless to do or undo the buckle." instead.

The description of the weight is "Shaped like a headless pyramid. '0.5 kg' is stamped in the top together with the seal of the Bureau. It's an official weight, intended to reassure citizens that they were getting a genuine standard measure."

The wet epigraph is r-abstract. The heft is 0. The description is "Currently it reads, '[one of]Unshakable faith is faith that has been shaken[or]Anyone who thinks the sky is the limit, has limited imagination[at random].'"

The wig is wearable and floppy. It covers the head-area.  The description is "A surprisingly realistic wig, cut to about the shoulder. It looks like our hair, but a good bit longer."
	The scent-description is "the faintest whiff of shampoo".


[There used to be a requirement about donning or doffing the wig or the hairpiece in private, which provided an additional purpose for the public convenience area. But this requirement confused lots of people, especially when the restriction prevented them from doing something else, like blindfolding themselves. I ultimately decided it was a bit of pseudo-realism that was way way way not worth it.]

The wight is a woman. The description is "She is wraithlike and slender, and from some angles she looks partially transparent. Her eyes are hollow and her hands tremble. Perhaps she is dangerous in some fashion, but at the moment she looks more like a war refugee."


The winner logo chocolate is edible. The description of the winner logo chocolate is "Just one waxy chocolate left from a box."

The description of the winner logo chocolates is "The box is branded with the label of Dental Consonants Limited. Handwriting script across the front says, 'A Gift... to You our Valued Employee.' Inside are tablets of cheap, waxy milk chocolate, each imprinted with the word WINNER." Understand "box" or "tablets" as the winner logo chocolates.
	The winner logo chocolates are edible.
	The scent-description of the winner logo chocolates is "nothing much, which is a bad sign in chocolate"

Instead of opening or closing the winner logo chocolates:
	say "The box isn't much use for anything, and I don't fancy the chocolates."

The description of the wire rack is "The new rack is miraculously much larger than the individual racks before, so it can still hold all the same merchandise." The wire rack is a scenery supporter.

The word is r-abstract. The heft of the word is 0. The description is "At the moment, the word is '[recent word]', floating about in [one of]handsome Garamond[or]Arno Pro[or]unmistakable Hoefler[or]slick Buivinga[or]elegant Didot[or]unimaginative Times[or]Helvetica[or]solid-bodied Gotham[at random] lettering."

To say recent word:
	let N be some text;
	now N is the substituted form of the description of a random marked-visible thing;
	if N is "":
		now N is description of the passage;
	let count be the number of words in N;
	let index be a random number between 1 and count;
	say word number index in N.

The words is r-abstract. The description is "[description of the wordage]". The heft of the words is 0.

The description of wordage is "A big lump of text that appears as though it's going to mean something, as long as you don't look too hard. The beginning reads, '[i][generictext]...[/i]'".

To say generictext:
	let N be a random number between 20 and 30;
	let assemblage be text;
	repeat with I running from 1 to N:
		let content be description of a random seen thing;
		if content is "":
			next;
		let max be the number of words in content;
		let word-choice be a random number between 1 and max;
		now assemblage is "[assemblage][word number word-choice in content] ";
		if word-choice is less than max and a random chance of 1 in 2 succeeds:
			increase word-choice by 1;
			now assemblage is "[assemblage][word number word-choice in content] ";
	now assemblage is assemblage in sentence case;
	say assemblage;


The clothing-wrap is a privately-named floppy wearable thing. The heft of the clothing-wrap is 1. The printed name is "wrap". Understand "wrap" as the clothing-wrap.
	The description is "Sheer silver fabric, here and there studded with little glittery rhinestones. It's meant to be worn around the upper arms with a fancy dress, I suppose. You'd know more about this than I do."

The description of a yam is "This particular one is as long as a baby's forearm and light brown."

Understand "yam cart" or "cart" as a yam-cart. The printed name of a yam-cart is "yam cart". The description of a yam-cart is "A cart made of metal and wood, designed for displaying goods at a farmer's market or similar fair. It is dedicated entirely to the purveyance of yams: large yams, small yams, white yams, purple yams, yams bearing an incidental resemblance to the head of Ronald Reagan and other yams that look a little bit like the Virgin Mary. Japanese mountain yams are available, as is a ground yam powder for making brown yam paste."

Rule for printing the plural name of yam-cart:
	say "yam carts".

The yams-collection is part of the yam-cart. The yams-collection is edible. Understand "yam" or "yams" or "large" or "small" or "white" or "purple" or "ronald reagan" or "virgin mary" or "japanese" or "mountain" or "ground" or "powder" or "brown" or "paste" or "collection of yams" or "collection" as the yams-collection. The printed name of the yams-collection is "collection of yams".
	The description is "I find the yam display a bit overwhelming, myself."

Sanity-check eating the yams-collection:
	say "Leaving aside my risky stomach just now, I understand that many kinds of yam will make you ill if not properly cooked." instead.

Sanity-check taking the yams-collection:
	say "There are far too many yams for us to carry, and no individual yam looks remotely useful." instead.

The description of the zen frond rinse is "This blue plastic bottle contains all that is necessary to maintain one's ferns in a state suitable for a Buddhist garden." The scent-description is "soapy chemicals".

[The heft of the zoo is 20. The description of the zoo is "Giraffes, bison, zebras, elephants, panthers, a polar bear: charismatic megafauna from every continent come tumbling past, and eventually overwhelm us."]


Repository Alphabetic Details ends here.
